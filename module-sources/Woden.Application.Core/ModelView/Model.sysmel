namespace Woden definition: {
namespace Application definition: {

useNamespace: Woden Runtime.
useNamespace: Stdn Graphics GUI.

#**
 * I am the model that lies underlying a Woden application.
 *#
class Model superclass: RenderingProcess; definition: {
    protected field engine type: EnginePtr.
    protected field views type: Stdn Collections Vector(View sharedPointer).

    const inline method engine => EnginePtr const ref
        := engine.

    virtual method initializeEngineWithCommandLine: (arguments: Char8 const pointer arraySlice) ::=> Boolean8 := {
        engine ifNotNil: {return: true}.

        let newEngine := Engine sharedNew.
        (newEngine _ initializeEngineWithCommandLine: arguments) ifFalse: {
            return: false.
        }.

        engine := newEngine.
        true
    }.

    virtual method initializeModel => Boolean8 := {
        true.
    }.

    virtual method parseCommandLine: (arguments: Char8 const pointer arraySlice) ::=> Boolean8 := {
        true
    }.

    virtual method initializeWithCommandLine: (arguments: Char8 const pointer arraySlice) ::=> Boolean8 := {
        (self parseCommandLine: arguments) &&
        (self initializeEngineWithCommandLine: arguments) &&
        self initializeModel
    }.

    virtual method createNewView => ViewPtr
        := View sharedNew.

    virtual method createNewSecondaryView => ViewPtr
        := View sharedNew.

    virtual method openApplication => Boolean8 := {
        self openView: self createNewView
    }.

    virtual method openView: (newView: ViewPtr const ref) ::=> Boolean8 := {
        newView isNotNil && (newView _ initializeWithModel: self asSharedPointer) ifFalse: {
            return: false.
        }.

        newView _ open ifFalse: {return: false}.
        views add: newView.
        views size = 1 ifTrue: {
            engine _ registerRenderingProcess: (self asWeakPointer upCastFor: RenderingProcess)
        }.
        true
    }.

    method viewDestroyed: (view: ViewPtr) ::=> Void := {
        views remove: view.
        views ifEmpty: {
            engine _ unregisterRenderingProcess: (self asWeakPointer upCastFor: RenderingProcess)
        }.
    }.

    virtual method runMainLoop ::=> Int32 := {
        engine ifNil: {
            return: 1
        }.

        engine _ runMainLoop
    }.

    virtual method shutdown ::=> Void := {
        engine ifNotNil: {
            engine _ shutdown.
            engine reset
        }.
    }.

    virtual method runMainLoopAndShutdown => Int32 := {
        let exitCode := self runMainLoop.
        self shutdown.
        exitCode
    }.

    override method onStartFrameTick: (event: TickEvent ref) ::=> Void := {
        views do: {:each :: Void |
            each _ onStartFrameTick: event.
        }
    }.

    override method prepareRendering => Void := {
        views do: {:each :: Void |
            each _ prepareRendering.
        }
    }.

    override method constructRenderingCommandList => Void := {
        views do: {:each :: Void |
            each _ constructRenderingCommandList.
        }
    }.

    override method commitSurfaces => Void := {
        views do: {:each :: Void |
            each _ commitSurfaces.
        }
    }.
}.

compileTime constant ModelPtr := Model sharedPointer

}. ## End of namespace Application
}. ## End of namespace Woden
