namespace Woden definition: {
namespace Application definition: {

useNamespace: Woden Runtime.

useNamespace: Std Graphics Core.
useNamespace: Std Graphics GUI.

#**
 * I am the model that lies underlying a Woden application.
 *#
class GenericViewWindow superclass: ViewWindow; definition: {
    protected field window type: WindowRef.
    protected field swapChain type: SwapChainRef.
    protected field currentSwapChainExtent type: UInt32x2.
    protected field displayRenderPass type: RenderPassRef.
    protected field isOutOfDate type: Boolean8.

    override method extent => UInt32x2
        := currentSwapChainExtent.

    override method validatePresentationSurfaces => Boolean8 := {
        window ifNotNil: {
            let newExtent := window extent.
            swapChain isNil || currentSwapChainExtent ~= newExtent || isOutOfDate ifTrue: {
                self createSwapChainWithExtent: newExtent
            }.
        }.

        swapChain isNotNil
    }.

    ## For displaying we only need a swap chain with the color buffer, by default.
    method createSwapChainWithExtent: (newExtent: UInt32x2) ::=> Void := {
        let device := self engine _ renderingDevice.
        let renderingSettings ref := self engine _ renderingSettings.
        window vsyncDisabled: renderingSettings vsyncEnabled not.

        let swapChainCreationInfo mutable := SwapChainCreationInfo()
            colorBufferFormat: self colorBufferFormat;
            depthStencilFormat: self depthStencilFormat;
            extent: newExtent;
            layerCount: (self isStereoRequested ifTrue: 2u ifFalse: 1u);
            bufferCount: FrameBufferingCount;
            yourself.

        let defaultCommandQueue := device getDefaultCommandQueue.
        swapChain := window createSwapChain: swapChainCreationInfo for: defaultCommandQueue.
        currentSwapChainExtent := newExtent.
        isOutOfDate := false.
    }.

    override method openForView: (theView: ViewPtr const ref) ::=> Boolean8 := {
        (super openForView: theView) ifFalse: {
            return: false.
        }.

        let renderingSettings ref := self engine _ renderingSettings.

        window := self engine _ windowSystem createWindow: (WindowCreationInfo()
			resizable: true;
			extent: (renderingSettings resolution castTo: Int32x2);
			title: "Test Window" asMutableString;
            fullscreen: renderingSettings fullscreen;
            animated: true;
            vsyncDisabled: renderingSettings vsyncEnabled not;
			yourself).
	    window ifNil: {
	        return: false.
	    }.

        window renderingDevice: self engine _ renderingDevice.

        ## Create the event handler.
        window eventHandler: (GenericViewEventHandler new
            viewWindow: self address;
            upCastFor: EventHandler).

        true
    }.

    override method getCurrentBackBufferLeftEye => FramebufferRef := {
        swapChain isNil || isOutOfDate ifTrue: {return: FramebufferRef nil}.

        swapChain getCurrentBackBufferForLayer: 0u
    }.

    override method getCurrentBackBufferRightEye => FramebufferRef := {
        swapChain isNil || isOutOfDate ifTrue: {return: FramebufferRef nil}.

        swapChain getCurrentBackBufferForLayer: 1u
    }.

    override method getDisplayRenderPass => RenderPassRef := {
        displayRenderPass ifNil: {
            let colorAttachments mutable := RenderPassColorAttachmentDescription array (
                RenderPassColorAttachmentDescription()
                    format: self colorBufferFormat;
                    beginAction: RenderPassAttachmentAction Clear;
                    endAction: RenderPassAttachmentAction Keep;
                    yourself
            ).
            let depthStencilAttachment mutable := RenderPassDepthStencilAttachmentDescription()
                format: self depthStencilFormat;
                beginAction: RenderPassAttachmentAction Clear;
                endAction: RenderPassAttachmentAction Keep;
                stencilBeginAction: RenderPassAttachmentAction Clear;
                stencilEndAction: RenderPassAttachmentAction Keep;
                clearValue: (DepthStencilValue()
                    depth: self defaultClearDepth;
                    stencil: self defaultClearStencil;
                    yourself)
                yourself.
            displayRenderPass := self engine _ renderingDevice createRenderPass: colorAttachments depthStencilAttachment:
                (depthStencilAttachment format = PixelFormat Unknown ifTrue: nil ifFalse: depthStencilAttachment address).
        }.
        displayRenderPass
    }.

    override method toggleFullscreen => Void
        := window toggleFullscreen.

    override method commitSurfaces => Void := {
        swapChain swapBuffers selectCase: #{
        SwapChainPresentationError OutOfDate : {
            isOutOfDate := true
        }.
        SwapChainPresentationError Suboptimal : {
            isOutOfDate := true
        }.
        SwapChainPresentationError Ok : {}.
        }
    }.

    override method close => Void := {
        window ifNil: {return: void}.
        (window eventHandler rcObjectAddress castTo: GenericViewEventHandler pointer) ifNotNil: {:eventHandler :: Void |
            eventHandler _ viewWindow := nil
        }.

        window close.
        window := nil.
        view _ viewWindowDestroyed.
    }.
}.

class GenericViewEventHandler superclass: EventHandler; definition: {
	public field viewWindow type: GenericViewWindow pointer.

    override method processEvent: (event: Event ref) ::=> Void := {
        super processEvent: event.

        viewWindow ifNotNil: {
            let view := viewWindow _ view.
            view ifNotNil: {
                view _ processEvent: event.
            }
        }.
    }.
}.

compileTime constant GenericViewWindowRef := GenericViewWindow rcPointer

}. ## End of namespace Application
}. ## End of namespace Woden
