namespace Woden definition: {
namespace Application definition: {

useNamespace: Woden Runtime.
useNamespace: Stdn Graphics Core.
useNamespace: Stdn Graphics GUI.
useNamespace: Stdn Math Geometry.

#**
 * I am the model that lies underlying a Woden application.
 *#
class SceneView superclass: View; definition: {
    protected field currentExtent type: UInt32x2.
    protected field currentBackbuffer type: FramebufferPtr.

    method sceneModel => SceneModel pointer
        := model getPointer castTo: SceneModel pointer.

    method scene => ScenePtr
        := self sceneModel _ scene.

    method camera => CameraPtr
        := self sceneModel _ mainCamera.

    protected field sceneRenderingPipelineViewport type: SceneRenderingPipelineViewportPtr.

    method ensureRenderingViewportWithExtent: (requiredExtent: UInt32x2) ::=> Void := {
        sceneRenderingPipelineViewport ifNil: {
            sceneRenderingPipelineViewport := self sceneModel _ sceneRenderingPipeline _ createSceneRenderingPipelineViewport.
        }.

        sceneRenderingPipelineViewport _ extent: requiredExtent.
        currentExtent := requiredExtent.
    }.

    virtual method prepareViewCamera => Void := {

    }.

    protected field framePreparationTime type: Float32.
    protected field commandListConstructionTime type: Float32.

    override method prepareRendering => Void := {
        viewWindow ifNil: {return: void}.

        framePreparationTime := (Stdn Chrono profileTimeToRun: {
            self ensureRenderingViewportWithExtent: viewWindow _ validatedExtent.
            currentBackbuffer := viewWindow _ getCurrentBackBuffer.
            self prepareViewCamera.
            sceneRenderingPipelineViewport _
                camera: self camera.
        }) castTo: Float32..
    }.

    override method constructRenderingCommandList => Void := {
        currentBackbuffer isNil || viewWindow isNil ifTrue: {return: void}.
        commandListConstructionTime := (Stdn Chrono profileTimeToRun: {
            sceneRenderingPipelineViewport _
                setCurrentBackBuffer: currentBackbuffer renderPass: viewWindow _ getDisplayRenderPass;
                setScene2D: (self buildOverlayScene2DWith: sceneRenderingPipelineViewport _ scene2DBuilder);
                constructAndEnqueueCommandList.
        }) castTo: Float32.
    }.

    override method commitSurfaces => Void := {
        currentBackbuffer isNil || viewWindow isNil ifTrue: {return: void}.
        viewWindow _ swapBuffers.
    }.

    private field defaultFontFaceWithSize type: FontFaceWithSizePtr.

    method defaultFontFaceWithSize => FontFaceWithSizePtr const ref := {
        defaultFontFaceWithSize ifNil: {
            let fontFace mutable := FontRegistry default defaultSans _ normalFace.
            defaultFontFaceWithSize := fontFace _ getOrCreateFaceWithSize: 12.
        }.

        defaultFontFaceWithSize
    }.

    virtual method buildOverlayFPSDisplayWith: (builder: Stdn Graphics Scene2D BuilderPtr const ref) ::=> Stdn Graphics Scene2D NodePtr := {
        let frameTimeString := Stdn String textStreamContents: {:out :: Void |
            out << "Preparation time " << (framePreparationTime * 1000.0f) << " ms "; nl.
            out << "3D CMDLIST time " << (commandListConstructionTime* 1000.0f) << " ms "; nl.
        }.

        (builder _ text: frameTimeString asArraySlice at: Float32x2(15.0f, 30.0f) font: self defaultFontFaceWithSize color: Float32x4 green) upCastFor: Stdn Graphics Scene2D Node
    }.

    virtual method buildOverlayScene2DWith: (builder: Stdn Graphics Scene2D BuilderPtr const ref) ::=> Stdn Graphics Scene2D NodePtr := {
        self buildOverlayFPSDisplayWith: builder
    }.
}.

compileTime constant SceneViewPtr := SceneView sharedPointer

}. ## End of namespace Application
}. ## End of namespace Woden
