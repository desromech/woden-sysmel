namespace Woden definition: {
namespace Application definition: {

useNamespace: Woden Runtime.
useNamespace: Stdn Graphics Core.
useNamespace: Stdn Graphics GUI.
useNamespace: Stdn Math Geometry.
useNamespace: Stdn Math LinearAlgebra.

#**
 * I am the model that lies underlying a Woden application.
 *#
class SceneView superclass: View; definition: {
    protected field currentExtent type: UInt32x2.
    protected field currentBackBufferLeftEye type: FramebufferPtr.
    protected field currentBackBufferRightEye type: FramebufferPtr.
    protected field stereoPresentationMode type: StereoPresentationMode.

    method sceneModel => SceneModel pointer
        := model getPointer castTo: SceneModel pointer.

    method scene => ScenePtr
        := isSecondaryView
            ifTrue: {self sceneModel _ secondaryScene}
            ifFalse: {self sceneModel _ scene}.

    method camera => CameraPtr
        := isSecondaryView
            ifTrue: {self sceneModel _ secondarySceneMainCamera}
            ifFalse: {self sceneModel _ mainCamera}.

    protected field sceneRenderingPipelineViewport type: SceneRenderingPipelineViewportPtr.

    method ensureRenderingViewportWithExtent: (requiredExtent: UInt32x2) ::=> Void := {
        sceneRenderingPipelineViewport ifNil: {
            sceneRenderingPipelineViewport := self sceneModel _ sceneRenderingPipeline _ createSceneRenderingPipelineViewport.
        }.

        ## Apply and sanitize the stereo presentation mode.
        stereoPresentationMode := self sceneModel _ stereoPresentationMode.
        stereoPresentationMode isMultiLayerSwapChain && viewWindow _ getCurrentBackBufferRightEye isNil ifTrue: {
            stereoPresentationMode := StereoPresentationMode Monoscopic.
        }.

        sceneRenderingPipelineViewport _
            stereoPresentationMode: stereoPresentationMode;
            extent: requiredExtent.
        currentExtent := requiredExtent.
    }.

    virtual method prepareViewCamera => Void := {

    }.

    override method newViewWindowInstance => ViewWindowPtr := {
        let result := self sceneModel _ sceneRenderingPipeline _ newViewWindowInstance.
        result ifNotNil: {
            return: (result downCastFor: ViewWindow)
        }.
        super newViewWindowInstance
    }.

    override method colorBufferFormat => PixelFormat
        := self sceneModel _ sceneRenderingPipeline _ swapChainColorBufferFormat.

    override method depthStencilFormat => PixelFormat
        := self sceneModel _ sceneRenderingPipeline _ swapChainDepthStencilBufferFormat.

    override method isStereoRequested => Boolean8
        := self sceneModel _ stereoPresentationMode isMultiLayerSwapChain.

    method stereoPresentationMode => StereoPresentationMode
        := stereoPresentationMode.

    protected field framePreparationTime type: Float32.
    protected field commandListConstructionTime type: Float32.
    protected field lastFrameVisibleObjectCount type: UIntPointer.
    protected field hasValidPresentationSurfaces type: Boolean8.

    override method prepareRendering => Void := {
        hasValidPresentationSurfaces := viewWindow isNotNil && viewWindow _ validatePresentationSurfaces.
        hasValidPresentationSurfaces ifFalse: {return: void}.

        framePreparationTime := (Stdn Chrono profileTimeToRun: {
            self ensureRenderingViewportWithExtent: viewWindow _ drawingSurfaceExtent.
            lastFrameVisibleObjectCount := sceneRenderingPipelineViewport _ getLastFrameVisibleObjectCount.
            currentBackBufferLeftEye := viewWindow _ getCurrentBackBufferLeftEye.
            currentBackBufferRightEye := viewWindow _ getCurrentBackBufferRightEye.
            self prepareViewCamera.
            sceneRenderingPipelineViewport _
                scene: self scene;
                camera: self camera;
                setScene2D: (self buildOverlayScene2DWith: sceneRenderingPipelineViewport _ scene2DBuilder);
                prepareRendering.
        }) castTo: Float32.
    }.

    override method constructRenderingCommandList => Void := {
        hasValidPresentationSurfaces ifFalse: {return: void}.

        commandListConstructionTime := (Stdn Chrono profileTimeToRun: {
            sceneRenderingPipelineViewport _
                setCurrentBackBufferLeftEye: currentBackBufferLeftEye rightEye: currentBackBufferRightEye renderPass: viewWindow _ getDisplayRenderPass;
                constructAndEnqueueCommandList.
        }) castTo: Float32.
    }.

    override method commitSurfaces => Void := {
        hasValidPresentationSurfaces ifFalse: {return: void}.

        viewWindow _ commitSurfaces.
    }.

    private field defaultFontFaceWithSize type: FontFaceWithSizePtr.

    method defaultFontFaceWithSize => FontFaceWithSizePtr const ref := {
        defaultFontFaceWithSize ifNil: {
            let fontFace mutable := FontRegistry default defaultSans _ normalFace.
            defaultFontFaceWithSize := fontFace _ getOrCreateFaceWithSize: 12.
        }.

        defaultFontFaceWithSize
    }.

    virtual method buildOverlayFPSDisplayWith: (builder: Stdn Graphics Scene2D BuilderPtr const ref) ::=> Stdn Graphics Scene2D NodePtr := {
        let frameTimeString := Stdn String textStreamContents: {:out :: Void |
            out << "Preparation time " << (framePreparationTime * 1000.0f) << " ms "; nl.
            out << "3D CMDLIST time " << (commandListConstructionTime* 1000.0f) << " ms "; nl.
            out << "Rendered objects: " << lastFrameVisibleObjectCount; nl.
        }.

        let result mutable := (builder _ text: frameTimeString asArraySlice at: Float32x2(10.0f, 10.0f) font: self defaultFontFaceWithSize color: Float32x4 green) upCastFor: Stdn Graphics Scene2D Node.
        result := (builder _ transform: (AffinePostTransform2dF32()
			setIdentity;
			scaleBy: currentExtent y / 480.0f;
			yourself) node: result) upCastFor: Stdn Graphics Scene2D Node.
        result
    }.

    virtual method buildOverlayScene2DWith: (builder: Stdn Graphics Scene2D BuilderPtr const ref) ::=> Stdn Graphics Scene2D NodePtr := {
        self buildOverlayFPSDisplayWith: builder
    }.
}.

compileTime constant SceneViewPtr := SceneView sharedPointer

}. ## End of namespace Application
}. ## End of namespace Woden
