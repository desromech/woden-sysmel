namespace Woden definition: {
namespace Application definition: {

useNamespace: Woden Runtime.

#**
 * I am the model that lies underlying a Woden application.
 *#
class SceneModel superclass: Model; definition: {
    protected field scene type: ScenePtr.
    protected field mainCamera type: CameraPtr.
    protected field sceneRenderingPipeline type: SceneRenderingPipelinePtr.

    const inline method scene => ScenePtr const ref
        := scene.

    const inline method mainCamera => CameraPtr const ref
        := mainCamera.

    override method initializeModel => Boolean8 := {
        scene := Scene sharedNew.
        true.
    }.

    override method createNewView => ViewPtr
        := SceneView sharedNew upCastFor: View.

    virtual method newScene => ScenePtr := {
        let newScene := Scene sharedNew.
        let newCamera := Camera sharedNew.

        mainCamera := newCamera.
        newScene _ add: (mainCamera upCastFor: SceneNode).

        scene := newScene.
        sceneRenderingPipeline ifNotNil: {
            sceneRenderingPipeline _ scene: scene
        }.
        newScene
    }.

    virtual method createSceneRenderingPipeline => SceneRenderingPipelinePtr
        := SceneRenderingPipelineRegistry uniqueInstance createDefaultFor: self engine.

    method sceneRenderingPipeline => SceneRenderingPipelinePtr const ref := {
        sceneRenderingPipeline ifNil: {
            sceneRenderingPipeline := self createSceneRenderingPipeline.
            sceneRenderingPipeline _ scene: scene.
        }.

        sceneRenderingPipeline
    }.

    override method needsToRender => Boolean8
        := true.

    override method prepareRendering => Void := {
        super prepareRendering.
        
    }.
}.

compileTime constant SceneModelPtr := SceneModel sharedPointer

}. ## End of namespace Application
}. ## End of namespace Woden
