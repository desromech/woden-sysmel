namespace Woden definition: {
namespace Assets definition: {

useNamespace: Std Serialization.
useNamespace: Std Graphics Core.
useNamespace: Std Math.

class WodenModelImporter definition: {
    field parentFolder type: Std FileReference.
    field modelAsset type: ModelAssetRef.

    method parseBuffer: (object: DOM Object const ref) ::=> Void := {
        let buffer := modelAsset newBuffer.
        buffer
            name: (object getString: "name" asMutableString default: "" asMutableString);
            dataSize: (object getUIntPointer: "dataSize" asMutableString).

        object at: "data" asMutableString ifPresent: {:(DOM Value const ref)dataValue :: Void |
            let content := DOM valueToByteArray(dataValue).
            Std assert: content size = buffer dataSize.

            buffer data reset: (Std malloc(content size) reinterpretCastTo: UInt8 pointer).
            Std memcpy(buffer data getPointer, content data, content size)
        } ifAbsent: {
            object at: "dataSize" asMutableString ifPresent: {:(DOM Value const ref)dataSizeValue :: Void |
                let dataSize := DOM valueToUIntPointer(dataSizeValue).
                buffer data reset: (Std zmalloc(dataSize) reinterpretCastTo: UInt8 pointer).
            } ifAbsent: {
                object at: "uri" asMutableString ifPresent: {:(DOM Value const ref)uriValue :: Void |
                    let uri := uriValue get: Std String.
                    let uriFileRef := parentFolder / uri asArraySlice.
                    uriFileRef exists ifTrue: {
                        let readStream := uriFileRef binaryUnbufferedReadStream.
                        readStream ifNil: {return: void}.

                        buffer data reset: (Std zmalloc(buffer dataSize) reinterpretCastTo: UInt8 pointer).
                        readStream _ read: buffer data getPointer size: buffer dataSize
                    }.
                }.
            }
        }
    }.

    method parseBuffers: (list: DOM List ref) ::=> Void := {
        list do: {:each :: Void |
            self parseBuffer: (each get: DOM ObjectPtr) _
        }.
    }.

    method readBinaryBufferData: (buffer: ModelAssetBinaryBufferRef const ref) from: (input: Std IO Stream ref) ::=> Void := {
        buffer dataSize > 0 ifTrue: {
            input read: buffer data getPointer size: buffer dataSize
        }
    }.

    method readBinaryBuffersDataFrom: (input: Std IO Stream ref) ::=> Void := {
        modelAsset buffers do: {:each :: Void |
            self readBinaryBufferData: each from: input
        }.
    }.

    method parseBufferView: (object: DOM Object const ref) ::=> Void := {
        modelAsset newBufferView
            name: (object getString: "name" asMutableString default: "" asMutableString);
            buffer: (modelAsset buffers[object getUIntPointer: "buffer" asMutableString]);
            offset: (object getUIntPointer: "offset" asMutableString default: 0);
            size: (object getUIntPointer: "size" asMutableString);
            stride: (object getUIntPointer: "stride" asMutableString default: 0);
            usage: ((object getUInt32: "usage" asMutableString default: 0) castTo: BufferUsageMode).
    }.

    method parseBufferViews: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseBufferView: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseAccessor: (object: DOM Object const ref) ::=> Void := {
        modelAsset newBufferAccessor
            name: (object getString: "name" asMutableString default: "" asMutableString);
            bufferView: (modelAsset bufferViews[object getUIntPointer: "bufferView" asMutableString]);
            offset: (object getUIntPointer: "offset" asMutableString default: 0);
            count: (object getUIntPointer: "count" asMutableString);
            minValue: (object getFloat64x4: "minValue" asMutableString default: 0);
            maxValue: (object getFloat64x4: "maxValue" asMutableString default: 0);
            format: ((object getUInt32: "format" asMutableString default: 0) castTo: PixelFormat);
            columnCount: (object getUInt32: "columnCount" asMutableString default: 0).
    }.

    method parseAccessors: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseAccessor: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseImage: (object: DOM Object const ref) ::=> Void := {
        let image := modelAsset newImage
            name: (object getString: "name" asMutableString default: "" asMutableString);
            uri: (object getString: "uri" asMutableString default: "" asMutableString);
            mimeType: (object getString: "mimeType" asMutableString default: "" asMutableString);
            usageMode: ((object getUInt32: "usageMode" asMutableString default: 0) castTo: ModelAssetImageUsageMode);
            yourself.
        object at: "bufferView" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            image bufferView: (modelAsset bufferViews[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseImages: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseImage: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseSampler: (object: DOM Object const ref) ::=> Void := {
        modelAsset newSampler
            name: (object getString: "name" asMutableString default: "" asMutableString)
    }.

    method parseSamplers: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseSampler: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseTexture: (object: DOM Object const ref) ::=> Void := {
        let texture := modelAsset newTexture
            name: (object getString: "name" asMutableString default: "" asMutableString);
            yourself.

        object at: "sampler" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            texture sampler: (modelAsset samplers[DOM valueToUIntPointer(value)])
        }.
        object at: "image" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            texture image: (modelAsset images[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseTextures: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseTexture: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseMetallicRoughnessMaterial: (object: DOM Object const ref) ::=> ModelAssetMaterialRef := {
        let material := modelAsset newMetallicRoughnessMaterial
            baseColorFactor: (object getFloat32x4: "baseColorFactor" asMutableString default: 1.0f);
            metallicFactor: (object getFloat32: "metallicFactor" asMutableString default: 1.0f);
            roughnessFactor: (object getFloat32: "roughnessFactor" asMutableString default: 1.0f);
            yourself.
        object at: "baseColorTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material baseColorTexture: modelAsset textures[DOM valueToUIntPointer(value)].
            material baseColorTexture setColorUsage
        }.
        object at: "metallicRoughnessTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material metallicRoughnessTexture: modelAsset textures[DOM valueToUIntPointer(value)].
            material metallicRoughnessTexture setLinearUsage
        }.

        material upCastFor: ModelAssetMaterial.
    }.

    method parseMaterial: (object: DOM Object const ref) ::=> Void := {
        let material mutable := ModelAssetMaterialRef nil.
        object at: "metallicRoughness" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material := self parseMetallicRoughnessMaterial: (value get: DOM ObjectPtr) _.
        }.

        material ifNil: {
            material := modelAsset newMetallicRoughnessMaterial upCastFor: ModelAssetMaterial.
        }.

        material
            name: (object getString: "name" asMutableString default: "" asMutableString);
            emissionFactor: (object getFloat32x3: "emissionFactor" asMutableString default: 0);
            doubleSided: (object getBoolean8: "doubleSided" asMutableString default: false);
            alphaCutoff: (object getFloat32: "alphaCutoff" asMutableString default: 0.5f);
            alphaMode: ((object getUInt32: "alphaMode" asMutableString default: 0) castTo: ModelAssetMaterialAlphaMode).

        object at: "occlusionTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material occlusionTexture: modelAsset textures[DOM valueToUIntPointer(value)].
            material occlusionTexture setLinearUsage
        }.

        object at: "emissionTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material emissionTexture: modelAsset textures[DOM valueToUIntPointer(value)].
            material emissionTexture setColorUsage
        }.

        object at: "normalTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material normalTexture: modelAsset textures[DOM valueToUIntPointer(value)].
            material normalTexture setNormalUsage
        }.
    }.

    method parseMaterials: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseMaterial: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseMeshPrimitiveAttributes: (list: DOM List const ref) ::=> ModelAssetMeshPrimitiveAttributesRef := {
        let attributeList mutable := ModelAssetMeshPrimitiveAttributeList().

        list do: {:(DOM Value const ref)attribute :: Void |
            let attributeObject ref := (attribute get: DOM ObjectPtr) _.
            let location := (attributeObject getUInt32: "location" asMutableString default: 0) castTo: VertexAttributeLocation.
            let accessor := modelAsset bufferAccessors[attributeObject getUInt32: "accessor" asMutableString default: 0].
            attributeList add: (ModelAssetMeshPrimitiveAttribute()
                accessor: accessor;
                location: location;
                yourself
            )
        }.

        modelAsset newMeshPrimitiveAttributes
            attributes: attributeList;
            yourself
    }.

    method parseMesh: (mesh: ModelAssetMeshRef const ref) primitive: (object: DOM Object const ref) ::=> Void := {

        let primitive := ModelAssetMeshPrimitive new.
        mesh primitives add: primitive.

        primitive
            attributes: (self parseMeshPrimitiveAttributes: (object getList: "attributes" asMutableString) _ );
            primitiveTopology: ((object getUInt32: "primitiveTopology" asMutableString default: 0) castTo: PrimitiveTopology).

        object at: "indices" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            primitive indices: (modelAsset bufferAccessors[DOM valueToUIntPointer(value)])
        }.

        object at: "material" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            primitive material: (modelAsset materials[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseMesh: (object: DOM Object const ref) ::=> Void := {
        let mesh := modelAsset newMesh
            name: (object getString: "name" asMutableString default: "" asMutableString);
            yourself.

        object at: "primitives" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            (value get: DOM ListPtr) _ do: {:each :: Void |
                self parseMesh: mesh primitive: (each get: DOM ObjectPtr) _
            }.
        }.
    }.

    method parseMeshes: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseMesh: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseSkeleton: (object: DOM Object const ref) ::=> Void := {
        let skeleton := modelAsset newSkeleton
            name: (object getString: "name" asMutableString default: "" asMutableString);
            yourself.

        object at: "inverseBindMatrices" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            skeleton inverseBindMatricesAccessor: modelAsset bufferAccessors[DOM valueToUIntPointer(value)]
        }.
    }.

    method parseSkeletons: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseSkeleton: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseSkeleton: (skeleton: ModelAssetSkeletonRef const ref) nodes:(object: DOM Object const ref) ::=> Void := {
        let joints := object getList: "joints" asMutableString.
        skeleton joints reserve: joints _ size.
        joints _ do: {:(DOM Value const ref)value :: Void |
            skeleton joints add: modelAsset nodes[DOM valueToUIntPointer(value)]
        }.

        object at: "rootJoint" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            skeleton rootJoint := modelAsset nodes[DOM valueToUIntPointer(value)]
        }.
    }.

    method parseSkeletonNodes: (list: DOM List const ref) ::=> Void := {
        list doWithIndex: {:each :index :: Void |
            self parseSkeleton: modelAsset skeletons[index] nodes: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseNode: (object: DOM Object const ref) ::=> Void := {
        let node := modelAsset newNode
            name: (object getString: "name" asMutableString default: "" asMutableString);
            isJoint: (object getBoolean8: "isJoint" asMutableString default: false);
            hasValidJointIndex: (object getBoolean8: "hasValidJointIndex" asMutableString default: false);
            jointIndex: (object getUIntPointer: "jointIndex" asMutableString default: 0);
            parentJointIndex: (object getIntPointer: "parentJointIndex" asMutableString default: -1);
            yourself.

        object at: "mesh" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            node mesh: (modelAsset meshes[DOM valueToUIntPointer(value)])
        }.

        object at: "skeleton" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            node skeleton: (modelAsset skeletons[DOM valueToUIntPointer(value)])
        }.

        object at: "scale" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let scale := DOM valueToFloat32x3(value, 1).
            node scale: scale.
            node transform scaleBy: scale.
        }.
        object at: "rotation" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let rotation := QuaternionF32 xyzw: DOM valueToFloat32x4(value, Float32x4(0, 0, 0, 1)).
            node rotationQuaternion: rotation.
            node transform applyOrthonormalMatrix: rotation asFloat32x3x3.
        }.
        object at: "translation" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let translation := DOM valueToFloat32x3(value, 0).
            node translation: translation.
            node transform translateBy: translation.
        }.

        object at: "matrix" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let matrix := DOM valueToFloat32x4x4(value, Float32x4x4 identity).
            Std stdout << "TODO: parse node matrix "; nl.
            Std stdout << matrix firstRow; nl.
            Std stdout << matrix secondRow; nl.
            Std stdout << matrix thirdRow; nl.
            Std stdout << matrix fourthRow; nl.
        }.

    }.

    method parseNode: (node: ModelAssetNodeRef const ref) children: (object: DOM Object const ref) ::=> Void := {
        object at: "children" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let children ref := (value get: DOM ListPtr) _.
            node children reserve: children size.
            0 until: children size do: {:i :: Void |
                node children add: (modelAsset nodes[children getUIntPointer: i])
            }
        }
    }.

    method parseNodes: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseNode: (each get: DOM ObjectPtr) _
        }.

        list doWithIndex: {:each :index :: Void |
            self parseNode: modelAsset nodes[index] children: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseScene: (object: DOM Object const ref) ::=> Void := {
        let scene := modelAsset newScene
            name: (object getString: "name" asMutableString default: "" asMutableString);
            yourself.

        object at: "rootNodes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let nodeList ref := (value get: DOM ListPtr) _.
            scene rootNodes reserve: nodeList size.
            0 until: nodeList size do: {:i :: Void |
                scene rootNodes add: (modelAsset nodes[nodeList getUIntPointer: i])
            }.
        }.
    }.

    method parseScenes: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseScene: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseAnimation: (animation: ModelAssetAnimationRef const ref) sampler: (object: DOM Object const ref) ::=> Void := {
        animation samplers add: (ModelAssetAnimationSampler new
            index: animation samplers size;
            inputAccessor: (modelAsset bufferAccessors[object getUIntPointer: "inputAccessor" asMutableString]);
            outputAccessor: (modelAsset bufferAccessors[object getUIntPointer: "outputAccessor" asMutableString]);
            interpolationMethod: ((object getUInt32: "interpolationMethod" asMutableString) castTo: ModelAssetAnimationInterpolationMethod);
            yourself).
    }.

    method parseAnimation: (animation: ModelAssetAnimationRef const ref) channel: (object: DOM Object const ref) ::=> Void := {
        let channel := ModelAssetAnimationChannel new
            sampler: (animation samplers[object getUIntPointer: "sampler" asMutableString]);
            targetProperty: ((object getUInt32: "targetProperty" asMutableString) castTo: ModelAssetAnimationTargetProperty);
            yourself.

        object at: "targetNode" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            channel targetNode: modelAsset nodes[DOM valueToUIntPointer(value)]
        }.

        animation channels add: channel.
    }.

    method parseAnimation: (object: DOM Object const ref) ::=> Void := {
        let animation := modelAsset newAnimation
            name: (object getString: "name" asMutableString default: "" asMutableString);
            yourself.

        let samplers := object getList: "samplers" asMutableString.
        animation samplers reserve: samplers _ size.
        samplers _ do: {:each :: Void |
            self parseAnimation: animation sampler: (each get: DOM ObjectPtr) _
        }.

        let channels := object getList: "channels" asMutableString.
        animation channels reserve: channels _ size.
        channels _ do: {:each :: Void |
            self parseAnimation: animation channel: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseAnimations: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseAnimation: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseTopLevel: (object: DOM Object const ref) ::=> ModelAssetRef := {
        modelAsset := ModelAsset new.
        modelAsset parentFolder: parentFolder.
        object at: "buffers" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseBuffers: (value get: DOM ListPtr) _
        }.
        object at: "bufferViews" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseBufferViews: (value get: DOM ListPtr) _
        }.
        object at: "accessors" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseAccessors: (value get: DOM ListPtr) _
        }.
        object at: "images" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseImages: (value get: DOM ListPtr) _
        }.
        object at: "samplers" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSamplers: (value get: DOM ListPtr) _
        }.
        object at: "textures" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseTextures: (value get: DOM ListPtr) _
        }.
        object at: "materials" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseMaterials: (value get: DOM ListPtr) _
        }.
        object at: "meshes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseMeshes: (value get: DOM ListPtr) _
        }.
        object at: "skeletons" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSkeletons: (value get: DOM ListPtr) _
        }.
        object at: "nodes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseNodes: (value get: DOM ListPtr) _
        }.
        object at: "skeletons" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSkeletonNodes: (value get: DOM ListPtr) _
        }.
        object at: "scenes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseScenes: (value get: DOM ListPtr) _
        }.
        object at: "animations" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseAnimations: (value get: DOM ListPtr) _
        }.

        modelAsset
    }.

    method importFromFileReference: (fileReference: Std FileReference const ref) ::=> ModelAssetRef := {
        parentFolder := fileReference parent.

        let inputReadStream mutable := fileReference binaryReadStream.
        inputReadStream ifNil: {return: ModelAssetRef nil}.

        let isReadingTextModel := fileReference extension asArraySlice = "wtmdl".
        let domValue := isReadingTextModel
            ifTrue: { JSON ValueParser parseStream: inputReadStream getPointer }
            ifFalse: { MessagePack ValueDecoder decodeStream: inputReadStream getPointer }.

        let result := self parseTopLevel: (domValue get: DOM ObjectPtr) _.
        isReadingTextModel ifFalse: {
            self readBinaryBuffersDataFrom: inputReadStream _
        }.

        result
    }.
}.

} ## End of namespace Assets
} ## End of namespace Woden
