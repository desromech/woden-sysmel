namespace Woden definition: {
namespace Application definition: {

useNamespace: Woden Runtime.
useNamespace: Std Graphics GUI.

class SampleSceneModel superclass: SceneModel; definition: {
    field animationScheduler type: AnimationSchedulerPtr.
    field accumulatedTime type: Float32.
    field updateTimeStep type: Float32.

    override method initialize => Void := {
        updateTimeStep := 1.0f / 60.0f.
    }.

    const inline method animationScheduler => AnimationSchedulerPtr const ref
        := animationScheduler.

    override method initializeModel => Boolean8 := {
        super initializeModel ifFalse: {return: false}.
        animationScheduler := AnimationScheduler rcNew.
        true.
    }.

    override method onStartFrameTick: (event: TickEvent ref) ::=> Void := {
        super onStartFrameTick: event.

        accumulatedTime := accumulatedTime + (event deltaSeconds castTo: Float32).
        while: accumulatedTime >= updateTimeStep do: {
            animationScheduler _ update: updateTimeStep.
            accumulatedTime := accumulatedTime - updateTimeStep
        }.

        ## Validate and interpolate the rendering state using the remaining delta.
        animationScheduler _ applyInterpolationForRendering: accumulatedTime / updateTimeStep.
    }.

    override method createNewView => ViewPtr
        := SampleSceneView rcNew upCastFor: View.
}.

}. ## End of namespace Application
}. ## End of namespace Woden
