namespace Woden definition: {
namespace GameSystem definition: {

useNamespace: Std Math Geometry.
useNamespace: Woden Runtime.
useNamespace: Woden MapDefinition.

class GlobalGameState superclass: Std RCObject; definition: {
    field engine type: EngineRef.
    field animationScheduler type: AnimationSchedulerRef.
    field activeSector type: SectorPtr.
    field player type: PlayerPtr.
    public field scene type: SceneRef.
    public field mainCamera type: CameraRef.

    method player => PlayerPtr const ref
        := player.

    method initializeWithEngine: (anEngine: EngineRef) ::=> Void := {
        engine := anEngine.
        animationScheduler := AnimationScheduler new.
    }.

    method update: (delta: Float32) ::=> Void := {
        animationScheduler update: delta.
        activeSector ifNotNil: {
            activeSector _ update: delta
        }.
    }.

    virtual method applyInterpolationForRendering: (alpha: Float32) ::=> Void := {
        animationScheduler applyInterpolationForRendering: alpha.
        activeSector ifNotNil: {
            activeSector _ applyInterpolationForRendering: alpha
        }.
    }.

    method loadSectorFromDefinition: (sectorDefinition: SectorDefinitionPtr const ref) ::=> Void := {
        let sector := Sector for: engine.
        sector _ loadFromDefinition: sectorDefinition.
        self activateSector: sector
    }.

    method activateSector: (sector: SectorPtr const ref) ::=> Void := {
        activeSector ifNotNil: {activeSector _ deactivateAndRemoveFromScene}.
        activeSector := sector.
        activeSector _ activateAndAddToScene: scene.
    }.

    virtual method startNewGame => Void := {
        player := self spawnPlayer.
    }.

    virtual method spawnPlayer => PlayerPtr := {
        activeSector ifNil: {return: PlayerPtr nil}.

        let startPosition := activeSector _ findPositionUsingMetadataEntityClassNamed: "PlayerStartPoint" default: Float32x3 zeros.
        let newPlayer := self newPlayerEntity.
        newPlayer _
            position: startPosition;
            postLoadInitialization.
        activeSector _ spawnEntity: (newPlayer upCastFor: Entity).
        newPlayer
    }.

    virtual method newPlayerEntity => PlayerPtr
        := Player rcNew.
}.

compileTime constant GlobalGameStatePtr := GlobalGameState rcPointer.

}. ## namespace GameSystem
}. ## namespace Woden
