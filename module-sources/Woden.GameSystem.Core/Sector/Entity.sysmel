namespace Woden definition: {
namespace GameSystem definition: {

useNamespace: Stdn Math.
useNamespace: Stdn Math Geometry.
useNamespace: Stdn Math LinearAlgebra.
useNamespace: Woden Runtime.
useNamespace: Woden MapDefinition.

class Entity superclass: Stdn SharedObject; definition: {
    public field parentSector type: Sector pointer.
    public field className type: Stdn String.
    public field name type: Stdn String.

    field orientation type: QuaternionF32.
    field transform type: ReversibleAffineTransform3dF32.
    field transformChanged type: Boolean8.

    public field localBoundingBox type: BoxF32.
    public field collisionObject type: CollisionObjectPtr.
    public field visualComponent type: VisualComponentPtr.
    public field sceneNode type: SceneNodePtr.

    meta definition: {
        macro method registerWithClassName: className
            := ``(global Factory mutable type: EntityFactory(SelfType, `,className))
    }.

    override method initialize => Void := {
        orientation := QuaternionF32 identity.
        transform := ReversibleAffineTransform3dF32 identity.
    }.

    const inline method isInSector2D => Boolean8
        := parentSector isNotNil && parentSector _ isSector2D.

    method boundingBox => BoxF32 :=
        localBoundingBox transformedWith: transform.

    method engine => EnginePtr
        := parentSector ifNil: {EnginePtr nil} ifNotNil: {parentSector _ engine}.

    method physicalSector => PhysicalSector pointer
        := parentSector
            ifNil: {nil}
            ifNotNil: {parentSector _ physicalSector getPointer}.

    virtual method loadFromDefinition: (definition: EntityDefinitionPtr const ref) fromLayer: (layerDefinition: SectorLayerDefinitionPtr const ref) ::=> Void := {
        className := definition _ className asArraySlice.
        name := definition _ name.
        self
            position: (definition _ positionForLayer: layerDefinition);
            orientation: definition _ orientation.
        localBoundingBox := definition _ localBoundingBox.
        localBoundingBox depth = 0 ifTrue: {
            localBoundingBox min z: layerDefinition _ layerMinZ.
            localBoundingBox max z: layerDefinition _ layerMaxZ.
        }.

        definition _ components do: {:each :: Void |
            each _ isCollisionShape ifTrue: {
                let shape := each downCastFor: CollisionShape.
                shape _ setupForLayer: layerDefinition.
                self buildCollisionObjectWithShape: shape

            }.

            each _ isVisualComponent ifTrue: {
                visualComponent := each downCastFor: VisualComponent.
                visualComponent _ setupForLayer: layerDefinition.
            }
        }.
    }.

    virtual method buildCollisionObjectWithShape: (collisionShape: CollisionShapePtr const ref) ::=> Void
        := {}.

    virtual method isMetadata => Boolean8
        := false.

    virtual method isAlive => Boolean8
        := true.

    virtual method needsTicking => Boolean8
        := false.

    virtual method spawn => Void := {

    }.

    virtual method prePhysicsTick: (delta: Float32) ::=> Void := {
        collisionObject _ position: self position.
    }.

    virtual method postPhysicsTick: (delta: Float32) ::=> Void := {
        self position: collisionObject _ position.
    }.

    virtual method onPhysicsCollisionHit: (collidingObject: CollisionObject ref) at: (collisionPoint: Float32x3) ::=> Void := {

    }.

    virtual method tick: (delta: Float32) ::=> Void := {

    }.

    virtual method receiveDamage: (damage: Float32) by: (damagingEntity: Entity ref) at: (damagePoint: Float32x3) ::=> Void := {

    }.

    virtual method destroyed => Void := {
        self removeFromScene.
        self removeFromPhysicalSector.
    }.

    virtual method removeFromScene => Void := {
        sceneNode ifNil: {return: void}.
        sceneNode _ removeFromScene.
    }.

    virtual method addToScene: (scene: ScenePtr const ref) ::=> Void := {
        sceneNode ifNil: {self createSceneNode}.
        sceneNode ifNotNil: {
            scene _ add: sceneNode
        }
    }.

    virtual method addToPhysicalSector: (physicalSector: PhysicalSectorPtr const ref) ::=> Void := {
        collisionObject ifNotNil: {
            physicalSector _ addCollisionObject: collisionObject
        }
    }.

    virtual method removeFromPhysicalSector => Void := {
        collisionObject ifNotNil: {
            collisionObject _ removeFromPhysicalSector
        }
    }.

    virtual method createSceneNode ::=> Void := {
        visualComponent ifNil: {return: void}.
        sceneNode := visualComponent _ createSceneNodeFor: self engine parentTransform: transform.
        self transformChanged.
    }.

    const inline method position => Float32x3
        := transform translation.

    inline method position: (aPosition: Float32x3) ::=> Void := {
        aPosition = transform translation ifFalse: {
            transform translation: aPosition.
            self transformChanged.
        }.
    }.

    const inline method orientation => QuaternionF32
        := orientation.

    inline method orientation: (newOrientation: QuaternionF32) ::=> Void := {
        orientation = newOrientation ifFalse: {
            orientation := newOrientation.
            transform orthonormal: orientation asMatrix3x3.
            self transformChanged.
        }.
    }.

    inline method transformChanged => Void := {
        transformChanged := true
    }.

    virtual method validateRenderingState: (renderingDelta: Float32) ::=> Void := {
        ## FIXME: use the rendering delta to perform and additional linear interpolation.
        transformChanged ifTrue: {
            sceneNode ifNotNil: {
                sceneNode _ transform: transform.
            }.
            transformChanged := false.
        }.
    }.
}.

compileTime constant EntityPtr := Entity sharedPointer.
compileTime constant EntityWeakPtr := Entity weakPointer.
compileTime constant Entities := Stdn Collections Vector(EntityPtr).

}. ## namespace GameSystem
}. ## namespace Woden
