namespace Woden definition: {
namespace GameSystem definition: {

useNamespace: Std Math.
useNamespace: Std Math Geometry.
useNamespace: Std Math LinearAlgebra.
useNamespace: Std Serialization.
useNamespace: Woden Runtime.
useNamespace: Woden MapDefinition.

class Entity superclass: Std RCObject; definition: {
    public field parentSector type: Sector pointer.
    public field className type: Std String.
    public field name type: Std String.

    field previousTransform type: DecomposedTransform3dF32.
    field transform type: DecomposedTransform3dF32.
    field renderTransform type: ReversibleAffineTransform3dF32.

    public field localBoundingBox type: BoxF32.
    public field collisionObject type: CollisionObjectRef.
    public field visualComponent type: VisualComponentRe.
    public field sceneNode type: SceneNodeRef.

    meta definition: {
        macro method registerWithClassName: className
            := ``(global Factory mutable type: EntityFactory(SelfType, `,className))
    }.

    virtual method postLoadInitialization => Void := {
        ## This is a place to initialize parameters and ignore there values after loading from a definition.
    }.

    const inline method isInSector2D => Boolean8
        := parentSector isNotNil && parentSector _ isSector2D.

    method boundingBox => BoxF32
        := localBoundingBox transformedWith: transform asReversibleAffineTransform3d.

    method engine => EngineRef
        := parentSector ifNil: {EngineRef nil} ifNotNil: {parentSector _ engine}.

    method physicalSector => PhysicalSector pointer
        := parentSector
            ifNil: {nil}
            ifNotNil: {parentSector _ physicalSector rcObjectAddress}.

    virtual method loadFromDefinition: (definition: EntityDefinitionRef const ref) fromLayer: (layerDefinition: SectorLayerDefinitionRef const ref) ::=> Void := {
        className := definition className asArraySlice.
        name := definition name.
        self
            scale: definition scale;
            orientation: definition orientation;
            position: (definition positionForLayer: layerDefinition).
        localBoundingBox := definition localBoundingBox.
        localBoundingBox depth = 0 ifTrue: {
            localBoundingBox min z: layerDefinition layerMinZ.
            localBoundingBox max z: layerDefinition layerMaxZ.
        }.

        definition components do: {:each :: Void |
            each isCollisionShape ifTrue: {
                let shape := each downCastFor: CollisionShape.
                shape setupForLayer: layerDefinition.
                self buildCollisionObjectWithShape: shape

            }.

            each isVisualComponent ifTrue: {
                visualComponent := each downCastFor: VisualComponent.
                visualComponent setupForLayer: layerDefinition.
            }
        }.
    }.

    virtual method buildCollisionObjectWithShape: (collisionShape: CollisionShapeRef const ref) ::=> Void
        := {}.

    virtual method isMetadata => Boolean8
        := false.

    virtual method isAlive => Boolean8
        := true.

    virtual method needsTicking => Boolean8
        := false.

    virtual method spawn => Void := {

    }.

    virtual method prePhysicsTick: (delta: Float32) ::=> Void := {
        collisionObject
            orientation: self orientation;
            position: self position.
    }.

    virtual method postPhysicsTick: (delta: Float32) ::=> Void := {
        self
            orientation: collisionObject orientation;
            position: collisionObject position.
    }.

    virtual method onPhysicsCollisionHit: (collidingObject: CollisionObject ref) at: (collisionPoint: Float32x3) ::=> Void := {

    }.

    virtual method tick: (delta: Float32) ::=> Void := {

    }.

    virtual method receiveDamage: (damage: Float32) by: (damagingEntity: Entity ref) at: (damagePoint: Float32x3) ::=> Void := {

    }.

    virtual method destroyed => Void := {
        self removeFromScene.
        self removeFromPhysicalSector.
    }.

    virtual method removeFromScene => Void := {
        sceneNode ifNil: {return: void}.
        sceneNode removeFromScene.
    }.

    virtual method addToScene: (scene: SceneRef const ref) ::=> Void := {
        sceneNode ifNil: {self createSceneNode}.
        sceneNode ifNotNil: {
            scene add: sceneNode
        }
    }.

    virtual method addToPhysicalSector: (physicalSector: PhysicalSectorRef const ref) ::=> Void := {
        collisionObject ifNotNil: {
            physicalSector addCollisionObject: collisionObject
        }
    }.

    virtual method removeFromPhysicalSector => Void := {
        collisionObject ifNotNil: {
            collisionObject removeFromPhysicalSector
        }
    }.

    virtual method createSceneNode ::=> Void := {
        visualComponent ifNil: {return: void}.
        sceneNode := visualComponent createSceneNodeFor: self engine parentTransform: transform asReversibleAffineTransform3d.
    }.

    const inline method transform => DecomposedTransform3dF32 const ref
        := transform.

    inline method transform: (aTransform: DecomposedTransform3dF32 const ref) ::=> Void := {
        transform := aTransform.
    }.

    const inline method scale => Float32x3
        := transform scale.

    inline method scale: (aPosition: Float32x3) ::=> Void := {
        transform scale: aPosition.
    }.

    const inline method position => Float32x3
        := transform translation.

    inline method position: (aPosition: Float32x3) ::=> Void := {
        transform translation: aPosition.
    }.

    const inline method orientation => QuaternionF32
        := transform rotation.

    inline method orientation: (newOrientation: QuaternionF32) ::=> Void := {
        transform rotation: newOrientation
    }.

    virtual method storeOldRenderingStateForInterpolation => Void := {
        previousTransform := transform.
    }.

    virtual method applyInterpolationForRendering: (alpha: Float32) ::=> Void := {
        let interpolatedTransform := previousTransform interpolateTo: transform at: alpha.
        renderTransform := interpolatedTransform asReversibleAffineTransform3d.
        sceneNode ifNotNil: {
            sceneNode transform: renderTransform.
        }.
    }.

    virtual method classDescription => Std ImmutableString
        := "Generic Entity".

    virtual method editorCategory => Std ImmutableString
        := "Generic".

    virtual method editorPreviewModel => Std ImmutableString
        := "".

    virtual method editorBoundingBox => BoxF32
        := localBoundingBox.

    virtual method editorEntityType => EditorEntityType
        := EditorEntityType Point.

    virtual method isAvailableInEditor => Boolean8
        := false.

    virtual method isMapIntrinsic => Boolean8
        := false.

    virtual method writeDescriptionOnto: (descriptionObject: DOM ObjectRef const ref) ::=> Void := {
        let editorBoundingBox := self editorBoundingBox.
        let descriptionProperties := DOM Object new.
        self writeDescriptionPropertiesOnto: descriptionProperties.

        descriptionObject
            at: "classDescription" put: self classDescription asMutableString;
            at: "editorCategory" put: self editorCategory asMutableString;
            at: "type" put: self editorEntityType asString asMutableString;
            at: "previewModel" put: self editorPreviewModel asMutableString;
            at: "boundingBox" put: (DOM Object new
                at: "min" put: (DOM List new
                    reserve: 3;
                    add: editorBoundingBox min x;
                    add: editorBoundingBox min y;
                    add: editorBoundingBox min z;
                    yourself);
                at: "max" put: (DOM List new
                    reserve: 3;
                    add: editorBoundingBox max x;
                    add: editorBoundingBox max y;
                    add: editorBoundingBox max z;
                    yourself);
                yourself);
            at: "isAvailableInEditor" put: self isAvailableInEditor;
            at: "isMapIntrinsic" put: self isMapIntrinsic;
            at: "isMetadata" put: self isMetadata;
            at: "needsTicking" put: self needsTicking;
            at: "properties" put: descriptionProperties.
    }.

    virtual method writeDescriptionPropertiesOnto: (descriptionObject: DOM ObjectRef const ref) ::=> Void := {

    }.
}.

compileTime constant EntityRef := Entity rcRef.
compileTime constant EntityWeakRef := Entity weakRCRef.
compileTime constant Entities := Std Collections Vector(EntityRef).

}. ## namespace GameSystem
}. ## namespace Woden
