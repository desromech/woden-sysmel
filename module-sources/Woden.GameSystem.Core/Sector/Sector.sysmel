namespace Woden definition: {
namespace GameSystem definition: {

useNamespace: Woden Runtime.
useNamespace: Woden MapDefinition.

class Sector superclass: Std RCObject; definition: {
    field engine type: EngineRef.
    field scene type: SceneRef.

    field isSector2D type: Boolean8.
    field physicalSector type: PhysicalSectorRef.
    field metadataEntities type: Entities.
    field physicalEntities type: Entities.
    field aliveEntities type: Entities.
    field tickingEntities type: Entities.

    meta definition: {
        method for: (engine: EngineRef const ref) ::=> SectorRef
            := Sector new initializeWithEngine: engine; yourself.
    }.

    inline method physicalSector => PhysicalSectorRef const ref
        := physicalSector.

    inline method engine => EngineRef const ref
        := engine.

    const inline method isSector2D => Boolean8
        := isSector2D.

    method loadFromDefinition: (definition: SectorDefinitionRef const ref) ::=> Void := {
        isSector2D := definition isSector2D.
        physicalSector := PhysicalSector new
            gravity: Float32x3(0, -9.8f, 0);
            yourself.

        definition layers do: {:eachLayer :: Void |
            eachLayer entities do: {:eachEntity :: Void |
                self loadEntityDefinition: eachEntity fromLayer: eachLayer
            }.
        }
    }.

    method loadEntityDefinition: (definition: EntityDefinitionRef const ref) fromLayer: (layerDefinition: SectorLayerDefinitionRef const ref) ::=> Void := {
        let entity := EntityRegistry uniqueInstance createEntityWithClassName: definition className.
        entity ifNil: {return: void}.
        entity
            parentSector: self address;
            loadFromDefinition: definition fromLayer: layerDefinition;
            postLoadInitialization.
        self spawnEntity: entity.
    }.

    method spawnEntity: (entity: EntityRef const ref) ::=> Void := {
        entity
            parentSector: self address;
            spawn;
            storeOldRenderingStateForInterpolation.
        entity isMetadata ifTrue: {
            metadataEntities add: entity.
        }.

        entity isAlive ifFalse: {return: void}.

        aliveEntities add: entity.
        entity collisionObject ifNotNil: {
            physicalEntities add: entity.
            physicalSector addCollisionObject: entity collisionObject.
        }.

        entity needsTicking ifTrue: {
            tickingEntities add: entity
        }.

        scene ifNotNil: {
            entity
                applyInterpolationForRendering: 0;
                addToScene: scene
        }.
    }.

    method initializeWithEngine: (anEngine: EngineRef const ref) ::=> Void := {
        engine := anEngine.
    }.

    method deactivateAndRemoveFromScene ::=> Void := {
        aliveEntities do: {:each :: Void |
            each removeFromScene
        }.

        scene := nil.
    }.

    method activateAndAddToScene: (newScene: SceneRef) ::=> Void := {
        scene == newScene ifTrue: {return: void}.
        scene := newScene.

        aliveEntities do: {:each :: Void |
            each addToScene: scene
        }.
    }.

    method update: (delta: Float32) ::=> Void := {
        ## Store the old rendering state.
        aliveEntities do: {:each :: Void |
            each storeOldRenderingStateForInterpolation.
        }.

        ## Physics
        {
            physicalEntities do: {:each :: Void |
                each prePhysicsTick: delta
            }.

            physicalSector update: delta.

            physicalEntities do: {:each :: Void |
                each postPhysicsTick: delta
            }.
        }.

        ## Non phyical related ticking.
        {
            tickingEntities do: {:each :: Void |
                each needsTicking ifTrue: {
                    each tick: delta.
                }.
            }.

            tickingEntities removeAllSuchThat: {:each | each isAlive not || each needsTicking not}.
        }.

        {
            aliveEntities removeAllSuchThat: {:each |
                let shouldRemove := each isAlive not.
                shouldRemove ifTrue: {
                    each destroyed
                }.
                shouldRemove
            }.
            physicalEntities removeAllSuchThat: {:each | each isAlive not}.
        }.
    }.

    method applyInterpolationForRendering: (alpha: Float32) ::=> Void := {
        aliveEntities do: {:each :: Void |
            each isAlive ifTrue: {
                each applyInterpolationForRendering: alpha.
            }
        }.
    }.

    method findMetadataEntityWithClassName: (className: Char8 const arraySlice) ::=> EntityRef := {
        metadataEntities do: {:each :: Void |
            each className asArraySlice = className ifTrue: {return: each}
        }.
        EntityRef nil
    }.

    method findPositionUsingMetadataEntityClassNamed: (className: Char8 const arraySlice) default: (defaultPosition: Float32x3) ::=> Float32x3 := {
        let metadataEntity := self findMetadataEntityWithClassName: className.
        metadataEntity
            ifNil: {defaultPosition}
            ifNotNil: {metadataEntity position}
    }.
}.

compileTime constant SectorRef := Sector rcRef.

}. ## namespace GameSystem
}. ## namespace Woden
