namespace Woden definition: {
namespace Assets definition: {

useNamespace: Std Serialization.
useNamespace: Std Graphics Core.
useNamespace: Std Math.

enum GLTFAccessorComponentType valueType: UInt32; values: #{
    Byte: 5120.
    UnsignedByte: 5121.
    Short: 5122.
    UnsignedShort: 5123.
    UnsignedInt: 5125.
    Float: 5126.
}.

enum GLTFPrimitiveMode valueType: UInt8; values: #{
    Points: 0.
    Lines: 1.
    LineLoop: 2.
    LineStrip: 3.
    Triangles: 4.
    TriangleStrip: 5.
    TriangleFan: 6
}; definition: {
    method asPrimitiveTopology => PrimitiveTopology := {
        self selectCase: #{
        Points : PrimitiveTopology Points.
        Lines : PrimitiveTopology Lines.
        LineStrip : PrimitiveTopology LineStrip.

        Triangles : PrimitiveTopology Triangles.
        TriangleStrip : PrimitiveTopology TriangleStrip.
        }
    }
}.

class GLTFModelImporter definition: {
    field parentFolder type: Std FileReference.
    field modelAsset type: ModelAssetRef.

    field embeddedBinaryBufferData type: UInt8 uniquePointer.
    field embeddedBinaryBufferSize type: UIntPointer.

    method importFromBinaryFileReference: (fileReference: Std FileReference const ref) ::=> ModelAssetRef := {
        parentFolder := fileReference parent.
        let readStream := fileReference binaryReadStream.
        readStream ifNil: {return: ModelAssetRef nil}.

        let binaryReader mutable := Std IO BinaryDataStreamReader for: readStream getPointer.
        binaryReader setForLittleEndian.

        let glbMagic := binaryReader nextUInt32.
        let glbVersion := binaryReader nextUInt32.
        let glbLength := binaryReader nextUInt32.

        let jsonObject mutable := DOM ObjectPtr nil.

        let remainingLength mutable := glbLength - 12.

        ## The first chunk must be the json.
        {
            let chunkLength := binaryReader nextUInt32.
            let chunkType := binaryReader nextUInt32.

            chunkType = 16r4E4F534A u ifFalse: {
                return: ModelAssetRef nil
            }.

            let jsonText := binaryReader nextUTF8StringOfSize: chunkLength.
            jsonObject := (JSON ValueParser parseString: jsonText) get: DOM ObjectPtr.

            remainingLength := remainingLength - (8u + chunkLength min: remainingLength).
        }.

        ## The second chunk may be the embedded binary buffer.
        {
            let chunkLength := binaryReader nextUInt32.
            let chunkType := binaryReader nextUInt32.

            chunkType = 16r004E4942 ifTrue: {
                embeddedBinaryBufferData reset: (Std zmalloc(chunkLength) reinterpretCastTo: UInt8 pointer).
                embeddedBinaryBufferSize := chunkLength.
                (readStream _ read: embeddedBinaryBufferData getPointer size: embeddedBinaryBufferSize) = (chunkLength castTo: IntPointer) ifFalse: {
                    return: ModelAssetRef nil
                }
            }.
        }.

        self parseTopLevel: jsonObject _.
    }.

    method parseBuffer: (object: DOM Object const ref) ::=> Void := {
        let buffer := modelAsset newBuffer.
        buffer
            name: (object getString: "name" asMutableString default: "" asMutableString);
            dataSize: (object getUIntPointer: "byteLength" asMutableString).

        object at: "uri" asMutableString ifPresent: {:(DOM Value const ref)uriValue :: Void |
            let uri := uriValue get: Std String.
            let uriFileRef := parentFolder / uri asArraySlice.
            uriFileRef exists ifTrue: {
                let readStream := uriFileRef binaryUnbufferedReadStream.
                readStream ifNil: {return: void}.

                buffer data reset: (Std zmalloc(buffer dataSize) reinterpretCastTo: UInt8 pointer).
                readStream _ read: buffer data getPointer size: buffer dataSize
            }.
        } ifAbsent: {
            embeddedBinaryBufferSize >= 0 && buffer dataSize <= embeddedBinaryBufferSize && embeddedBinaryBufferData isNotNil ifTrue: {
                buffer data reset: embeddedBinaryBufferData release
            }.
        }.

    }.

    method parseBuffers: (list: DOM List ref) ::=> Void := {
        list do: {:each :: Void |
            self parseBuffer: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseBufferView: (object: DOM Object const ref) ::=> Void := {
        let bufferView := modelAsset newBufferView.
        bufferView
            name: (object getString: "name" asMutableString default: "" asMutableString);
            buffer: (modelAsset buffers[object getUIntPointer: "buffer" asMutableString]);
            offset: (object getUIntPointer: "byteOffset" asMutableString default: 0);
            size: (object getUIntPointer: "byteLength" asMutableString);
            stride: (object getUIntPointer: "byteStride" asMutableString default: 0).

        let target := object getUInt32: "target" asMutableString default: 0.
        target selectCase: #{
        34962 : {bufferView usage: BufferUsageMode Array}.
        34963 : {bufferView usage: BufferUsageMode ElementArray}.
        _ : {}
        }.
    }.

    method parseBufferViews: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseBufferView: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseAccessor: (object: DOM Object const ref) ::=> Void := {
        let bufferAccessor := modelAsset newBufferAccessor.
        bufferAccessor
            name: (object getString: "name" asMutableString default: "" asMutableString);
            bufferView: (modelAsset bufferViews[object getUIntPointer: "bufferView" asMutableString]);
            offset: (object getUIntPointer: "byteOffset" asMutableString default: 0);
            count: (object getUIntPointer: "count" asMutableString);
            minValue: (object getFloat64x4: "min" asMutableString default: 0);
            maxValue: (object getFloat64x4: "max" asMutableString default: 0).

        let componentType := (object getUIntPointer: "componentType" asMutableString) castTo: GLTFAccessorComponentType.
        let accessorType := object getString: "type" asMutableString.
        let normalized := object getBoolean8: "normalized" asMutableString default: false.
        let columnCount mutable := 1u.

        bufferAccessor format: (
            accessorType asArraySlice selectCase: #{
            "SCALAR" : {
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8_SNorm ifFalse: PixelFormat R8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8_UNorm ifFalse: PixelFormat R8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16_SNorm ifFalse: PixelFormat R16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16_UNorm ifFalse: PixelFormat R16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32_Float.
                }
            }.
            "VEC2" : {
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8_SNorm ifFalse: PixelFormat R8G8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8_UNorm ifFalse: PixelFormat R8G8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16_SNorm ifFalse: PixelFormat R16G16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16_UNorm ifFalse: PixelFormat R16G16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32_Float.
                }
            }.
            "VEC3" : {
                componentType selectCase: #{
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32_Float.
                }
            }.
            "VEC4" : {
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8B8A8_SNorm ifFalse: PixelFormat R8G8B8A8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8B8A8_UNorm ifFalse: PixelFormat R8G8B8A8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16B16A16_SNorm ifFalse: PixelFormat R16G16B16A16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16B16A16_UNorm ifFalse: PixelFormat R16G16B16A16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32A32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32A32_Float.
                }
            }.
            "MAT2" : {
                columnCount := 2u.
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8_SNorm ifFalse: PixelFormat R8G8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8_UNorm ifFalse: PixelFormat R8G8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16_SNorm ifFalse: PixelFormat R16G16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16_UNorm ifFalse: PixelFormat R16G16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32_Float.
                }
            }.
            "MAT3" : {
                columnCount := 3u.
                componentType selectCase: #{
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32_Float.
                }
            }.
            "MAT4" : {
                columnCount := 4u.
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8B8A8_SNorm ifFalse: PixelFormat R8G8B8A8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8B8A8_UNorm ifFalse: PixelFormat R8G8B8A8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16B16A16_SNorm ifFalse: PixelFormat R16G16B16A16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16B16A16_UNorm ifFalse: PixelFormat R16G16B16A16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32A32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32A32_Float.
                }
            }.
            }
        ).
        bufferAccessor columnCount: columnCount.
    }.

    method parseAccessors: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseAccessor: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseImage: (object: DOM Object const ref) ::=> Void := {
        let image := modelAsset newImage.
        image
            name: (object getString: "name" asMutableString default: "" asMutableString);
            uri: (object getString: "uri" asMutableString default: "" asMutableString);
            mimeType: (object getString: "mimeType" asMutableString default: "" asMutableString).
        object at: "bufferView" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            image bufferView: (modelAsset bufferViews[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseImages: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseImage: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseSampler: (object: DOM Object const ref) ::=> Void := {
        let sampler := modelAsset newSampler.
        sampler
            name: (object getString: "name" asMutableString default: "" asMutableString).
        ##Std stdout << "TODO: parseSampler " << sampler name; nl.
    }.

    method parseSamplers: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseSampler: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseTexture: (object: DOM Object const ref) ::=> Void := {
        let texture := modelAsset newTexture.
        texture
            name: (object getString: "name" asMutableString default: "" asMutableString).

        object at: "sampler" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            texture sampler: (modelAsset samplers[DOM valueToUIntPointer(value)])
        }.
        object at: "source" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            texture image: (modelAsset images[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseTextures: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseTexture: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseMaterialTexture: (object: DOM Object const ref) ::=> ModelAssetTextureRef := {
        let result mutable := ModelAssetTextureRef nil.
        object at: "index" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            result := modelAsset textures[DOM valueToUIntPointer(value)]
        }.

        result
    }.

    method parsePbrMetallicRoughnessMaterial: (object: DOM Object const ref) ::=> ModelAssetMaterialRef := {
        let material := modelAsset newMetallicRoughnessMaterial.

        material
            baseColorFactor: Std Math Color decodeSRGB(object getFloat32x4: "baseColorFactor" asMutableString default: 1.0f);
            metallicFactor: (object getFloat32: "metallicFactor" asMutableString default: 1.0f);
            roughnessFactor: (object getFloat32: "roughnessFactor" asMutableString default: 1.0f).
        object at: "baseColorTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material baseColorTexture: (self parseMaterialTexture: (value get: DOM ObjectPtr) _)..
            material baseColorTexture setColorUsage
        }.
        object at: "metallicRoughnessTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material metallicRoughnessTexture: (self parseMaterialTexture: (value get: DOM ObjectPtr) _).
            material metallicRoughnessTexture setLinearUsage
        }.

        material upCastFor: ModelAssetMaterial.
    }.

    method parseMaterial: (object: DOM Object const ref) ::=> Void := {
        let material mutable := ModelAssetMaterialRef nil.
        object at: "pbrMetallicRoughness" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material := self parsePbrMetallicRoughnessMaterial: (value get: DOM ObjectPtr) _.
        }.

        material ifNil: {
            material := modelAsset newMetallicRoughnessMaterial upCastFor: ModelAssetMaterial.
        }.

        material
            name: (object getString: "name" asMutableString default: "" asMutableString);
            emissionFactor: Std Math Color decodeSRGB(Float32x4(object getFloat32x3: "emissiveFactor" asMutableString default: 0, 0)) rgb;
            doubleSided: (object getBoolean8: "doubleSided" asMutableString default: false);
            alphaCutoff: (object getFloat32: "alphaCutoff" asMutableString default: 0.5f).

        let alphaMode := object getString: "alphaMode" asMutableString default: "OPAQUE" asMutableString.
        material alphaMode: (alphaMode asArraySlice selectCase: #{
        "MASK" : ModelAssetMaterialAlphaMode Mask.
        "BLEND" : ModelAssetMaterialAlphaMode Blend.
        _ : ModelAssetMaterialAlphaMode Opaque.
        }).

        object at: "occlusionTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material occlusionTexture: (self parseMaterialTexture: (value get: DOM ObjectPtr) _).
            material occlusionTexture setLinearUsage
        }.

        object at: "emissiveTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material emissionTexture: (self parseMaterialTexture: (value get: DOM ObjectPtr) _).
            material emissionTexture setColorUsage
        }.

        object at: "normalTexture" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            material normalTexture: (self parseMaterialTexture: (value get: DOM ObjectPtr) _).
            material normalTexture setNormalUsage
        }.
    }.

    method parseMaterials: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseMaterial: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseMeshPrimitiveAttributes: (object: DOM Object const ref) ::=> ModelAssetMeshPrimitiveAttributesRef := {
        let attributeList mutable := ModelAssetMeshPrimitiveAttributeList().

        object keysAndValuesDo: {:(Std String const ref)key :(DOM Value const ref)value :: Void |
            let location := key asArraySlice selectCase: #{
            "POSITION" : VertexAttributeLocation Position.
            "NORMAL" : VertexAttributeLocation Normal.
            "TANGENT" : VertexAttributeLocation Tangent4.
            "TEXCOORD_0" : VertexAttributeLocation Texcoord0.
            "TEXCOORD_1" : VertexAttributeLocation Texcoord1.
            "COLOR_0" : VertexAttributeLocation Color0.
            "JOINTS_0" : VertexAttributeLocation Joints0.
            "WEIGHTS_0" : VertexAttributeLocation Weights0.
            }.

            let accessor := modelAsset bufferAccessors[DOM valueToUIntPointer(value)].

            attributeList add: (ModelAssetMeshPrimitiveAttribute()
                accessor: accessor;
                location: location;
                yourself
            )
        }.

        attributeList sort.

        ## TODO: Attempt to cache these primitive lists.
        modelAsset newMeshPrimitiveAttributes
            attributes: attributeList;
            yourself
    }.

    method parseMesh: (mesh: ModelAssetMeshRef const ref) primitive: (object: DOM Object const ref) ::=> Void := {

        let primitive := ModelAssetMeshPrimitive new.
        mesh primitives add: primitive.

        primitive
            attributes: (self parseMeshPrimitiveAttributes: (object getObject: "attributes" asMutableString) _ );
            primitiveTopology: ((object getUInt32: "mode" asMutableString default: 4) castTo: GLTFPrimitiveMode) asPrimitiveTopology.

        object at: "indices" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            primitive indices: (modelAsset bufferAccessors[DOM valueToUIntPointer(value)])
        }.

        object at: "material" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            primitive material: (modelAsset materials[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseMesh: (object: DOM Object const ref) ::=> Void := {
        let mesh := modelAsset newMesh.
        mesh
            name: (object getString: "name" asMutableString default: "" asMutableString).

        object at: "primitives" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            (value get: DOM ListPtr) _ do: {:each :: Void |
                self parseMesh: mesh primitive: (each get: DOM ObjectPtr) _
            }.
        }.
    }.

    method parseMeshes: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseMesh: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseSkin: (object: DOM Object const ref) ::=> Void := {
        let skeleton := modelAsset newSkeleton.
        skeleton
            name: (object getString: "name" asMutableString default: "" asMutableString).

        object at: "inverseBindMatrices" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            skeleton inverseBindMatricesAccessor: modelAsset bufferAccessors[DOM valueToUIntPointer(value)]
        }.
    }.

    method parseSkins: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseSkin: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseSkin: (skeleton: ModelAssetSkeletonRef const ref) nodes:(object: DOM Object const ref) ::=> Void := {
        let joints := object getList: "joints" asMutableString.
        skeleton joints reserve: joints _ size.
        joints _ do: {:(DOM Value const ref)value :: Void |
            skeleton joints add: modelAsset nodes[DOM valueToUIntPointer(value)]
        }.

        object at: "skeleton" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            skeleton rootJoint := modelAsset nodes[DOM valueToUIntPointer(value)]
        }.
    }.

    method parseSkinNodes: (list: DOM List const ref) ::=> Void := {
        list doWithIndex: {:each :index :: Void |
            self parseSkin: modelAsset skeletons[index] nodes: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseNode: (object: DOM Object const ref) ::=> Void := {
        let node := modelAsset newNode.
        node
            name: (object getString: "name" asMutableString default: "" asMutableString).

        object at: "mesh" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            node mesh: (modelAsset meshes[DOM valueToUIntPointer(value)])
        }.

        object at: "skin" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            node skeleton: (modelAsset skeletons[DOM valueToUIntPointer(value)])
        }.

        object at: "scale" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let scale := DOM valueToFloat32x3(value, 1).
            node scale: scale.
            node transform scaleBy: scale.
        }.
        object at: "rotation" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let rotation := QuaternionF32 xyzw: DOM valueToFloat32x4(value, Float32x4(0, 0, 0, 1)).
            node rotationQuaternion: rotation.
            node transform applyOrthonormalMatrix: rotation asFloat32x3x3.
        }.
        object at: "translation" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let translation := DOM valueToFloat32x3(value, 0).
            node translation: translation.
            node transform translateBy: translation.
        }.

        object at: "matrix" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let matrix := DOM valueToFloat32x4x4(value, Float32x4x4 identity).
            Std stdout << "TODO: parse node matrix "; nl.
            Std stdout << matrix firstRow; nl.
            Std stdout << matrix secondRow; nl.
            Std stdout << matrix thirdRow; nl.
            Std stdout << matrix fourthRow; nl.
        }.

    }.

    method parseNode: (node: ModelAssetNodeRef const ref) children: (object: DOM Object const ref) ::=> Void := {
        object at: "children" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let children ref := (value get: DOM ListPtr) _.
            node children reserve: children size.
            0 until: children size do: {:i :: Void |
                node children add: (modelAsset nodes[children getUIntPointer: i])
            }
        }
    }.

    method parseNodes: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseNode: (each get: DOM ObjectPtr) _
        }.

        list doWithIndex: {:each :index :: Void |
            self parseNode: modelAsset nodes[index] children: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseScene: (object: DOM Object const ref) ::=> Void := {
        let scene := modelAsset newScene.
        scene
            name: (object getString: "name" asMutableString default: "" asMutableString).

        object at: "nodes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            let nodeList ref := (value get: DOM ListPtr) _.
            scene rootNodes reserve: nodeList size.
            0 until: nodeList size do: {:i :: Void |
                scene rootNodes add: (modelAsset nodes[nodeList getUIntPointer: i])
            }.
        }.
    }.

    method parseScenes: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseScene: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseAnimation: (animation: ModelAssetAnimationRef const ref) sampler: (object: DOM Object const ref) ::=> Void := {
        let sampler := ModelAssetAnimationSampler new.
        sampler
            index: animation samplers size;
            inputAccessor: (modelAsset bufferAccessors[object getUIntPointer: "input" asMutableString]);
            outputAccessor: (modelAsset bufferAccessors[object getUIntPointer: "output" asMutableString]).

        let interpolationMethod := object getString: "interpolation" asMutableString default: "LINEAR" asMutableString.
        sampler interpolationMethod: (interpolationMethod asArraySlice selectCase: #{
            "LINEAR" : ModelAssetAnimationInterpolationMethod Linear.
            "STEP" : ModelAssetAnimationInterpolationMethod Step.
            "CUBICSPLINE" : ModelAssetAnimationInterpolationMethod CubicSpline.
            _ : ModelAssetAnimationInterpolationMethod Linear.
        }).
        animation samplers add: sampler.
    }.

    method parseAnimation: (animation: ModelAssetAnimationRef const ref) channel: (object: DOM Object const ref) ::=> Void := {
        let channel := ModelAssetAnimationChannel new.
        channel sampler: (animation samplers[object getUIntPointer: "sampler" asMutableString]).

        let target := object getObject: "target" asMutableString.
        let path := target _ getString: "path" asMutableString.
        channel targetProperty: (path asArraySlice selectCase: #{
            "translation" : ModelAssetAnimationTargetProperty Translation.
            "rotation" : ModelAssetAnimationTargetProperty Rotation.
            "scale" : ModelAssetAnimationTargetProperty Scale.
            "weights" : ModelAssetAnimationTargetProperty Weights.
        }).

        target _ at: "node" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            channel targetNode: modelAsset nodes[DOM valueToUIntPointer(value)]
        }.

        animation channels add: channel.
    }.

    method parseAnimation: (object: DOM Object const ref) ::=> Void := {
        let animation := modelAsset newAnimation.
        animation
            name: (object getString: "name" asMutableString default: "" asMutableString).

        let samplers := object getList: "samplers" asMutableString.
        animation samplers reserve: samplers _ size.
        samplers _ do: {:each :: Void |
            self parseAnimation: animation sampler: (each get: DOM ObjectPtr) _
        }.

        let channels := object getList: "channels" asMutableString.
        animation channels reserve: channels _ size.
        channels _ do: {:each :: Void |
            self parseAnimation: animation channel: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseAnimations: (list: DOM List const ref) ::=> Void := {
        list do: {:each :: Void |
            self parseAnimation: (each get: DOM ObjectPtr) _
        }.
    }.

    method parseTopLevel: (object: DOM Object const ref) ::=> ModelAssetRef := {
        modelAsset := ModelAsset new.
        modelAsset parentFolder: parentFolder.
        object at: "buffers" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseBuffers: (value get: DOM ListPtr) _
        }.
        object at: "bufferViews" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseBufferViews: (value get: DOM ListPtr) _
        }.
        object at: "accessors" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseAccessors: (value get: DOM ListPtr) _
        }.
        object at: "images" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseImages: (value get: DOM ListPtr) _
        }.
        object at: "samplers" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSamplers: (value get: DOM ListPtr) _
        }.
        object at: "textures" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseTextures: (value get: DOM ListPtr) _
        }.
        object at: "materials" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseMaterials: (value get: DOM ListPtr) _
        }.
        object at: "meshes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseMeshes: (value get: DOM ListPtr) _
        }.
        object at: "skins" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSkins: (value get: DOM ListPtr) _
        }.
        object at: "nodes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseNodes: (value get: DOM ListPtr) _
        }.
        object at: "skins" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSkinNodes: (value get: DOM ListPtr) _
        }.
        object at: "scenes" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseScenes: (value get: DOM ListPtr) _
        }.
        object at: "animations" asMutableString ifPresent: {:(DOM Value const ref)value :: Void |
            self parseAnimations: (value get: DOM ListPtr) _
        }.

        modelAsset
            cleanupMeshes;
            cleanupSkeletons.
        modelAsset
    }.

    method importFromFileReference: (fileReference: Std FileReference const ref) ::=> ModelAssetRef := {
        parentFolder := fileReference parent.

        let jsonReadStream := fileReference binaryReadStream.
        jsonReadStream ifNil: {return: ModelAssetRef nil}.

        let jsonValue := JSON ValueParser parseStream: jsonReadStream getPointer.
        self parseTopLevel: (jsonValue get: DOM ObjectPtr) _.
    }.
}.

class GLTFModelImporterExporter superclass: ModelImporterExporter; definition: {
    const override method supportsExtension: (extension: Char8 const arraySlice) ::=> Boolean8
        := extension = "gltf" || extension = "glb".

    const override method readFromFileReference: (fileReference: Std FileReference const ref) ::=> ModelAssetRef := {
        let importer mutable := GLTFModelImporter().
        fileReference extension = "glb" ifTrue: {
            importer importFromBinaryFileReference: fileReference
        } ifFalse: {
            importer importFromFileReference: fileReference
        }.
    }.
}.

global GLTFModelImporterExporterRegistration mutable type: ModelImporterExporterRegisterer(GLTFModelImporterExporter).

} ## End of namespace Assets
} ## End of namespace Woden
