namespace Woden definition: {
namespace Assets definition: {

useNamespace: Std Serialization.
useNamespace: Std Graphics Core.
useNamespace: Std Math.

enum GLTFAccessorComponentType valueType: UInt32; values: #{
    Byte: 5120.
    UnsignedByte: 5121.
    Short: 5122.
    UnsignedShort: 5123.
    UnsignedInt: 5125.
    Float: 5126.
}.

enum GLTFPrimitiveMode valueType: UInt8; values: #{
    Points: 0.
    Lines: 1.
    LineLoop: 2.
    LineStrip: 3.
    Triangles: 4.
    TriangleStrip: 5.
    TriangleFan: 6
}; definition: {
    method asPrimitiveTopology => PrimitiveTopology := {
        self selectCase: #{
        Points : PrimitiveTopology Points.
        Lines : PrimitiveTopology Lines.
        LineStrip : PrimitiveTopology LineStrip.

        Triangles : PrimitiveTopology Triangles.
        TriangleStrip : PrimitiveTopology TriangleStrip.
        }
    }
}.

class GLTFModelImporter definition: {
    field parentFolder type: Std FileReference.
    field modelAsset type: ModelAssetRef.

    field embeddedBinaryBufferData type: UInt8 uniquePointer.
    field embeddedBinaryBufferSize type: UIntPointer.

    method importFromBinaryFileReference: (fileReference: Std FileReference const ref) ::=> ModelAssetRef := {
        parentFolder := fileReference parent.
        let readStream := fileReference binaryReadStream.
        readStream ifNil: {return: ModelAssetRef nil}.

        let binaryReader mutable := Std IO BinaryDataStreamReader for: readStream getPointer.
        binaryReader setForLittleEndian.

        let glbMagic := binaryReader nextUInt32.
        let glbVersion := binaryReader nextUInt32.
        let glbLength := binaryReader nextUInt32.

        let jsonObject mutable := DOM ObjectRef nil.

        let remainingLength mutable := glbLength - 12.

        ## The first chunk must be the json.
        {
            let chunkLength := binaryReader nextUInt32.
            let chunkType := binaryReader nextUInt32.

            chunkType = 16r4E4F534A u ifFalse: {
                return: ModelAssetRef nil
            }.

            let jsonText := binaryReader nextUTF8StringOfSize: chunkLength.
            jsonObject := (JSON ValueParser parseString: jsonText) get: DOM ObjectRef.

            remainingLength := remainingLength - (8u + chunkLength min: remainingLength).
        }.

        ## The second chunk may be the embedded binary buffer.
        {
            let chunkLength := binaryReader nextUInt32.
            let chunkType := binaryReader nextUInt32.

            chunkType = 16r004E4942 ifTrue: {
                embeddedBinaryBufferData reset: (Std zmalloc(chunkLength) reinterpretCastTo: UInt8 pointer).
                embeddedBinaryBufferSize := chunkLength.
                (readStream _ read: embeddedBinaryBufferData getPointer size: embeddedBinaryBufferSize) = (chunkLength castTo: IntPointer) ifFalse: {
                    return: ModelAssetRef nil
                }
            }.
        }.

        self parseTopLevel: jsonObject.
    }.

    method parseBuffer: (object: DOM ObjectRef const ref) ::=> Void := {
        let buffer := modelAsset newBuffer.
        buffer
            name: (object getString: "name" default: "");
            dataSize: (object getUIntPointer: "byteLength").

        object at: "uri" ifPresent: {:(DOM Value const ref)uriValue :: Void |
            let uri := uriValue get: Std String.
            let uriFileRef := parentFolder / uri asArraySlice.
            uriFileRef exists ifTrue: {
                let readStream := uriFileRef binaryUnbufferedReadStream.
                readStream ifNil: {return: void}.

                buffer data reset: (Std zmalloc(buffer dataSize) reinterpretCastTo: UInt8 pointer).
                readStream _ read: buffer data getPointer size: buffer dataSize
            }.
        } ifAbsent: {
            embeddedBinaryBufferSize >= 0 && buffer dataSize <= embeddedBinaryBufferSize && embeddedBinaryBufferData isNotNil ifTrue: {
                buffer data reset: embeddedBinaryBufferData release
            }.
        }.

    }.

    method parseBuffers: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseBuffer: (each get: DOM ObjectRef)
        }.
    }.

    method parseBufferView: (object: DOM ObjectRef const ref) ::=> Void := {
        let bufferView := modelAsset newBufferView.
        bufferView
            name: (object getString: "name" default: "");
            buffer: (modelAsset buffers[object getUIntPointer: "buffer"]);
            offset: (object getUIntPointer: "byteOffset" default: 0);
            size: (object getUIntPointer: "byteLength");
            stride: (object getUIntPointer: "byteStride" default: 0).

        let target := object getUInt32: "target" default: 0.
        target selectCase: #{
        34962 : {bufferView usage: BufferUsageMode Array}.
        34963 : {bufferView usage: BufferUsageMode ElementArray}.
        _ : {}
        }.
    }.

    method parseBufferViews: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseBufferView: (each get: DOM ObjectRef)
        }.
    }.

    method parseAccessor: (object: DOM ObjectRef const ref) ::=> Void := {
        let bufferAccessor := modelAsset newBufferAccessor.
        bufferAccessor
            name: (object getString: "name" default: "");
            bufferView: (modelAsset bufferViews[object getUIntPointer: "bufferView"]);
            offset: (object getUIntPointer: "byteOffset" default: 0);
            count: (object getUIntPointer: "count");
            minValue: (object getFloat64x4: "min" default: 0);
            maxValue: (object getFloat64x4: "max" default: 0).

        let componentType := (object getUIntPointer: "componentType") castTo: GLTFAccessorComponentType.
        let accessorType := object getString: "type".
        let normalized := object getBoolean8: "normalized" default: false.
        let columnCount mutable := 1u.

        bufferAccessor format: (
            accessorType asArraySlice selectCase: #{
            "SCALAR" : {
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8_SNorm ifFalse: PixelFormat R8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8_UNorm ifFalse: PixelFormat R8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16_SNorm ifFalse: PixelFormat R16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16_UNorm ifFalse: PixelFormat R16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32_Float.
                }
            }.
            "VEC2" : {
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8_SNorm ifFalse: PixelFormat R8G8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8_UNorm ifFalse: PixelFormat R8G8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16_SNorm ifFalse: PixelFormat R16G16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16_UNorm ifFalse: PixelFormat R16G16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32_Float.
                }
            }.
            "VEC3" : {
                componentType selectCase: #{
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32_Float.
                }
            }.
            "VEC4" : {
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8B8A8_SNorm ifFalse: PixelFormat R8G8B8A8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8B8A8_UNorm ifFalse: PixelFormat R8G8B8A8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16B16A16_SNorm ifFalse: PixelFormat R16G16B16A16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16B16A16_UNorm ifFalse: PixelFormat R16G16B16A16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32A32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32A32_Float.
                }
            }.
            "MAT2" : {
                columnCount := 2u.
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8_SNorm ifFalse: PixelFormat R8G8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8_UNorm ifFalse: PixelFormat R8G8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16_SNorm ifFalse: PixelFormat R16G16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16_UNorm ifFalse: PixelFormat R16G16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32_Float.
                }
            }.
            "MAT3" : {
                columnCount := 3u.
                componentType selectCase: #{
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32_Float.
                }
            }.
            "MAT4" : {
                columnCount := 4u.
                componentType selectCase: #{
                GLTFAccessorComponentType Byte : (normalized ifTrue: PixelFormat R8G8B8A8_SNorm ifFalse: PixelFormat R8G8B8A8_SInt).
                GLTFAccessorComponentType UnsignedByte : (normalized ifTrue: PixelFormat R8G8B8A8_UNorm ifFalse: PixelFormat R8G8B8A8_UInt).
                GLTFAccessorComponentType Short : (normalized ifTrue: PixelFormat R16G16B16A16_SNorm ifFalse: PixelFormat R16G16B16A16_SInt).
                GLTFAccessorComponentType UnsignedShort : (normalized ifTrue: PixelFormat R16G16B16A16_UNorm ifFalse: PixelFormat R16G16B16A16_UInt).
                GLTFAccessorComponentType UnsignedInt : PixelFormat R32G32B32A32_UInt.
                GLTFAccessorComponentType Float : PixelFormat R32G32B32A32_Float.
                }
            }.
            }
        ).
        bufferAccessor columnCount: columnCount.
    }.

    method parseAccessors: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseAccessor: (each get: DOM ObjectRef)
        }.
    }.

    method parseImage: (object: DOM ObjectRef const ref) ::=> Void := {
        let image := modelAsset newImage.
        image
            name: (object getString: "name" default: "");
            uri: (object getString: "uri" default: "");
            mimeType: (object getString: "mimeType" default: "").
        object at: "bufferView" ifPresent: {:(DOM Value const ref)value :: Void |
            image bufferView: (modelAsset bufferViews[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseImages: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseImage: (each get: DOM ObjectRef)
        }.
    }.

    method parseSampler: (object: DOM ObjectRef const ref) ::=> Void := {
        let sampler := modelAsset newSampler.
        sampler
            name: (object getString: "name" default: "").
        ##Std stdout << "TODO: parseSampler " << sampler name; nl.
    }.

    method parseSamplers: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseSampler: (each get: DOM ObjectRef)
        }.
    }.

    method parseTexture: (object: DOM ObjectRef const ref) ::=> Void := {
        let texture := modelAsset newTexture.
        texture
            name: (object getString: "name" default: "").

        object at: "sampler" ifPresent: {:(DOM Value const ref)value :: Void |
            texture sampler: (modelAsset samplers[DOM valueToUIntPointer(value)])
        }.
        object at: "source" ifPresent: {:(DOM Value const ref)value :: Void |
            texture image: (modelAsset images[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseTextures: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseTexture: (each get: DOM ObjectRef)
        }.
    }.

    method parseMaterialTexture: (object: DOM ObjectRef const ref) ::=> ModelAssetTextureRef := {
        let result mutable := ModelAssetTextureRef nil.
        object at: "index" ifPresent: {:(DOM Value const ref)value :: Void |
            result := modelAsset textures[DOM valueToUIntPointer(value)]
        }.

        result
    }.

    method parsePbrMetallicRoughnessMaterial: (object: DOM ObjectRef const ref) ::=> ModelAssetMaterialRef := {
        let material := modelAsset newMetallicRoughnessMaterial.

        material
            baseColorFactor: Std Math Color decodeSRGB(object getFloat32x4: "baseColorFactor" default: 1.0f);
            metallicFactor: (object getFloat32: "metallicFactor" default: 1.0f);
            roughnessFactor: (object getFloat32: "roughnessFactor" default: 1.0f).
        object at: "baseColorTexture" ifPresent: {:(DOM Value const ref)value :: Void |
            material baseColorTexture: (self parseMaterialTexture: (value get: DOM ObjectRef) ).
            material baseColorTexture setColorUsage
        }.
        object at: "metallicRoughnessTexture" ifPresent: {:(DOM Value const ref)value :: Void |
            material metallicRoughnessTexture: (self parseMaterialTexture: (value get: DOM ObjectRef)).
            material metallicRoughnessTexture setLinearUsage
        }.

        material upCastFor: ModelAssetMaterial.
    }.

    method parseMaterial: (object: DOM ObjectRef const ref) ::=> Void := {
        let material mutable := ModelAssetMaterialRef nil.
        object at: "pbrMetallicRoughness" ifPresent: {:(DOM Value const ref)value :: Void |
            material := self parsePbrMetallicRoughnessMaterial: (value get: DOM ObjectRef).
        }.

        material ifNil: {
            material := modelAsset newMetallicRoughnessMaterial upCastFor: ModelAssetMaterial.
        }.

        material
            name: (object getString: "name" default: "");
            emissionFactor: Std Math Color decodeSRGB(Float32x4(object getFloat32x3: "emissiveFactor" default: 0, 0)) rgb;
            doubleSided: (object getBoolean8: "doubleSided" default: false);
            alphaCutoff: (object getFloat32: "alphaCutoff" default: 0.5f).

        let alphaMode := object getString: "alphaMode" default: "OPAQUE".
        material alphaMode: (alphaMode asArraySlice selectCase: #{
        "MASK" : ModelAssetMaterialAlphaMode Mask.
        "BLEND" : ModelAssetMaterialAlphaMode Blend.
        _ : ModelAssetMaterialAlphaMode Opaque.
        }).

        object at: "occlusionTexture" ifPresent: {:(DOM Value const ref)value :: Void |
            material occlusionTexture: (self parseMaterialTexture: (value get: DOM ObjectRef)).
            material occlusionTexture setLinearUsage
        }.

        object at: "emissiveTexture" ifPresent: {:(DOM Value const ref)value :: Void |
            material emissionTexture: (self parseMaterialTexture: (value get: DOM ObjectRef)).
            material emissionTexture setColorUsage
        }.

        object at: "normalTexture" ifPresent: {:(DOM Value const ref)value :: Void |
            material normalTexture: (self parseMaterialTexture: (value get: DOM ObjectRef)).
            material normalTexture setNormalUsage
        }.
    }.

    method parseMaterials: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseMaterial: (each get: DOM ObjectRef)
        }.
    }.

    method parseMeshPrimitiveAttributes: (object: DOM ObjectRef const ref) ::=> ModelAssetMeshPrimitiveAttributesRef := {
        let attributeList mutable := ModelAssetMeshPrimitiveAttributeList().

        object keysAndValuesDo: {:(Std String const ref)key :(DOM Value const ref)value :: Void |
            let location := key asArraySlice selectCase: #{
            "POSITION" : VertexAttributeLocation Position.
            "NORMAL" : VertexAttributeLocation Normal.
            "TANGENT" : VertexAttributeLocation Tangent4.
            "TEXCOORD_0" : VertexAttributeLocation Texcoord0.
            "TEXCOORD_1" : VertexAttributeLocation Texcoord1.
            "COLOR_0" : VertexAttributeLocation Color0.
            "JOINTS_0" : VertexAttributeLocation Joints0.
            "WEIGHTS_0" : VertexAttributeLocation Weights0.
            }.

            let accessor := modelAsset bufferAccessors[DOM valueToUIntPointer(value)].

            attributeList add: (ModelAssetMeshPrimitiveAttribute()
                accessor: accessor;
                location: location;
                yourself
            )
        }.

        attributeList sort.

        ## TODO: Attempt to cache these primitive lists.
        modelAsset newMeshPrimitiveAttributes
            attributes: attributeList;
            yourself
    }.

    method parseMesh: (mesh: ModelAssetMeshRef const ref) primitive: (object: DOM ObjectRef const ref) ::=> Void := {

        let primitive := ModelAssetMeshPrimitive new.
        mesh primitives add: primitive.

        primitive
            attributes: (self parseMeshPrimitiveAttributes: (object getObject: "attributes"));
            primitiveTopology: ((object getUInt32: "mode" default: 4) castTo: GLTFPrimitiveMode) asPrimitiveTopology.

        object at: "indices" ifPresent: {:(DOM Value const ref)value :: Void |
            primitive indices: (modelAsset bufferAccessors[DOM valueToUIntPointer(value)])
        }.

        object at: "material" ifPresent: {:(DOM Value const ref)value :: Void |
            primitive material: (modelAsset materials[DOM valueToUIntPointer(value)])
        }.
    }.

    method parseMesh: (object: DOM ObjectRef const ref) ::=> Void := {
        let mesh := modelAsset newMesh.
        mesh
            name: (object getString: "name" default: "").

        object at: "primitives" ifPresent: {:(DOM Value const ref)value :: Void |
            (value get: DOM ListRef) do: {:(DOM Value const ref)each :: Void |
                self parseMesh: mesh primitive: (each get: DOM ObjectRef)
            }.
        }.
    }.

    method parseMeshes: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseMesh: (each get: DOM ObjectRef)
        }.
    }.

    method parseSkin: (object: DOM ObjectRef const ref) ::=> Void := {
        let skeleton := modelAsset newSkeleton.
        skeleton
            name: (object getString: "name" default: "").

        object at: "inverseBindMatrices" ifPresent: {:(DOM Value const ref)value :: Void |
            skeleton inverseBindMatricesAccessor: modelAsset bufferAccessors[DOM valueToUIntPointer(value)]
        }.
    }.

    method parseSkins: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseSkin: (each get: DOM ObjectRef)
        }.
    }.

    method parseSkin: (skeleton: ModelAssetSkeletonRef const ref) nodes:(object: DOM ObjectRef const ref) ::=> Void := {
        let joints := object getList: "joints".
        skeleton joints reserve: joints size.
        joints do: {:(DOM Value const ref)value :: Void |
            skeleton joints add: modelAsset nodes[DOM valueToUIntPointer(value)]
        }.

        object at: "skeleton" ifPresent: {:(DOM Value const ref)value :: Void |
            skeleton rootJoint := modelAsset nodes[DOM valueToUIntPointer(value)]
        }.
    }.

    method parseSkinNodes: (list: DOM ListRef const ref) ::=> Void := {
        list doWithIndex: {:(DOM Value const ref)each :(UIntPointer)index :: Void |
            self parseSkin: modelAsset skeletons[index] nodes: (each get: DOM ObjectRef)
        }.
    }.

    method parseNode: (object: DOM ObjectRef const ref) ::=> Void := {
        let node := modelAsset newNode.
        node
            name: (object getString: "name" default: "").

        object at: "mesh" ifPresent: {:(DOM Value const ref)value :: Void |
            node mesh: (modelAsset meshes[DOM valueToUIntPointer(value)])
        }.

        object at: "skin" ifPresent: {:(DOM Value const ref)value :: Void |
            node skeleton: (modelAsset skeletons[DOM valueToUIntPointer(value)])
        }.

        object at: "scale" ifPresent: {:(DOM Value const ref)value :: Void |
            let scale := DOM valueToFloat32x3(value, 1).
            node scale: scale.
            node transform scaleBy: scale.
        }.
        object at: "rotation" ifPresent: {:(DOM Value const ref)value :: Void |
            let rotation := QuaternionF32 xyzw: DOM valueToFloat32x4(value, Float32x4(0, 0, 0, 1)).
            node rotationQuaternion: rotation.
            node transform applyOrthonormalMatrix: rotation asFloat32x3x3.
        }.
        object at: "translation" ifPresent: {:(DOM Value const ref)value :: Void |
            let translation := DOM valueToFloat32x3(value, 0).
            node translation: translation.
            node transform translateBy: translation.
        }.

        object at: "matrix" ifPresent: {:(DOM Value const ref)value :: Void |
            let matrix := DOM valueToFloat32x4x4(value, Float32x4x4 identity).
            Std stdout << "TODO: parse node matrix "; nl.
            Std stdout << matrix firstRow; nl.
            Std stdout << matrix secondRow; nl.
            Std stdout << matrix thirdRow; nl.
            Std stdout << matrix fourthRow; nl.
        }.

    }.

    method parseNode: (node: ModelAssetNodeRef const ref) children: (object: DOM ObjectRef const ref) ::=> Void := {
        object at: "children" ifPresent: {:(DOM Value const ref)value :: Void |
            let children ref := (value get: DOM ListRef).
            node children reserve: children size.
            0 until: children size do: {:i :: Void |
                node children add: (modelAsset nodes[children getUIntPointer: i])
            }
        }
    }.

    method parseNodes: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseNode: (each get: DOM ObjectRef)
        }.

        list doWithIndex: {:(DOM Value const ref)each :(UIntPointer)index :: Void |
            self parseNode: modelAsset nodes[index] children: (each get: DOM ObjectRef)
        }.
    }.

    method parseScene: (object: DOM ObjectRef const ref) ::=> Void := {
        let scene := modelAsset newScene.
        scene
            name: (object getString: "name" default: "").

        object at: "nodes" ifPresent: {:(DOM Value const ref)value :: Void |
            let nodeList ref := (value get: DOM ListRef).
            scene rootNodes reserve: nodeList size.
            0 until: nodeList size do: {:i :: Void |
                scene rootNodes add: (modelAsset nodes[nodeList getUIntPointer: i])
            }.
        }.
    }.

    method parseScenes: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseScene: (each get: DOM ObjectRef)
        }.
    }.

    method parseAnimation: (animation: ModelAssetAnimationRef const ref) sampler: (object: DOM ObjectRef const ref) ::=> Void := {
        let sampler := ModelAssetAnimationSampler new.
        sampler
            index: animation samplers size;
            inputAccessor: (modelAsset bufferAccessors[object getUIntPointer: "input"]);
            outputAccessor: (modelAsset bufferAccessors[object getUIntPointer: "output"]).

        let interpolationMethod := object getString: "interpolation" default: "LINEAR".
        sampler interpolationMethod: (interpolationMethod asArraySlice selectCase: #{
            "LINEAR" : ModelAssetAnimationInterpolationMethod Linear.
            "STEP" : ModelAssetAnimationInterpolationMethod Step.
            "CUBICSPLINE" : ModelAssetAnimationInterpolationMethod CubicSpline.
            _ : ModelAssetAnimationInterpolationMethod Linear.
        }).
        animation samplers add: sampler.
    }.

    method parseAnimation: (animation: ModelAssetAnimationRef const ref) channel: (object: DOM ObjectRef const ref) ::=> Void := {
        let channel := ModelAssetAnimationChannel new.
        channel sampler: (animation samplers[object getUIntPointer: "sampler"]).

        let target := object getObject: "target".
        let path := target getString: "path".
        channel targetProperty: (path asArraySlice selectCase: #{
            "translation" : ModelAssetAnimationTargetProperty Translation.
            "rotation" : ModelAssetAnimationTargetProperty Rotation.
            "scale" : ModelAssetAnimationTargetProperty Scale.
            "weights" : ModelAssetAnimationTargetProperty Weights.
        }).

        target at: "node" ifPresent: {:(DOM Value const ref)value :: Void |
            channel targetNode: modelAsset nodes[DOM valueToUIntPointer(value)]
        }.

        animation channels add: channel.
    }.

    method parseAnimation: (object: DOM ObjectRef const ref) ::=> Void := {
        let animation := modelAsset newAnimation.
        animation
            name: (object getString: "name" default: "").

        let samplers := object getList: "samplers".
        animation samplers reserve: samplers size.
        samplers do: {:(DOM Value const ref)each :: Void |
            self parseAnimation: animation sampler: (each get: DOM ObjectRef)
        }.

        let channels := object getList: "channels".
        animation channels reserve: channels size.
        channels do: {:(DOM Value const ref)each :: Void |
            self parseAnimation: animation channel: (each get: DOM ObjectRef)
        }.
    }.

    method parseAnimations: (list: DOM ListRef const ref) ::=> Void := {
        list do: {:(DOM Value const ref)each :: Void |
            self parseAnimation: (each get: DOM ObjectRef)
        }.
    }.

    method parseTopLevel: (object: DOM ObjectRef const ref) ::=> ModelAssetRef := {
        modelAsset := ModelAsset new.
        modelAsset parentFolder: parentFolder.
        object at: "buffers" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseBuffers: (value get: DOM ListRef)
        }.
        object at: "bufferViews" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseBufferViews: (value get: DOM ListRef)
        }.
        object at: "accessors" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseAccessors: (value get: DOM ListRef)
        }.
        object at: "images" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseImages: (value get: DOM ListRef)
        }.
        object at: "samplers" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSamplers: (value get: DOM ListRef)
        }.
        object at: "textures" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseTextures: (value get: DOM ListRef)
        }.
        object at: "materials" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseMaterials: (value get: DOM ListRef)
        }.
        object at: "meshes" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseMeshes: (value get: DOM ListRef)
        }.
        object at: "skins" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSkins: (value get: DOM ListRef)
        }.
        object at: "nodes" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseNodes: (value get: DOM ListRef)
        }.
        object at: "skins" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseSkinNodes: (value get: DOM ListRef)
        }.
        object at: "scenes" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseScenes: (value get: DOM ListRef)
        }.
        object at: "animations" ifPresent: {:(DOM Value const ref)value :: Void |
            self parseAnimations: (value get: DOM ListRef)
        }.

        modelAsset
            cleanupMeshes;
            cleanupSkeletons.
        modelAsset
    }.

    method importFromFileReference: (fileReference: Std FileReference const ref) ::=> ModelAssetRef := {
        parentFolder := fileReference parent.

        let jsonReadStream := fileReference binaryReadStream.
        jsonReadStream ifNil: {return: ModelAssetRef nil}.

        let jsonValue := JSON ValueParser parseStream: jsonReadStream getPointer.
        self parseTopLevel: (jsonValue get: DOM ObjectRef).
    }.
}.

class GLTFModelImporterExporter superclass: ModelImporterExporter; definition: {
    const override method supportsExtension: (extension: Char8 const arraySlice) ::=> Boolean8
        := extension = "gltf" || extension = "glb".

    const override method readFromFileReference: (fileReference: Std FileReference const ref) ::=> ModelAssetRef := {
        let importer mutable := GLTFModelImporter().
        fileReference extension = "glb" ifTrue: {
            importer importFromBinaryFileReference: fileReference
        } ifFalse: {
            importer importFromFileReference: fileReference
        }.
    }.
}.

global GLTFModelImporterExporterRegistration mutable type: ModelImporterExporterRegisterer(GLTFModelImporterExporter).

} ## End of namespace Assets
} ## End of namespace Woden
