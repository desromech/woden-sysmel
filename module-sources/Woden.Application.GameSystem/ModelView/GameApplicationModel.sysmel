namespace Woden definition: {
namespace GameSystem definition: {

useNamespace: Woden Runtime.
useNamespace: Woden Application.
useNamespace: Std Graphics GUI.

class GameApplicationModel superclass: SceneModel; definition: {
    field globalGameState type: GlobalGameStateRef.
    field accumulatedTime type: Float32.
    field updateTimeStep type: Float32.

    override method initialize => Void := {
        updateTimeStep := 1.0f / 60.0f.
    }.

    method globalGameState => GlobalGameStateRef const ref
        := globalGameState.

    method player => PlayerRef := {
        globalGameState ifNil: {return: PlayerRef nil}.
        globalGameState player
    }.

    virtual method newGlobalGameState => GlobalGameStateRef
        := GlobalGameState new.

    override method initializeModel => Boolean8 := {
        super initializeModel ifFalse: {return: false}.
        self newScene.
        globalGameState := self newGlobalGameState
            initializeWithEngine: self engine;
            scene: scene;
            mainCamera: mainCamera;
            yourself.
        true.
    }.

    override method onStartFrameTick: (event: TickEvent ref) ::=> Void := {
        globalGameState ifNotNil: {
            ## Update the game state in deterministic steps.
            let deltaSeconds := (event deltaSeconds castTo: Float32) min: 2.0f.
            accumulatedTime := accumulatedTime + deltaSeconds.
            while: accumulatedTime >= updateTimeStep do: {
                globalGameState update: updateTimeStep.
                accumulatedTime := accumulatedTime - updateTimeStep.
            }.

            ## Validate and interpolate the rendering state using the remaining delta.
            globalGameState applyInterpolationForRendering: accumulatedTime / updateTimeStep.
        }.

        super onStartFrameTick: event.
    }.

    override method createNewView => ViewRef
        := GameApplicationView new upCastFor: View.

}.

compileTime constant GameApplicationModelRef := GameApplicationModel rcRef.

}. ## namespace GameSystem
}. ## namespace Woden
