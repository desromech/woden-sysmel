namespace Woden definition: {
namespace GameSystem definition: {

useNamespace: Woden Runtime.
useNamespace: Woden Application.
useNamespace: Stdn Graphics GUI.

class GameApplicationModel superclass: SceneModel; definition: {
    field globalGameState type: GlobalGameStatePtr.
    field accumulatedTime type: Float32.
    field updateTimeStep type: Float32.

    override method initialize => Void := {
        updateTimeStep := 1.0f / 60.0f.
    }.

    method globalGameState => GlobalGameStatePtr const ref
        := globalGameState.

    method player => PlayerPtr := {
        globalGameState ifNil: {return: PlayerPtr nil}.
        globalGameState _ player
    }.

    virtual method newGlobalGameState => GlobalGameStatePtr
        := GlobalGameState sharedNew.

    override method initializeModel => Boolean8 := {
        super initializeModel ifFalse: {return: false}.
        self newScene.
        globalGameState := self newGlobalGameState.
        globalGameState _
            initializeWithEngine: self engine;
            scene: scene;
            mainCamera: mainCamera.
        true.
    }.

    override method onStartFrameTick: (event: TickEvent ref) ::=> Void := {
        globalGameState ifNotNil: {
            ## Update the game state in deterministic steps.
            let deltaSeconds := (event deltaSeconds castTo: Float32) min: 2.0f.
            accumulatedTime := accumulatedTime + deltaSeconds.
            while: accumulatedTime >= updateTimeStep do: {
                globalGameState _ update: updateTimeStep.
                accumulatedTime := accumulatedTime - updateTimeStep.
            }.

            ## Validate and interpolate the rendering state using the remaining delta.
            globalGameState _ applyInterpolationForRendering: accumulatedTime / updateTimeStep.
        }.

        super onStartFrameTick: event.
    }.

    override method createNewView => ViewPtr
        := GameApplicationView sharedNew upCastFor: View.

}.

compileTime constant GameApplicationModelPtr := GameApplicationModel sharedPointer.

}. ## namespace GameSystem
}. ## namespace Woden
