namespace Woden definition: {
namespace Shaders definition: {

function fresnelSchlick(F0: Float32x3, cosTheta: Float32) => Float32x3 := {
    let powFactor := 1.0f - cosTheta.
    let powFactor2 := powFactor * powFactor.
    let powFactor4 := powFactor2 * powFactor2.
    let powValue := powFactor4 * powFactor.

    F0 + (Float32x3(1.0f, 1.0f, 1.0f) - F0) * powValue
}.

function fresnelSchlick(F0: Float32, cosTheta: Float32) => Float32 := {
    let powFactor := 1.0f - cosTheta.
    let powFactor2 := powFactor * powFactor.
    let powFactor4 := powFactor2 * powFactor2.
    let powValue := powFactor4 * powFactor.

    F0 + (1.0f - F0) * powValue
}.

function computeLightFalloff(distance: Float32, radius: Float32) => Float32 := {
	let a := distance / radius.
	let a2 := a*a.
	let a4 := a2*a2.
	let num := 1.0f - a4 clampMin: 0.0 max: 1.0f.
	num*num / (distance*distance + 1.0f).
}.

function ggxSpecularDistribution(alpha: Float32, cosTheta: Float32) => Float32 := {
	let alphaSquare := alpha*alpha.
	let den := cosTheta*cosTheta*(alphaSquare - 1.0f) + 1.0f.
	alphaSquare / (Float32 pi * den*den).
}.

function specularDistribution(alpha: Float32, cosTheta: Float32) => Float32
    := ggxSpecularDistribution(alpha, cosTheta).

compileTime constant SquareRootOfTwoOverPi := 0.7978845608028654f.

function smithSchlickBeckmannReciprocalFunction(k: Float32, cosTheta: Float32) => Float32
    := cosTheta*(1.0f - k) + k.

function cookTorranceSmithSchlickGGXMasking(alpha: Float32, NdotL: Float32, NdotV: Float32) => Float32 := {
	let k := alpha * 0.5f.
	1.0f / (4.0f*smithSchlickBeckmannReciprocalFunction(k, NdotL)*smithSchlickBeckmannReciprocalFunction(k, NdotV))
}.

function cookTorranceMasking(alpha: Float32, NdotL: Float32, NdotV: Float32) => Float32
    := cookTorranceSmithSchlickGGXMasking(alpha, NdotL, NdotV).

function projectShadowPosition(shadowPoint: Float32x4, shadowMapLayer: Float32) => Float32x4 := {
    let projectedShadowPoint := shadowPoint xyz / shadowPoint w.
    let shadowCoord := Float32x3(projectedShadowPoint xy, shadowMapLayer).
    Float32x4(shadowCoord, projectedShadowPoint z).
}.

function shadowMapRandomRotation(shadowPoint: Float32x2) => Float32x2x2 := {
	let randomFloat := ( (shadowPoint xy dot: Float32x2(12.9898f,78.233f)) sin * 43758.5453f) fract.
	let s := randomFloat*2.0f - 1.0f.
	let c := (1.0f - s*s) sqrt.

	Float32x2x2(
		Float32x2(c, s),
		Float32x2(-s, c))
}.

struct LightingParameters definition: {
    public field P type: Float32x3.
    public field V type: Float32x3.
    public field N type: Float32x3.
    public field surfaceNormal type: Float32x3.
    public field screenPosition type: Float32x4.

    public field emission type: Float32x3.
    public field occlusion type: Float32.

    public field Cdiff type: Float32x3.
    public field F0 type: Float32x3.

    public field alpha type: Float32.
    public field directAlpha type: Float32.
    public field directMaskingAlpha type: Float32.
    public field opacityFactor type: Float32.
}.

struct MetallicRoughnessLightingParameters definition: {
    public field P type: Float32x3.
    public field V type: Float32x3.
    public field N type: Float32x3.
    public field surfaceNormal type: Float32x3.
    public field screenPosition type: Float32x4.

    public field emission type: Float32x3.
    public field albedo type: Float32x3.

    public field occlusion type: Float32.
    public field metallic type: Float32.
    public field roughness type: Float32.
    public field opacityFactor type: Float32.

    method asGenericLightingParameters => LightingParameters := {
        let dielectricSpecular := Float32x3(0.04f, 0.04f, 0.04f).

        let Cdiff := albedo rgb * (1.0f - dielectricSpecular r) interpolateTo: Float32x3 zeros at: metallic.
        let F0 := dielectricSpecular interpolateTo: albedo rgb at: metallic.

        let alpha := roughness * roughness.
        let directRoughness := 0.01f interpolateTo: 1.0f at: roughness.
        let directAlpha := directRoughness*directRoughness.

        let directMaskingRoughness := (directRoughness + 1) * 0.5f.
        let directMaskingAlpha := directMaskingRoughness*directMaskingRoughness.
        LightingParameters()
            P: P;
            V: V;
            N: N;
            surfaceNormal: surfaceNormal;
            screenPosition: screenPosition;
            emission: emission;
            Cdiff: Cdiff;
            F0: F0;
            occlusion: occlusion;
            alpha: alpha;
            directAlpha: directAlpha;
            directMaskingAlpha: directMaskingAlpha;
            opacityFactor: opacityFactor;
            yourself
    }.
}.

class ForwardSceneRenderingShaderSignature; definition: {

    ## Poisson sampling disk from http://www.opengl-tutorial.org/es/intermediate-tutorials/tutorial-16-shadow-mapping/#pcf
    let shadowPCFSampleCount := 4.
    let shadowDisplacementRadius := 1.5f.
    let shadowDisplacementOffsets := Float32x2 array(
        Float32x2( -0.94201624f,  -0.39906216f ),
        Float32x2(  0.94558609f,  -0.76890725f ),
        Float32x2( -0.094184101f, -0.92938870f ),
        Float32x2(  0.34495938f,   0.29387760f )
    ).

    method sampleShadowPoint: (shadowPoint: Float32x3) depthReference: (depthReference: Float32) rotation: (rotation: Float32x2x2) displacementIndex: (displacementIndex: Int32) ::=> Float32 := {
        let displacement := (rotation*shadowDisplacementOffsets[displacementIndex])*shadowDisplacementRadius.

        (self shadowSampler sample: self lightingStateBinding _ spotShadowMaps
            at: shadowPoint + Float32x3(displacement * self globalLightingState shadowMapInverseExtent, 0)
            depth: depthReference)
    }.

    method computeShadowFactorFor: (shadowPoint: Float32x3) depthReference: (depthReference: Float32) ::=> Float32 := {
        shadowPCFSampleCount <= 1 ifTrue: {
            (self shadowSampler sample: self lightingStateBinding _ spotShadowMaps at: shadowPoint depth: depthReference)
        } ifFalse: {
            let rotation := shadowMapRandomRotation(shadowPoint xy).
            ((self sampleShadowPoint: shadowPoint depthReference: depthReference rotation: rotation displacementIndex: 0) +
            (self sampleShadowPoint: shadowPoint depthReference: depthReference rotation: rotation displacementIndex: 1) +
            (self sampleShadowPoint: shadowPoint depthReference: depthReference rotation: rotation displacementIndex: 2) +
            (self sampleShadowPoint: shadowPoint depthReference: depthReference rotation: rotation displacementIndex: 3)) * 0.25f
        }
    }.

    method computeForwardLightingWith: (lightingParameters: MetallicRoughnessLightingParameters) ::=> Float32x4 := {
        self computeForwardLightingWith: lightingParameters asGenericLightingParameters
    }.

    method computeForwardLightingWith: (lightingParameters: LightingParameters) ::=> Float32x4 := {
        let P := lightingParameters P.
        let V := lightingParameters V.
        let N := lightingParameters N.
        let surfaceNormal := lightingParameters surfaceNormal.

        let emission := lightingParameters emission.
        let Cdiff := lightingParameters Cdiff.
        let F0 := lightingParameters F0.
        let alpha := lightingParameters alpha.
        let directAlpha := lightingParameters directAlpha.
        let directMaskingAlpha := lightingParameters directMaskingAlpha.

        let diff := Cdiff * Float32 piReciprocal.
        let NdotV := (N dot: V) clampMin: 0.0f max: 1.0f.

        ## Ambient lighting
        let hemiFactor := (N dot: self globalLightingState sunDirection)*0.5f + 0.5f.
        let hemiLightingFactor := self globalLightingState groundLighting
            interpolateTo: self globalLightingState skyLighting
            at: hemiFactor.

        let result mutable := hemiLightingFactor rgb * (Cdiff + F0).

        ## Apply the ambient occlusion.
        let screenCoordinate := (lightingParameters screenPosition xy / lightingParameters screenPosition w) * 0.5f + 0.5f.
        let ssao := (self albedoSampler sample: self lightingStateBinding _ ssaoTexture at: screenCoordinate) r.
        let occlusion := lightingParameters occlusion * ssao.
        result := result * occlusion.

        ## Add the emission.
        result := result + emission.

        ## Opacity factor
        let opacityWithFresnel := fresnelSchlick(lightingParameters opacityFactor, NdotV).

        ## Accumulate the different lights.
        0 until: self globalLightingState numberOfLights do: {:i :: Void |
            let lightSource ref := self globalLightingState lightSources[i].

            let L mutable := lightSource position xyz.
            let dist mutable := 0.0f.
            let isDirectional := lightSource position w = 0.
            isDirectional ifFalse: {
                L := L - P.
                dist := L length.
                L := L / dist.
            }.

            let NdotL := (N dot: L) max: 0.0f.
            NdotL > 0.0f ifTrue: {

                let attenuation mutable := 1.0f.
                isDirectional ifFalse: {
                    attenuation := computeLightFalloff(dist, lightSource radius).

                    ## Apply the spot attenuation factor.
                    lightSource outerCosCutoff > -1.0f ifTrue: {
                        let spotCos := L dot: lightSource spotDirection.
                        spotCos < lightSource outerCosCutoff ifTrue: {
                            attenuation := 0.0f.
                        } ifFalse: {
                            let spotAttenuation := (lightSource outerCosCutoff smoothStepTo: lightSource innerCosCutoff value: spotCos) *
                                spotCos raisedTo: lightSource spotExponent.
                            attenuation := attenuation * spotAttenuation
                        }
                    }.
                }.

                attenuation > 0.0f && lightSource shadowMapLayer >= 0.0f ifTrue: {
                    let shadowFactor mutable := 1.0f.
                    let cameraShadowPosition := Float32x4(P + surfaceNormal*0.01f, 1.0f).

                    let cascadeSplitOffsets := lightSource cascadeSplitOffsets.
                    let cascadeDistance := lightSource cascadeFrustumPlane dot: cameraShadowPosition.

                    cascadeDistance < cascadeSplitOffsets y ifTrue: {
                        cascadeDistance < cascadeSplitOffsets x ifTrue: {
                            let shadowPosition := lightSource shadowMapTransform * cameraShadowPosition.
                            let projectedShadowPoint := projectShadowPosition(shadowPosition, lightSource shadowMapLayer).
                            shadowFactor := self computeShadowFactorFor: projectedShadowPoint xyz depthReference: projectedShadowPoint w.
                        } ifFalse: {
                            let shadowPosition := lightSource shadowMapTransform2 * cameraShadowPosition.
                            let projectedShadowPoint := projectShadowPosition(shadowPosition, lightSource shadowMapLayer + 1).
                            shadowFactor := self computeShadowFactorFor: projectedShadowPoint xyz depthReference: projectedShadowPoint w.
                        }.
                    } ifFalse: {
                        cascadeDistance < cascadeSplitOffsets z ifTrue: {
                            let shadowPosition := lightSource shadowMapTransform3 * cameraShadowPosition.
                            let projectedShadowPoint := projectShadowPosition(shadowPosition, lightSource shadowMapLayer + 2).
                            shadowFactor := self computeShadowFactorFor: projectedShadowPoint xyz depthReference: projectedShadowPoint w.
                        } ifFalse: {
                            let shadowPosition := lightSource shadowMapTransform4 * cameraShadowPosition.
                            let projectedShadowPoint := projectShadowPosition(shadowPosition, lightSource shadowMapLayer + 3).
                            shadowFactor := self computeShadowFactorFor: projectedShadowPoint xyz depthReference: projectedShadowPoint w.
                        }.
                    }.

                    attenuation := attenuation * shadowFactor
                }.

                attenuation > 0.0f ifTrue: {
                    ## Compute the specular input factors.
                    let H := (L + V) normalized.
                    let NdotH := (N dot: H) max: 0.0f.
            		let VdotH := (V dot: H) max: 0.0f.

                    let F := fresnelSchlick(F0, VdotH).
                    let D := specularDistribution(directAlpha, NdotH).
                    let G := cookTorranceMasking(directMaskingAlpha, NdotL, NdotV).

                    result := result + lightSource intensity rgb * (diff + F*D*G) * (attenuation * Float32 pi * NdotL).
                }.
            }.
        }.

        Float32x4(result, opacityWithFresnel)
    }.
}.

}. ## End of namespace Shaders
}. ## End of namespace Woden
