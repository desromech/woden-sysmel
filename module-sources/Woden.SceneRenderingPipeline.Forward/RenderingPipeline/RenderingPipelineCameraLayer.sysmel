namespace Woden definition: {
namespace ForwardRenderer definition: {

useNamespace: Woden Runtime.
useNamespace: Std Graphics Core.
useNamespace: Std Math Geometry.
useNamespace: Std Math LinearAlgebra.

class RenderingPipelineCameraLayer definition: {
    public field cameraTransform type: ReversibleAffineTransform3dF32.
    public field cameraState type: Woden Shaders CameraStateData.
    public field frustum type: FrustumF32.
    public field sceneRenderingPipeline type: ForwardSceneRenderingPipelineRef.

    public field renderableSceneObjects type: Std Collections Vector(ViewportLayerRenderableObject).

    public field cameraStateBuffers type: FrameBufferedBuffer.
    public field cameraStateBindings type: FrameBufferedShaderResourcesBindings.
    public field owner type: RenderingPipelineCameraEye pointer.

    const inline method scene => SceneRef const ref
        := owner _ scene.

    const inline method engine => EngineRef
        := sceneRenderingPipeline engine.

    virtual method ensureResourcesAreCreatedFor: (theSceneRenderingPipeline: ForwardSceneRenderingPipelineRef const ref) owner: (theOwner: RenderingPipelineCameraEye pointer) ::=> Void := {
        owner := theOwner.
        sceneRenderingPipeline := theSceneRenderingPipeline.

        ## Create the camera state buffer and binding.
        cameraStateBuffers isCreated ifFalse: {
            cameraStateBuffers for: self engine createWithDescription: (BufferDescription()
                size: Woden Shaders CameraStateData instanceSize;
                heapType: MemoryHeapType HostToDevice;
                usageModes: BufferUsageMode Uniform;
                mainUsageMode: BufferUsageMode Uniform;
                mappingFlags: BufferMappingFlags DynamicStorage;
                yourself
            ) initialData: nil.

            cameraStateBindings for: self engine createForShaderSignature: sceneRenderingPipeline shaderSignature bindingSet: 1.
            cameraStateBindings bindings doWithIndex: {:each :index :: Void |
                each bindOn: 0 uniformBuffer: cameraStateBuffers buffers[index]
            }.
        }.
    }.

    method sortRenderingObjects => Void := {
        renderableSceneObjects sort
    }.

    method uploadCameraState => Void := {
        cameraStateBuffers current uploadAt: 0 size: Woden Shaders CameraStateData instanceSize data: cameraState address.
    }.

    method renderSceneObject: (renderObject: RenderingSceneRenderableRef const ref) with: (context: SceneRenderingContext ref) ::=> Void := {
        renderObject spatialObject isGPUSpatialObject ifFalse: {return: void}.

        let spatialObject := renderObject spatialObject downCastFor: AbstractGPUSpatialObject.
        spatialObject objectStateBuffers isCreated ifFalse: {
            spatialObject objectStateBuffers for: self engine createWithDescription: (BufferDescription()
                size: Woden Shaders ObjectStateData instanceSize;
                heapType: MemoryHeapType HostToDevice;
                usageModes: BufferUsageMode Uniform;
                mainUsageMode: BufferUsageMode Uniform;
                mappingFlags: BufferMappingFlags DynamicStorage;
                yourself
            ) initialData: nil.

            spatialObject objectStateBindings for: self engine createForShaderSignature: sceneRenderingPipeline shaderSignature bindingSet: 3.
            spatialObject objectStateBindings bindings doWithIndex: {:each :index :: Void |
                each bindOn: 0 uniformBuffer: spatialObject objectStateBuffers buffers[index]
            }.
        }.

        spatialObject objectStateBuffers current
            uploadAt: 0 size: Woden Shaders ObjectStateData instanceSize data: spatialObject state address.

        context stateTracker
            useGraphicShaderBindings: spatialObject objectStateBindings current.

        renderObject renderable renderWithContext: context.
    }.

    virtual method renderSceneObjectsWith: (context: SceneRenderingContext ref) ::=> Void := {
        context stateTracker
            useGraphicShaderBindings: cameraStateBindings current.

        renderableSceneObjects do: {:each :: Void |
            context resetObjectAttributes.
            self renderSceneObject: each object with: context
        }.
    }.
}.


}. ## End of namespace ForwardRenderer
}. ## End of namespace Woden
