namespace Woden definition: {
namespace ForwardRenderer definition: {

useNamespace: Woden Runtime.
useNamespace: Std Graphics Core.
useNamespace: Std Math Geometry.

#**
 * An abstract pipeline for rendering a null scene.
 *#
class ForwardSceneRenderingPipeline superclass: SceneRenderingPipeline; definition: {
    compileTime constant ShadowMapTextureLayers := 8u.
    
    public field shaderSignature type: ShaderSignatureRef.
    protected field sampler type: SamplerRef.
    protected field cubeMapSampler type: SamplerRef.
    protected field deferredSampler type: SamplerRef.
    public field samplingStateBinding type: ShaderResourceBindingsRef.

    public field specularBRDFLutTexture type: TextureRef.
    public field specularBRDFLutTextureView type: TextureViewRef.

    compileTime constant LuminanceComputationTextureCount := 9.
    let LuminanceComputationTextureSizes := UInt32x2 array(
        256, 128, 64, 32, 16, 8, 4, 2, 1
    ).

    public field luminanceComputationRenderPass type: RenderPassRef.
    public field luminanceComputationFramebuffers type: (FramebufferRef array: LuminanceComputationTextureCount).
    public field luminanceComputationMaterialBindings type: (ShaderResourceBindingsRef array: LuminanceComputationTextureCount).

    protected field shadowMapSampler type: SamplerRef.
    protected field shadowMapTextureSize type: UInt32.
    public field shadowMapTexture type: TextureRef.
    public field shadowMapTextureView type: TextureViewRef.
    public field shadowMapRenderPass type: RenderPassRef.
    public field shadowMapFramebuffers type: (FramebufferRef array: ShadowMapTextureLayers).

    method luminanceComputationResultMaterialBindings => ShaderResourceBindingsRef const ref
        := luminanceComputationMaterialBindings[LuminanceComputationTextureCount - 1].


    const inline method shadowMapTextureSize => UInt32
        := shadowMapTextureSize.

    const inline method shadowMapTextureExtent => UInt32x2
        := UInt32x2(shadowMapTextureSize, shadowMapTextureSize).

    override method defaultClearDepth => Float32
        := 0.0f.

    override method initializeForEngine: (anEngine: EngineRef const ref) ::=> Void := {
        super initializeForEngine: anEngine.

        let device := anEngine renderingDevice.
        let renderingSettings ref := anEngine renderingSettings.
        shadowMapTextureSize := renderingSettings shadowMapSize max: 256.

        ## Get or create the shader signature.
        {
            let shaderSignatureDescription := ShaderSignatureDescription()
            ## Sampling state.
            addBank: (ShaderSignatureBankDescription()
                maxBindingPointCountHint: 1;
                addElementWithType: ShaderBindingType Sampler;
                addElementWithType: ShaderBindingType Sampler;
                addElementWithType: ShaderBindingType Sampler;
                addElementWithType: ShaderBindingType Sampler;
                addElementWithType: ShaderBindingType Sampler;
                addElementWithType: ShaderBindingType Sampler;
                addElementWithType: ShaderBindingType Sampler;
                yourself
            );

            ## CameraState.
            addBank: (ShaderSignatureBankDescription()
                addElementWithType: ShaderBindingType UniformBuffer;
                addElementWithType: ShaderBindingType StorageBuffer;
                yourself
            );

            ## LightingState.
            addBank: (ShaderSignatureBankDescription()
                ## State
                addElementWithType: ShaderBindingType UniformBuffer;
                ## spotShadowMaps
                addElementWithType: ShaderBindingType SampledImage;

                ## pointShadowMaps
                addElementWithType: ShaderBindingType SampledImage;
                ## screenSpaceAmbientOcclusion
                addElementWithType: ShaderBindingType SampledImage;
                ## specularBRDFLut
                addElementWithType: ShaderBindingType SampledImage;
                ## diffuseIrradianceMap
                addElementWithType: ShaderBindingType SampledImage;
                ## specularIrradianceMap
                addElementWithType: ShaderBindingType SampledImage;
                yourself
            );

            ## ObjectState.
            addBank: (ShaderSignatureBankDescription()
                addElementWithType: ShaderBindingType UniformBuffer;
                yourself
            );

            ## RenderableState.
            addBank: (ShaderSignatureBankDescription()
                addElementWithType: ShaderBindingType UniformBuffer;
                yourself
            );

            ## MaterialState.
            addBank: (ShaderSignatureBankDescription()
                addElementWithType: ShaderBindingType UniformBuffer;
                addElementWithType: ShaderBindingType SampledImage;
                addElementWithType: ShaderBindingType SampledImage;
                addElementWithType: ShaderBindingType SampledImage;
                addElementWithType: ShaderBindingType SampledImage;
                addElementWithType: ShaderBindingType SampledImage;
                yourself
            );
            yourself.

            shaderSignature := device getDefaultShaderSignatureCache getOrCreateWithDescription: shaderSignatureDescription.
        }.

        ## Create the sampling state binding.
        {
            let anisotropy := renderingSettings maxAnisotropy max: 1u.
            let anisotropicFilter := anisotropy > 1
                ifTrue: (TextureFilter Anisotropic)
                ifFalse: (TextureFilter MinLinear | TextureFilter MagLinear | TextureFilter MipmapLinear).

            sampler := device createSampler: (SamplerDescription()
                filter: anisotropicFilter;
                addressU: TextureAddressMode Wrap;
                addressV: TextureAddressMode Wrap;
                addressW: TextureAddressMode Wrap;
                maxLod: 10000.0f;
                maxAnisotropy: anisotropy;
                yourself).

            samplingStateBinding := shaderSignature createShaderResourceBindings: 0.
            0 until: 6 do: {:(UInt32)i :: Void |
                samplingStateBinding bindOn: i sampler: sampler.
            }.

            shadowMapSampler := device createSampler: (SamplerDescription()
                filter: TextureFilter MinLinear | TextureFilter MagLinear | TextureFilter MipmapNearest;
                addressU: TextureAddressMode Clamp;
                addressV: TextureAddressMode Clamp;
                addressW: TextureAddressMode Clamp;
                comparisonEnabled: true;
                comparisonFunction: CompareFunction GreaterThan;
                maxLod: 10000.0f;
                yourself).
            samplingStateBinding bindOn: 3 sampler: shadowMapSampler.

            cubeMapSampler := device createSampler: (SamplerDescription()
                filter: anisotropicFilter;
                addressU: TextureAddressMode Border;
                addressV: TextureAddressMode Border;
                addressW: TextureAddressMode Border;
                maxAnisotropy: anisotropy;
                yourself).
            samplingStateBinding bindOn: 4 sampler: cubeMapSampler.

            deferredSampler := device createSampler: (SamplerDescription()
                filter: TextureFilter MinLinear | TextureFilter MagLinear | TextureFilter MipmapNearest;
                addressU: TextureAddressMode Clamp;
                addressV: TextureAddressMode Clamp;
                addressW: TextureAddressMode Clamp;
                maxLod: 10000.0f;
                yourself).
            samplingStateBinding bindOn: 6 sampler: deferredSampler.

        }.

        ## Create the specularBRDFLut texture.
        {
            specularBRDFLutTexture := device createTexture: (TextureDescription()
                type: TextureType Texture2D;
                width: 512;
                height: 512;
                depth: 1;
                layers: 1;
                miplevels: 1;
                format: PixelFormat R16G16_UNorm;
                usageModes: TextureUsageMode Sampled | TextureUsageMode ColorAttachment;
                mainUsageMode: TextureUsageMode Sampled;
                heapType: MemoryHeapType DeviceLocal;
                sampleCount: 1;
                yourself).

            specularBRDFLutTextureView := specularBRDFLutTexture getOrCreateFullView.
            ## TODO: generate the content of the lut texture.
        }.

        ## Create the luminance computation textures and framebuffers.
        {
            LuminanceComputationTextureSizes doWithIndex: {:textureExtent :i :: Void |
                let luminanceIntermediateTexture := device createTexture: (TextureDescription()
                    type: TextureType Texture2D;
                    width: textureExtent x;
                    height: textureExtent y;
                    depth: 1;
                    layers: 1;
                    miplevels: 1;
                    format: PixelFormat R16_Float;
                    usageModes: TextureUsageMode Sampled | TextureUsageMode ColorAttachment;
                    mainUsageMode: TextureUsageMode Sampled;
                    heapType: MemoryHeapType DeviceLocal;
                    sampleCount: 1;
                    yourself).
                let luminanceIntermediateTextureView := luminanceIntermediateTexture getOrCreateFullView.

                let colorAttachments mutable := TextureViewRef array(luminanceIntermediateTextureView).
                luminanceComputationFramebuffers[i] := device createFramebuffer: textureExtent colorViews: colorAttachments depthStencilView: TextureViewRef nil.

                luminanceComputationMaterialBindings[i] := shaderSignature createShaderResourceBindings: 5.
                luminanceComputationMaterialBindings[i]
                    bindOn: 1 sampledTextureView: luminanceIntermediateTextureView.
            }.
        }.

        {
            let colorAttachments mutable := RenderPassColorAttachmentDescription array (
                RenderPassColorAttachmentDescription()
                    format: PixelFormat R16_Float;
                    beginAction: RenderPassAttachmentAction Clear;
                    endAction: RenderPassAttachmentAction Keep;
                    yourself
            ).

            luminanceComputationRenderPass := device createRenderPass: colorAttachments depthStencilAttachment: nil.
        }.

        ## Create the shadow map texture and associated framebuffers.
        {
            let extent := self shadowMapTextureExtent.
            shadowMapTexture := device createTexture: (TextureDescription()
                type: TextureType Texture2D;
                width: extent x;
                height: extent y;
                depth: 1;
                layers: ShadowMapTextureLayers;
                miplevels: 1;
                format: PixelFormat R32_Typeless;
                usageModes: TextureUsageMode Sampled | TextureUsageMode DepthAttachment;
                mainUsageMode: TextureUsageMode Sampled;
                heapType: MemoryHeapType DeviceLocal;
                sampleCount: 1;
                yourself).

            let shadowMapTextureViewDescription mutable := shadowMapTexture getFullViewDescription.
            shadowMapTextureViewDescription format: PixelFormat R32_Float.
            shadowMapTextureView := shadowMapTexture createView: shadowMapTextureViewDescription.

            0 until: ShadowMapTextureLayers do: {:(UInt32)i :: Void |
                let layerDescription mutable := shadowMapTexture getFullViewDescription.
                layerDescription
                    format: PixelFormat D32_Float;
                    usageMode: TextureUsageMode DepthAttachment;
                    subresourceRange: (TextureSubResourceRange()
                        aspect: TextureAspect Depth;
                        baseMipLevel: 0;
                        levelCount: 1;
                        baseArrayLayer: i;
                        layerCount: 1;
                        yourself).

                let layerView := shadowMapTexture createView: layerDescription.
                shadowMapFramebuffers[i] := device createFramebuffer: extent colorViews: TextureViewRef const arraySlice() depthStencilView: layerView.
            }.
        }.

        {
            let depthStencilAttachment mutable := RenderPassDepthStencilAttachmentDescription()
                format: PixelFormat D32_Float;
                beginAction: RenderPassAttachmentAction Clear;
                endAction: RenderPassAttachmentAction Keep;
                yourself.

            shadowMapRenderPass := device createRenderPass: RenderPassColorAttachmentDescription const arraySlice() depthStencilAttachment: depthStencilAttachment address.
        }.
    }.

    override method createSceneRenderingPipelineViewport => SceneRenderingPipelineViewportRef
        := ForwardSceneRenderingPipelineViewport new
            initializeWithSceneRenderingPipeline: self asRCRef;
            upCastFor: SceneRenderingPipelineViewport.

    method screenQuadShader => GPU ShaderEntryPointInfo const pointer := {
        self engine renderingDevice shouldInvertProjectionY ifTrue: {
            Woden Shaders CompositionMaterialShaders screenQuad shaderEntryPointInfo address
        } ifFalse: {
            Woden Shaders CompositionMaterialShaders screenQuadFlippedY shaderEntryPointInfo address
        }.
    }.

    method performAverageLuminanceComputationWith: (stateTracker: StateTrackerRef const ref) ::=> Void := {
        ## Color to luma conversion
        stateTracker
            beginRenderPass: luminanceComputationRenderPass on: luminanceComputationFramebuffers first contentOnBundle: false;
            setViewport: (RectangleI32 min: Int32x2 zeros max: (LuminanceComputationTextureSizes first castTo: Int32x2));
            setScissor: (RectangleI32 min: Int32x2 zeros max: (LuminanceComputationTextureSizes first castTo: Int32x2));

            vertexShader: self screenQuadShader;
            fragmentShader: Woden Shaders ToneMappingMaterialShaders colorToLuminanceConversion shaderEntryPointInfo address;
            primitiveTopology: PrimitiveTopology Triangles;
            drawArrays: 3 instanceCount: 1 firstVertex: 0 baseInstance: 0;

            endRenderPass;

            fragmentShader: Woden Shaders ToneMappingMaterialShaders luminanceDownSample shaderEntryPointInfo address.

        ## Down sampling until single pixel.
        1 until: LuminanceComputationTextureCount do: {:i :: Void |

            let extent := LuminanceComputationTextureSizes[i].
            stateTracker
                useGraphicShaderBindings: luminanceComputationMaterialBindings[i - 1];
                beginRenderPass: luminanceComputationRenderPass on: luminanceComputationFramebuffers[i] contentOnBundle: false;
                setViewport: (RectangleI32 min: Int32x2 zeros max: (extent castTo: Int32x2));
                setScissor: (RectangleI32 min: Int32x2 zeros max: (extent castTo: Int32x2));

                vertexShader: self screenQuadShader;
                drawArrays: 3 instanceCount: 1 firstVertex: 0 baseInstance: 0;
                endRenderPass.
        }.
    }.

    method performCameraAutoExposureStateComputationWith: (stateTracker: StateTrackerRef const ref) ::=> Void := {
        stateTracker
            computeShader: Woden Shaders ToneMappingMaterialShaders cameraAutoExposureStateComputation shaderEntryPointInfo address;
            dispatchCompute: 1.
    }.
}.

compileTime constant ForwardSceneRenderingPipelineRef := ForwardSceneRenderingPipeline rcRef.

}. ## End of namespace ForwardRenderer
}. ## End of namespace Woden
