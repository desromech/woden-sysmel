namespace Woden definition: {
namespace MapDefinition definition: {

useNamespace: Std Math.
useNamespace: Std Math LinearAlgebra.
useNamespace: Std Math Geometry.

class EntityDefinition superclass: LevelElementDefinition; definition: {
    public field className type: Std String.
    public field transform type: DecomposedTransform3dF32.
    public field localBoundingBox type: BoxF32.
    public field components type: Std Collections Vector(EntityComponentDefinitionPtr).
    public field is2DEntity type: Boolean8.

    const inline method position => Float32x3
        := transform translation.

    inline method position: (position: Float32x3) ::=> Void
        := transform translation: position.

    const inline method orientation => QuaternionF32
        := transform rotation.

    inline method orientation: (orientation: QuaternionF32) ::=> Void
        := transform rotation: orientation.

    const inline method scale => Float32x3
        := transform scale.

    inline method scale: (scale: Float32x3) ::=> Void
        := transform scale: scale.

    method positionForLayer: (layer: SectorLayerDefinition rcPointer const ref) ::=> Float32x3 := {
        is2DEntity
            ifTrue: {Float32x3(self position xy, layer _ defaultZ)}
            ifFalse: {self position}
    }.

    override method accept: (visitor: LevelDefinitionVisitor ref) ::=> Void
        := visitor visitEntityDefinition: self.

    override method isEntity => Boolean8 := true.

    method localBoundingRectangle: (rectangle: RectangleF32 const ref) ::=> Void := {
        localBoundingBox := BoxF32 min: Float32x3(rectangle min, 0) max: Float32x3(rectangle max, 0)
    }.

    method addComponent: (component: EntityComponentDefinitionPtr const ref) ::=> Void := {
        components add: component
    }.

    inline method addVisualComponent: (visualComponent: VisualComponent rcPointer const ref) ::=> Void
        := self addComponent: (visualComponent upCastFor: EntityComponentDefinition).

    inline method addCollisionShape: (collisionShape: CollisionShape rcPointer const ref) ::=> Void
        := self addComponent: (collisionShape upCastFor: EntityComponentDefinition).
}.

compileTime constant EntityDefinitionPtr := EntityDefinition rcPointer.

}. ## namespace MapDefinition
}. ## namespace Woden
