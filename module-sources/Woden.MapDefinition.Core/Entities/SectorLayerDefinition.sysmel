namespace Woden definition: {
namespace MapDefinition definition: {

useNamespace: Std Math Geometry.

class SectorLayerDefinition superclass: LevelElementDefinition; definition: {
    public field entities type: Std Collections Vector(EntityDefinitionRef).
    public field layerMinZ type: Float32.
    public field layerMaxZ type: Float32.

    override method accept: (visitor: LevelDefinitionVisitor ref) ::=> Void
        := visitor visitSectorLayerDefinition: self.

    override method isSectorLayer => Boolean8 := true.

    const inline method defaultZ => Float32
        := layerMinZ + self defaultHalfThickness.

    const inline method defaultThickness => Float32
        := layerMaxZ - layerMinZ.

    const inline method defaultHalfThickness => Float32
        := self defaultThickness * 0.5f.

    inline method defaultThickness: (thickness: Float32) ::=> Void := {
        layerMinZ := thickness * -0.5f.
        layerMaxZ := thickness * 0.5f.
    }.

    method addEntity: (entity: EntityDefinitionRef const ref) ::=> Void := {
        entities add: entity.
    }.

    method addPlayerStartPoint: (position: Float32x2) ::=> Void := {
        self addEntity: (EntityDefinition new
            is2DEntity: true;
            className: "PlayerStartPoint";
            position: Float32x3(position, 0.0f);
            yourself).
    }.

    method addPlayerStartPoint: (position: Float32x3) ::=> Void := {
        self addEntity: (EntityDefinition new
            className: "PlayerStartPoint";
            position: position;
            yourself).
    }.

    method addEnemyAt: (position: Float32x2) ::=> Void := {
        self addEntity: (EntityDefinition new
            is2DEntity: true;
            className: "Enemy";
            position: Float32x3(position, 0.0f);
            yourself).
    }.

    method addEnemyAt: (position: Float32x3) ::=> Void := {
        self addEntity: (EntityDefinition new
            className: "Enemy";
            position: position;
            yourself)
    }.

    method addPointLightAt: (position: Float32x2) color: (color: Float32x3)
        intensity: (intensity: Float32) radius: (radius: Float32) castShadows: (castShadows: Boolean8) ::=> Void := {
        self addEntity: (EntityDefinition new
            is2DEntity: true;
            className: "PointLight";
            addVisualComponent: (VisualComponent pointLightSourceWithColor: color intensity: intensity radius: radius castShadows: castShadows);
            position: Float32x3(position, 0.0f);
            yourself)
    }.

    method addPointLightAt: (position: Float32x3) color: (color: Float32x3)
        intensity: (intensity: Float32) radius: (radius: Float32) castShadows: (castShadows: Boolean8) ::=> Void := {
        self addEntity: (EntityDefinition new
            className: "PointLight";
            addVisualComponent: (VisualComponent pointLightSourceWithColor: color intensity: intensity radius: radius castShadows: castShadows);
            position: position;
            yourself)
    }.

    method createWallMin: (min: Float32x2) max: (max: Float32x2) color: (color: Float32x4) ::=> Void := {
        let boundingRectangle := RectangleF32 min: min max: max.
        let center := boundingRectangle center.

        let localRectangle := boundingRectangle translatedBy: center negated.
        self addEntity: (EntityDefinition new
            is2DEntity: true;
            className: "Wall";
            position: Float32x3(center, 0);
            localBoundingRectangle: localRectangle;
            addVisualComponent: (VisualComponent rectangle: localRectangle color: color);
            addCollisionShape: (CollisionShape rectangleWithHalfExtent: localRectangle halfExtent);
            yourself)
    }.

    method createWallMin: (min: Float32x3) max: (max: Float32x3) color: (color: Float32x4) ::=> Void := {
        let boundingBox := BoxF32 min: min max: max.
        let center := boundingBox center.

        let localBox := boundingBox translatedBy: center negated.
        self addEntity: (EntityDefinition new
            className: "Wall";
            position: center;
            localBoundingBox: localBox;
            addVisualComponent: (VisualComponent box: localBox color: color);
            addCollisionShape: (CollisionShape boxWithHalfExtent: localBox halfExtent);
            yourself)
    }.

    method createNonSolidWallMin: (min: Float32x2) max: (max: Float32x2) color: (color: Float32x4) ::=> Void := {
        let boundingRectangle := RectangleF32 min: min max: max.
        let center := boundingRectangle center.

        let localRectangle := boundingRectangle translatedBy: center negated.
        self addEntity: (EntityDefinition new
            is2DEntity: true;
            className: "Wall";
            position: Float32x3(center, 0);
            localBoundingRectangle: localRectangle;
            addVisualComponent: (VisualComponent rectangle: localRectangle color: color);
            yourself)
    }.

    method createNonSolidWallMin: (min: Float32x3) max: (max: Float32x3) color: (color: Float32x4) ::=> Void := {
        let boundingBox := BoxF32 min: min max: max.
        let center := boundingBox center.

        let localBox := boundingBox translatedBy: center negated.
        self addEntity: (EntityDefinition new
            className: "Wall";
            position: center;
            localBoundingBox: localBox;
            addVisualComponent: (VisualComponent box: localBox color: color);
            yourself)
    }.
}.

compileTime constant SectorLayerDefinitionRef := SectorLayerDefinition rcRef.

}. ## namespace MapDefinition
}. ## namespace Woden
