namespace Woden definition: {
namespace MapDefinition definition: {

class LevelDefinition superclass: LevelElementDefinition; definition: {
    public field sectors type: Std Collections Vector(SectorDefinitionRef).

    meta definition: {
        static method simpleSampleLevel => LevelDefinition rcRef := {
            let backgroundLayer := SectorLayerDefinition new
                layerMinZ: -3.0f;
                layerMaxZ: -2.0f;
                createNonSolidWallMin: Float32x2(-5.0f, -5.0f) max: Float32x2(20.0f, 8.0f) color: Float32x4(0.1f, 0.1f, 0.1f, 1.0f);
                yourself.

            let mainLayer := SectorLayerDefinition new
                defaultThickness: 4.0f;
                addPlayerStartPoint: Float32x2 zeros;
                addEnemyAt: Float32x2(4.0f, 2.0f);
                addEnemyAt: Float32x2(15.0f, 0.0f);
                createWallMin: Float32x2(-5.0f, -5.0f) max: Float32x2(20.0f, -4.0f) color: Float32x4(0.2f, 0.2f, 0.2f, 1.0f);
                createWallMin: Float32x2(3.0f, -1.0f) max: Float32x2(8.0f, 0.0f) color: Float32x4(0.2f, 0.2f, 0.2f, 1.0f);
                yourself.

            let lightingLayer := SectorLayerDefinition new
                layerMinZ: 2.0f;
                layerMaxZ: 3.0f;
                addPointLightAt: Float32x2(8.0f, 4.0f) color: Float32x3 ones intensity: 50.0 radius: 50 castShadows: false;
                yourself.

            let mainSector := SectorDefinition new
                isSector2D: true;
                ##gravity: Float32x3(0, -9.8f, 0);
                addLayer: backgroundLayer;
                addLayer: mainLayer;
                addLayer: lightingLayer;
                yourself.

            LevelDefinition new
                addSector: mainSector;
                yourself
        }.
    }.

    override method accept: (visitor: LevelDefinitionVisitor ref) ::=> Void
        := visitor visitLevelDefinition: self.

    override method isLevel => Boolean8 := true.

    method addSector: (sector: SectorDefinitionRef) ::=> Void := {
        sectors add: sector
    }.
}.

compileTime constant LevelDefinitionRef := LevelDefinition rcRef.

}. ## namespace MapDefinition
}. ## namespace Woden
