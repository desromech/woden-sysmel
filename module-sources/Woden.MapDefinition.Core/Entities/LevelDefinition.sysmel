namespace Woden definition: {
namespace MapDefinition definition: {

class LevelDefinition superclass: LevelElementDefinition; definition: {
    public field sectors type: Stdn Collections Vector(SectorDefinitionPtr).

    meta definition: {
        static method simpleSampleLevel => LevelDefinition sharedPointer := {
            let backgroundLayer := SectorLayerDefinition sharedNew.
            backgroundLayer _
                defaultMinZ: -3.0f;
                defaultMaxZ: -2.0f;
                createNonSolidWallMin: Float32x2(-5.0f, -5.0f) max: Float32x2(20.0f, 8.0f).

            let mainLayer := SectorLayerDefinition sharedNew.
            mainLayer _
                defaultThickness: 4.0f;
                addPlayerStartPoint: Float32x2 zeros;
                addEnemyAt: Float32x2(4.0f, 2.0f);
                addEnemyAt: Float32x2(15.0f, 0.0f);
                addPointLightAt: Float32x2(8.0f, 4.0f) color: Float32x3 ones intensity: 50.0 radius: 50 castShadows: false;
                createWallMin: Float32x2(-5.0f, -5.0f) max: Float32x2(20.0f, -4.0f);
                createWallMin: Float32x2(3.0f, -1.0f) max: Float32x2(8.0f, 0.0f).

            let mainSector := SectorDefinition sharedNew.
            mainSector _
                isSector2D: true;
                ##gravity: Float32x3(0, -9.8f, 0);
                addLayer: backgroundLayer;
                addLayer: mainLayer.

            let level := LevelDefinition sharedNew.
            level _ addSector: mainSector.
            level
        }.
    }.

    override method accept: (visitor: LevelDefinitionVisitor ref) ::=> Void
        := visitor visitLevelDefinition: self.

    override method isLevel => Boolean8 := true.

    method addSector: (sector: SectorDefinitionPtr) ::=> Void := {
        sectors add: sector
    }.
}.

compileTime constant LevelDefinitionPtr := LevelDefinition sharedPointer.

}. ## namespace MapDefinition
}. ## namespace Woden
