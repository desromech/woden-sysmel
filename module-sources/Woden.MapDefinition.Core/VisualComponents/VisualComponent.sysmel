namespace Woden definition: {
namespace MapDefinition definition: {

useNamespace: Std Math Geometry.
useNamespace: Std Math LinearAlgebra.
useNamespace: Woden Runtime.

class VisualComponent superclass: EntityComponentDefinition; definition: {
    meta definition: {
        static method rectangle: (rectangle: RectangleF32) ::=> VisualComponent rcRef
            := self rectangle: rectangle material: MaterialDefinitionRef nil.

        static method rectangle: (rectangle: RectangleF32) color: (color: Float32x4) ::=> VisualComponent rcRef
            := self rectangle: rectangle material: (MetallicRoughnessMaterialDefinition baseColorFactor: color).

        static method rectangle: (rectangle: RectangleF32) material: (material: MaterialDefinitionRef const ref)::=> VisualComponent rcRef := {
            RectangleVisualComponent new
                rectangle: rectangle;
                material: material;
                upCastFor: VisualComponent
        }.

        static method box: (box: BoxF32) ::=> VisualComponent rcRef
            := self box: box material: MaterialDefinitionRef nil.

        static method box: (box: BoxF32) color: (color: Float32x4) ::=> VisualComponent rcRef
            := self box: box material: (MetallicRoughnessMaterialDefinition baseColorFactor: color).

        static method box: (box: BoxF32) material: (material: MaterialDefinitionRef const ref) ::=> VisualComponent rcRef := {
            BoxVisualComponent new
                box: box;
                material: material;
                upCastFor: VisualComponent
        }.

        static method pointLightSourceWithColor: (color: Float32x3) intensity: (intensity: Float32) radius: (radius: Float32) castShadows: (castShadows: Boolean8) ::=> VisualComponent rcRef := {
            PointLightSourceVisualComponent new
                color: color;
                intensity: intensity;
                radius: radius;
                castShadows: castShadows;
                upCastFor: VisualComponent
        }.
    }.

    override method accept: (visitor: LevelDefinitionVisitor ref) ::=> Void
        := visitor visitEntityComponentDefinition: self.

    override method isVisualComponent => Boolean8 := true.

    virtual method boundingBox => BoxF32
        := BoxF32 empty.

    virtual method createSceneNodeFor: (engine: EngineRef) parentTransform: (parentTransform: ReversibleAffineTransform3dF32 const ref) ::=> SceneNodeRef
        := SceneNodeRef nil.

    method createSceneNodeFor: (engine: EngineRef) ::=> SceneNodeRef
        := self createSceneNodeFor: engine parentTransform: ReversibleAffineTransform3dF32 identity.

    virtual method setupForLayer: (layerDefinition: SectorLayerDefinition rcRef const ref) ::=> Void
        := {}.
}.

compileTime constant VisualComponentRe := VisualComponent rcRef.

}. ## namespace MapDefinition
}. ## namespace Woden
