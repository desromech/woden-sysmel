namespace Woden definition: {
namespace MapDefinition definition: {

useNamespace: Stdn Math Geometry.
useNamespace: Stdn Math LinearAlgebra.
useNamespace: Woden Runtime.

class VisualComponent superclass: EntityComponentDefinition; definition: {
    meta definition: {
        static method rectangle: (rectangle: RectangleF32) ::=> VisualComponent sharedPointer
            := self rectangle: rectangle material: MaterialDefinitionPtr nil.

        static method rectangle: (rectangle: RectangleF32) color: (color: Float32x4) ::=> VisualComponent sharedPointer
            := self rectangle: rectangle material: (MetallicRoughnessMaterialDefinition baseColorFactor: color).

        static method rectangle: (rectangle: RectangleF32) material: (material: MaterialDefinitionPtr const ref)::=> VisualComponent sharedPointer := {
            let result := RectangleVisualComponent sharedNew.
            result _
                rectangle: rectangle;
                material: material.
            result upCastFor: VisualComponent
        }.

        static method box: (box: BoxF32) ::=> VisualComponent sharedPointer
            := self box: box material: MaterialDefinitionPtr nil.

        static method box: (box: BoxF32) color: (color: Float32x4) ::=> VisualComponent sharedPointer
            := self box: box material: (MetallicRoughnessMaterialDefinition baseColorFactor: color).

        static method box: (box: BoxF32) material: (material: MaterialDefinitionPtr const ref) ::=> VisualComponent sharedPointer := {
            let result := BoxVisualComponent sharedNew.
            result _
                box: box;
                material: material.
            result upCastFor: VisualComponent
        }.

        static method pointLightSourceWithColor: (color: Float32x3) intensity: (intensity: Float32) radius: (radius: Float32) castShadows: (castShadows: Boolean8) ::=> VisualComponent sharedPointer := {
            let result := PointLightSourceVisualComponent sharedNew.
            result _
                color: color;
                intensity: intensity;
                radius: radius;
                castShadows: castShadows.
            result upCastFor: VisualComponent
        }.
    }.

    override method accept: (visitor: LevelDefinitionVisitor ref) ::=> Void
        := visitor visitEntityComponentDefinition: self.

    override method isVisualComponent => Boolean8 := true.

    virtual method boundingBox => BoxF32
        := BoxF32 empty.

    virtual method createSceneNodeFor: (engine: EnginePtr) parentTransform: (parentTransform: ReversibleAffineTransform3dF32 const ref) ::=> SceneNodePtr
        := SceneNodePtr nil.

    method createSceneNodeFor: (engine: EnginePtr) ::=> SceneNodePtr
        := self createSceneNodeFor: engine parentTransform: ReversibleAffineTransform3dF32 identity.

    virtual method setupForLayer: (layerDefinition: SectorLayerDefinition sharedPointer const ref) ::=> Void
        := {}.
}.

compileTime constant VisualComponentPtr := VisualComponent sharedPointer.

}. ## namespace MapDefinition
}. ## namespace Woden
