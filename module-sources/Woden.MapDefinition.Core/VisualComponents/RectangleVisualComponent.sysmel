namespace Woden definition: {
namespace MapDefinition definition: {

useNamespace: Std Math Geometry.
useNamespace: Std Math LinearAlgebra.
useNamespace: Woden Runtime.

class RectangleVisualComponent superclass: VisualComponent2d; definition: {
    public field rectangle type: RectangleF32.
    public field materialAttributes type: FaceMaterialAttributes.

    method material: (material: MaterialDefinitionRef const ref) ::=> Void := {
        materialAttributes material: material
    }.

    override method boundingRectangle => RectangleF32
        := rectangle.

    field renderable type: RenderableRef.

    method getOrCreateRenderableFor: (engine: EngineRef) parentTransform: (parentTransform: ReversibleAffineTransform3dF32 const ref) ::=> RenderableRef := {
        renderable ifNotNil: {return: renderable}.

        materialAttributes material ifNil: {
            renderable := engine resourceCache unitCubeRenderable.
            return: renderable
        }.

        renderable := engine newGenericMeshBuilder
            currentMaterial: (materialAttributes material getOrCreateMaterialFor: engine);
            currentTransform: self boundingBox reversibleTransformToConvertUnitBoxToMe;

    		addCubeWithWidth: 1.0 height: 1.0 depth: 1.0;

    		## Use a planar texture coordinates
    		planeTextureTransform: materialAttributes textureTransform withModelTransform: parentTransform;
    		computeTangentsBitangents;
            renderable
    }.

    override method createSceneNodeFor: (engine: EngineRef) parentTransform: (parentTransform: ReversibleAffineTransform3dF32 const ref) ::=> SceneNodeRef := {
        let node := SpatialObjectNode new.

        materialAttributes material ifNil: {
            ## Make deformed rectangle for the non-material case.
            node addChild: (SpatialObjectNode new
                renderable: (self getOrCreateRenderableFor: engine parentTransform: parentTransform);
                transform: self boundingBox reversibleTransformToConvertUnitBoxToMe;
                upCastFor: SceneNode).

        } ifNotNil: {
            node renderable: (self getOrCreateRenderableFor: engine parentTransform: parentTransform).
        }.

        node upCastFor: SceneNode
    }.
}.

compileTime constant RectangleVisualComponentRef := RectangleVisualComponent rcRef.

}. ## namespace MapDefinition
}. ## namespace Woden
