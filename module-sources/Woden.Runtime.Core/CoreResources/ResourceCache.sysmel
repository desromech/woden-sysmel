namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Graphics GUI.


#**
 * I am core engine instance. I hold strong references to the core subsystem registries.
 *#
class ResourceCache superclass: Stdn SharedObject; definition: {
    private field engine type: EnginePtr.
    private field assetLocations type: Stdn Collections Vector(Stdn FileReference).

    const inline method engine => EnginePtr const ref
        := engine.

    private field defaultMaterial type: MaterialPtr.

    const inline method defaultMaterial => MaterialPtr const ref
        := defaultMaterial.

    private field checkboardMaterial type: MaterialPtr.

    const inline method checkboardMaterial => MaterialPtr const ref
        := checkboardMaterial.

    private field whiteTexture type: TextureWithViewPtr.

    const inline method whiteTexture => TextureWithViewPtr const ref
        := whiteTexture.

    private field blackTexture type: TextureWithViewPtr.

    const inline method blackTexture => TextureWithViewPtr const ref
        := blackTexture.

    private field transparentTexture type: TextureWithViewPtr.

    const inline method transparentTexture => TextureWithViewPtr const ref
        := transparentTexture.

    private field checkboardTexture type: TextureWithViewPtr.

    const inline method checkboardTexture => TextureWithViewPtr const ref
        := checkboardTexture.

    private field neutralNormalTexture type: TextureWithViewPtr.

    const inline method neutralNormalTexture => TextureWithViewPtr const ref
        := neutralNormalTexture.

    private field whiteTextureCube type: TextureWithViewPtr.

    const inline method whiteTextureCube => TextureWithViewPtr const ref
        := whiteTextureCube.

    method initializeWithEngine: (anEngine: EnginePtr const ref) ::=> Void := {
        engine := anEngine.

        assetLocations
            add: Stdn FileSystem resourcesDirectory / "woden-demo-assets";
            add: Stdn FileSystem resourcesDirectory / "game-assets".

        whiteTexture := self createTextureWithSolidColor: UInt8x4(255, 255, 255, 255) format: PixelFormat R8G8B8A8_UNorm.
        blackTexture := self createTextureWithSolidColor: UInt8x4(0, 0, 0, 255) format: PixelFormat R8G8B8A8_UNorm.
        transparentTexture := self createTextureWithSolidColor: UInt8x4 zeros format: PixelFormat R8G8B8A8_UNorm.
        neutralNormalTexture := self createTextureWithSolidColor: UInt8x4(128, 128, 255, 0) format: PixelFormat R8G8B8A8_UNorm.
        whiteTextureCube := self createTextureCubeWithSolidColor: UInt8x4(255, 255, 255, 255) format: PixelFormat R8G8B8A8_UNorm.
        checkboardTexture := self createCheckboardTexture.

        defaultMaterial := (MetallicRoughnessMaterial for: engine) upCastFor: Material.
        checkboardMaterial := {
            let material := MetallicRoughnessMaterial for: engine.
            material _ albedoTexture: checkboardTexture.
            material upCastFor: Material
        }.
    }.

    method solveResourceFileReference: (fileReference: Stdn FileReference const ref) withExtensions: (extensions: Char8 const arraySlice arraySlice) ::=> Stdn FileReference := {
        fileReference exists ifTrue: {
            return: fileReference
        }.

        assetLocations reverseDo: {:location :: Void |
            let fileRefWithParent := location resolve: fileReference.
            fileRefWithParent exists ifTrue: {
                return: fileRefWithParent
            }.

            extensions do: {:each :: Void |
                let candidate := fileRefWithParent withExtension: each.
                candidate exists ifTrue: {
                    return: candidate
                }.
            }.
        }.

        fileReference
    }.

    method solveTextureFileReference: (fileReference: Stdn FileReference const ref) ::=> Stdn FileReference := {
        let extensions mutable := Char8 const arraySlice array(
            "dds"
        ).

        self solveResourceFileReference: fileReference withExtensions: extensions
    }.

    method createTextureWithSolidColor: (color: UInt8x4) format: (format: PixelFormat) ::=> TextureWithViewPtr := {
        let device := engine _ renderingDevice.
        let texture := device _ createTexture: (TextureDescription()
            type: TextureType Texture2D;
            width: 1;
            height: 1;
            depth: 1;
            layers: 1;
            miplevels: 1;
            format: format;
            usageModes: TextureUsageMode CopyDestination | TextureUsageMode Sampled;
            mainUsageMode: TextureUsageMode Sampled;
            heapType: MemoryHeapType DeviceLocal;
            sampleCount: 1;
            yourself).

        let data mutable := color.
        texture _ uploadTextureLevel: 0 layer: 0 pitch: 4 slicePitch: 4 data: data address.
        texture _ asTextureWithView
    }.

    method createTextureCubeWithSolidColor: (color: UInt8x4) format: (format: PixelFormat) ::=> TextureWithViewPtr := {
        let device := engine _ renderingDevice.
        let texture := device _ createTexture: (TextureDescription()
            type: TextureType TextureCube;
            width: 1;
            height: 1;
            depth: 1;
            layers: 1;
            miplevels: 1;
            format: format;
            usageModes: TextureUsageMode CopyDestination | TextureUsageMode Sampled;
            mainUsageMode: TextureUsageMode Sampled;
            heapType: MemoryHeapType DeviceLocal;
            sampleCount: 1;
            yourself).

        let data mutable := color.
        0 until: 6 do: {:i :: Void |
            texture _ uploadTextureLevel: 0 layer: (i castTo: UIntPointer) pitch: 4 slicePitch: 4 data: data address.
        }.

        texture _ asTextureWithView
    }.

    method createCheckboardTexture => TextureWithViewPtr := {
        let device := engine _ renderingDevice.
        let texture := device _ createTexture: (TextureDescription()
            type: TextureType Texture2D;
            width: 64;
            height: 64;
            depth: 1;
            layers: 1;
            miplevels: 1;
            format: PixelFormat R8G8B8A8_UNormSRGB;
            usageModes: TextureUsageMode CopyDestination | TextureUsageMode Sampled;
            mainUsageMode: TextureUsageMode Sampled;
            heapType: MemoryHeapType DeviceLocal;
            sampleCount: 1;
            yourself).

        let data mutable type: (UInt8x4 array: 64*64).

        let destIndex mutable := 0.
        0 until: 64 do: {:y :: Void |
            let cellY := (y >> 4) & 1.
            0 until: 64 do: {:x :: Void |
                let cellX := (x >> 4) & 1.
                let isWhite := (cellX ^ cellY) anyMask: 1.
                data[destIndex] := isWhite ifTrue: UInt8x4(255, 255, 255, 255) ifFalse: UInt8x4(0, 0, 0, 255).
                destIndex := destIndex + 1.
            }
        }.

        texture _ uploadTextureLevel: 0 layer: 0 pitch: 4*64 slicePitch: 4*64*64 data: data address.
        texture _ asTextureWithView
    }.

    method loadColorTextureFrom: (fileReference: Stdn FileReference const ref) ::=> TextureWithViewPtr := {
        let solvedFileReference := self solveTextureFileReference: fileReference.
        solvedFileReference exists ifFalse: {return: checkboardTexture}.

        let textureData := TextureData readFromFileReference: solvedFileReference.
        textureData ifNil: {return: checkboardTexture}.

        let texture := engine _ renderingDevice _ createColorTextureWithData: textureData _.
        texture ifNil: {return: checkboardTexture}.

        texture _ asTextureWithView
    }.

    method loadColorCubeTextureFrom: (fileReference: Stdn FileReference const ref) ::=> TextureWithViewPtr := {
        let solvedFileReference := self solveTextureFileReference: fileReference.
        solvedFileReference exists ifFalse: {return: TextureWithViewPtr nil}.

        let textureData := TextureData readFromFileReference: solvedFileReference.
        textureData ifNil: {return: TextureWithViewPtr nil}.

        let texture := engine _ renderingDevice _ createColorTextureWithData: textureData _.
        texture ifNil: {return: TextureWithViewPtr nil}.

        texture _ asTextureWithView
    }.

    method loadTextureFrom: (fileReference: Stdn FileReference const ref) ::=> TextureWithViewPtr := {
        let solvedFileReference := self solveTextureFileReference: fileReference.
        solvedFileReference exists ifFalse: {return: TextureWithViewPtr nil}.

        let textureData := TextureData readFromFileReference: solvedFileReference.
        textureData ifNil: {return: TextureWithViewPtr nil}.

        let texture := engine _ renderingDevice _ createTextureWithData: textureData _.
        texture ifNil: {return: TextureWithViewPtr nil}.

        texture _ asTextureWithView
    }.
}.

compileTime constant ResourceCachePtr := ResourceCache sharedPointer.

}. ## End of namespace Runtime
}. ## End of namespace Woden
