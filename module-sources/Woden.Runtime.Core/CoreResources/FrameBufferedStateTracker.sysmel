namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Std Graphics Core.

#**
 * I keep track of a state tracker and its corresponding command list allocator in a per frame basis.
 *#
class FrameBufferedStateTracker superclass: FrameBufferedRenderingResource; definition: {
    public field allocators type: (CommandListAllocatorRef array: FrameBufferingCount).
    public field stateTrackers type: (StateTrackerRef array: FrameBufferingCount).

    method for: (anEngine: EnginePtr const ref) createOfType: (type: CommandListType) ::=> Void := {
        engine := anEngine.
        let device ref := engine _ renderingDevice.
        let commandQueue ref := device getDefaultCommandQueue.

        0 until: FrameBufferingCount do: {:i :: Void |
            allocators[i] := device createCommandAllocatorOfType: type for: commandQueue..
            stateTrackers[i] := device createDeferredStateTrackerOfType: type for: allocators[i].
            stateTrackers[i] close.
        }
    }.

    method currentAllocator => CommandListAllocatorRef const ref
        := allocators[self frameBufferingIndex].

    method current => StateTrackerRef const ref
        := stateTrackers[self frameBufferingIndex].

    method currentReseted => StateTrackerRef const ref := {
        let index := self frameBufferingIndex.

        allocators[index] resetAllocator.
        stateTrackers[index] resetFor: allocators[index].

        stateTrackers[index]
    }.
}.

}. ## End of namespace Runtime
}. ## End of namespace Woden
