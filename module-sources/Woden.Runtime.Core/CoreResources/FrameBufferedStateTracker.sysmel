namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Stdn Graphics Core.

#**
 * I keep track of a state tracker and its corresponding command list allocator in a per frame basis.
 *#
class FrameBufferedStateTracker superclass: FrameBufferedRenderingResource; definition: {
    public field allocators type: (CommandListAllocatorPtr array: FrameBufferingCount).
    public field stateTrackers type: (StateTrackerPtr array: FrameBufferingCount).

    method for: (anEngine: EnginePtr const ref) createOfType: (type: CommandListType) ::=> Void := {
        engine := anEngine.
        let device := engine _ renderingDevice.
        let commandQueue := device _ getDefaultCommandQueue.

        0 until: FrameBufferingCount do: {:i :: Void |
            allocators[i] := device _ createCommandAllocatorOfType: type for: commandQueue..
            stateTrackers[i] := device _ createDeferredStateTrackerOfType: type for: allocators[i].
            stateTrackers[i] _ close.
        }
    }.

    method currentAllocator => CommandListAllocatorPtr const ref
        := allocators[self frameBufferingIndex].

    method current => StateTrackerPtr const ref
        := stateTrackers[self frameBufferingIndex].

    method currentReseted => StateTrackerPtr const ref := {
        let index := self frameBufferingIndex.

        allocators[index] _ resetAllocator.
        stateTrackers[index] _ resetFor: allocators[index].

        stateTrackers[index]
    }.
}.

}. ## End of namespace Runtime
}. ## End of namespace Woden
