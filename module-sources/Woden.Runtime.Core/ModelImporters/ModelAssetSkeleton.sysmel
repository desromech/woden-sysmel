namespace Woden definition: {
namespace Assets definition: {

useNamespace: Woden Runtime.
useNamespace: Std Math Geometry.
useNamespace: Std Graphics Core.

#**
 * I represent a skeleton which is composed of a hierarchy of unions.
 *#
class ModelAssetSkeleton superclass: ModelAssetElement; definition: {
    public field inverseBindMatricesAccessor type: ModelAssetBinaryBufferAccessorRef.
    public field joints type: Std Collections Vector(ModelAssetNodeRef).
    public field rootJoint type: ModelAssetNodeRef.

    method cleanupSkeleton => Void := {
        joints do: {:each :: Void |
            each
                isJoint: true;
                hasValidJointIndex: false.
        }.

        let nextJointIndex mutable type: UIntPointer.
        rootJoint ifNotNil: {
            nextJointIndex := self cleanUpJoint: rootJoint parentIndex: -1 joinIndex: nextJointIndex.
        }.

        joints do: {:each :: Void |
            nextJointIndex := self cleanUpJoint: each parentIndex: -1 joinIndex: nextJointIndex.
        }.
        Std assert: nextJointIndex = joints size.
    }.

    method cleanUpJoint: (joint: ModelAssetNodeRef const ref) parentIndex: (parentIndex: IntPointer) joinIndex: (nextJointIndex: UIntPointer) ::=> UIntPointer := {
        joint hasValidJointIndex ifTrue: {return: nextJointIndex}.

        let childrenParentIndex mutable := parentIndex.
        let result mutable := nextJointIndex.

        joint isJoint ifTrue: {
            joint
                jointIndex: nextJointIndex;
                parentJointIndex: parentIndex;
                hasValidJointIndex: true.
            childrenParentIndex := nextJointIndex castTo: IntPointer.
            result := nextJointIndex + 1u
        }.

        joint children do: {:child :: Void |
            result := self cleanUpJoint: child parentIndex: childrenParentIndex joinIndex: result
        }.

        result
    }.

    field skeleton type: SkeletonRef.

    method permutateBoneIndex: (boneIndex: UInt32) ::=> UInt32
        := joints[boneIndex] jointIndex castTo: UInt32.

    method permutateBoneIndicesInVertices: (vertexData: ModelAssetBinaryBufferAccessorRef const ref) ::=> Void := {
        vertexData format selectCase: #{
        PixelFormat R32G32B32A32_UInt : {
            let data := vertexData asCPUBufferPointer reinterpretCastTo: UInt32 pointer.
            0 until: vertexData count * 4 do: {:i :: Void |
                data[i] := (self permutateBoneIndex: (data[i] castTo: UInt32)) castTo: UInt32
            }
        }.
        PixelFormat R32G32B32A32_SInt : {
            let data := vertexData asCPUBufferPointer reinterpretCastTo: Int32 pointer.
            0 until: vertexData count * 4 do: {:i :: Void |
                data[i] := (self permutateBoneIndex: (data[i] castTo: UInt32)) castTo: Int32
            }
        }.
        PixelFormat R16G16B16A16_UInt : {
            let data := vertexData asCPUBufferPointer reinterpretCastTo: UInt16 pointer.
            0 until: vertexData count * 4 do: {:i :: Void |
                data[i] := (self permutateBoneIndex: (data[i] castTo: UInt32)) castTo: UInt16
            }
        }.
        PixelFormat R16G16B16A16_SInt : {
            let data := vertexData asCPUBufferPointer reinterpretCastTo: Int16 pointer.
            0 until: vertexData count * 4 do: {:i :: Void |
                data[i] := (self permutateBoneIndex: (data[i] castTo: UInt32)) castTo: Int16
            }
        }.
        PixelFormat R8G8B8A8_UInt : {
            let data := vertexData asCPUBufferPointer reinterpretCastTo: UInt8 pointer.
            0 until: vertexData count * 4 do: {:i :: Void |
                data[i] := (self permutateBoneIndex: (data[i] castTo: UInt32)) castTo: UInt8
            }
        }.
        PixelFormat R8G8B8A8_SInt : {
            let data := vertexData asCPUBufferPointer reinterpretCastTo: Int8 pointer.
            0 until: vertexData count * 4 do: {:i :: Void |
                data[i] := (self permutateBoneIndex: (data[i] castTo: UInt32)) castTo: Int8
            }
        }.
        }.
    }.

    method asSkeleton => SkeletonRef := {
        skeleton ifNotNil: {return: skeleton}.

        let boneCount := joints size.
        let inverseBindMatrices := inverseBindMatricesAccessor decodeAsVectorOfFloat32x4x4.
        Std assert: inverseBindMatrices size = boneCount.

        skeleton := Skeleton new.
        skeleton bones resizeTo: boneCount.
        joints doWithIndex: {:joint :index :: Void |
            let jointSortedIndex := joint jointIndex.
            Std assert: jointSortedIndex < boneCount && joint hasValidJointIndex.

            skeleton bones[jointSortedIndex]
                parentIndex: joint parentJointIndex;
                inverseBindMatrix: inverseBindMatrices[index];
                restPose: (BonePose()
                    scale: joint scale;
                    rotation: joint rotationQuaternion;
                    translation: joint translation;
                    yourself)
        }.

        skeleton
    }.
}.

compileTime constant ModelAssetSkeletonRef := ModelAssetSkeleton rcRef.

} ## End of namespace Assets
} ## End of namespace Woden
