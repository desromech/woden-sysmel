namespace Woden definition: {
namespace Assets definition: {

useNamespace: Woden Runtime.

#**
 * I am an animation present in a model asset.
 *#
class ModelAssetAnimation superclass: ModelAssetElement; definition: {
    public field channels type: Std Collections Vector(ModelAssetAnimationChannelRef).
    public field samplers type: Std Collections Vector(ModelAssetAnimationSamplerRef).

    field convertedAnimation type: Animation rcRef.

    method asAnimation => Animation rcRef := {
        convertedAnimation ifNotNil: {return: convertedAnimation}.

        let multiChannelAnimation := MultiChannelAnimation new
            duration: 0;
            yourself.

        multiChannelAnimation curves reserve: samplers size.
        samplers do: {:each :: Void |
            let curve := each asAnimationCurve.
            multiChannelAnimation curves add: curve.
            multiChannelAnimation duration: (multiChannelAnimation duration max: curve endTime)
        }.

        multiChannelAnimation channels reserve: channels size.
        channels do: {:each :: Void |
            multiChannelAnimation channels add: each asAnimationChannel
        }.

        convertedAnimation := multiChannelAnimation upCastFor: Animation.
        convertedAnimation
    }.
}.

compileTime constant ModelAssetAnimationRef := ModelAssetAnimation rcRef.

} ## End of namespace Assets
} ## End of namespace Woden
