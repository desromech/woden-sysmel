namespace Woden definition: {
namespace Assets definition: {

useNamespace: Woden Runtime.
useNamespace: Std Graphics Core.


#**
 * I am a texture in a model asset.
 *#
class ModelAssetImage superclass: ModelAssetElement; definition: {
    public field uri type: Std String.
    public field mimeType type: Std String.
    public field bufferView type: ModelAssetBinaryBufferViewPtr.
    public field usageMode type: ModelAssetImageUsageMode.

    method asTextureDataFromFileFor: (engine: EnginePtr const ref) ::=> TextureDataRef := {
        uri ifEmpty: {return: TextureDataRef nil}.

        let fileReference := self modelAsset _ parentFolder resolve: uri asNativeFileReference.
        let solvedFileReference := engine _ resourceCache _ solveTextureFileReference: fileReference.
        TextureData readFromFileReference: solvedFileReference
    }.

    method asTextureDataFromBufferView ::=> TextureDataRef := {
        mimeType ifEmpty: {return: TextureDataRef nil}.

        let memoryStream mutable := bufferView _ asFixedMemoryStream.
        TextureData readFromStream: memoryStream withMimeType: mimeType asArraySlice.
    }.

    method asTextureDataFor: (engine: EnginePtr const ref) ::=> TextureDataRef := {
        let result := bufferView ifNil: {
            self asTextureDataFromFileFor: engine
        } ifNotNil: {
            self asTextureDataFromBufferView
        }.
        result ifNil: {return: TextureDataRef nil}.

        ## Do not generate mipmaps if there are already present, or if they cannot be generated.
        result hasGeneratedMipmaps || result hasOptimalTargetSpecificTiling ifTrue: {
            return: result
        }.

        let resultWithMiplevels := usageMode selectCase: #{
        ModelAssetImageUsageMode Color : {result generateColorMiplevels}.
        ModelAssetImageUsageMode Linear : {result generateMiplevels}.
        ModelAssetImageUsageMode Normal : {result generateNormalMiplevels}.
        }.
        resultWithMiplevels ifNil: {return: result}.
        resultWithMiplevels
    }.

    method asOfflineTextureDataFromFile ::=> TextureDataRef := {
        uri ifEmpty: {return: TextureDataRef nil}.

        TextureData readFromFileReference: uri asNativeFileReference
    }.

    method asOfflineTextureData ::=> TextureDataRef := {
        bufferView ifNil: {
            self asOfflineTextureDataFromFile
        } ifNotNil: {
            self asTextureDataFromBufferView
        }.
    }.
}.

compileTime constant ModelAssetImagePtr := ModelAssetImage rcPointer.

} ## End of namespace Assets
} ## End of namespace Woden
