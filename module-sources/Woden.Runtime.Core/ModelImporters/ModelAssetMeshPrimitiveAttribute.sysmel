namespace Woden definition: {
namespace Assets definition: {

useNamespace: Woden Runtime.
useNamespace: Std Graphics Core.
useNamespace: Std Math Geometry.

compileTime constant VertexAttributeLocation := Woden GpuStructures VertexAttributeLocation.


struct ModelAssetMeshPrimitiveAttribute definition: {
    public field accessor type: ModelAssetBinaryBufferAccessorPtr.
    public field location type: VertexAttributeLocation.

    const method = (o: SelfType const ref) ::=> Boolean8
        := accessor = o accessor && location = o location.

    const method hash ::=> Std Hash
        := accessor hash ^ location hash.

    const method < (o: SelfType const ref) ::=> Boolean8 := {
        accessor _ bufferView = o accessor _ bufferView ifTrue: {
            location < o location
        } ifFalse: {
            accessor _ bufferView getPointer < o accessor _ bufferView getPointer
        }
    }.
}.

compileTime constant ModelAssetMeshPrimitiveAttributeList := Std Collections Vector(ModelAssetMeshPrimitiveAttribute).

struct ModelAssetPrimitiveAttributesBindingDescription definition: {
    public field vertexLayout type: VertexLayoutPtr.
    public field buffers type: Std Collections Vector(BufferPtr).
    public field offsets type: Std Collections Vector(UInt32).

    const method = (o: SelfType const ref) ::=> Boolean8
        := vertexLayout = o vertexLayout && buffers = o buffers && offsets = o offsets.

    const method hash => Std Hash
        := vertexLayout hash ^ buffers hash ^ offsets hash.
}.

class ModelAssetMeshPrimitiveAttributes superclass: ModelAssetElement; definition: {
    public field attributes type: ModelAssetMeshPrimitiveAttributeList.

    method createAccessorFor: (vertexBufferView: ModelAssetBinaryBufferViewPtr const ref) offset: (offset: UIntPointer) format: (format: PixelFormat) location: (location: VertexAttributeLocation) ::=> Void := {
        let accessor := vertexBufferView _ modelAsset _ newBufferAccessor.
        accessor _
            bufferView: vertexBufferView;
            offset: offset;
            format: format.

        attributes add: (ModelAssetMeshPrimitiveAttribute()
            accessor: accessor;
            location: location;
            yourself)
    }.

    const method vertexCount => UIntPointer := {
        attributes ifEmpty: {return: 0}.
        attributes first accessor _ count.
    }.

    const method hasNormal => Boolean8 := {
        attributes do: {:each :: Void |
            each location = VertexAttributeLocation Normal ifTrue: {
                return: true
            }
        }.

        false
    }.

    const method hasTangent4 => Boolean8 := {
        attributes do: {:each :: Void |
            each location = VertexAttributeLocation Tangent4 ifTrue: {
                return: true
            }
        }.

        false
    }.

    method boundingBox => BoxF32 := {
        attributes do: {:each :: Void |
            each location = VertexAttributeLocation Position ifTrue: {
                return: each accessor _ boundingBox.
            }
        }.

        BoxF32 empty
    }.

    method boundingBox: (box: BoxF32 const ref) ::=> Void := {
        attributes do: {:each :: Void |
            each location = VertexAttributeLocation Position ifTrue: {
                each accessor _ boundingBox: box.
            }
        }.
    }.

    field convertedVertexBinding type: VertexBindingPtr.
    field convertedVertexAttributeMask type: UInt32.

    method asVertexBindingFor: (engine: EnginePtr const ref) ::=> VertexBindingPtr := {
        convertedVertexBinding ifNotNil: {
            return: convertedVertexBinding
        }.

        attributes sort.

        let vertexLayoutDescription mutable := VertexLayoutDescription().
        let lastBufferView mutable := ModelAssetBinaryBufferViewPtr nil.

        let bindingDescription mutable := ModelAssetPrimitiveAttributesBindingDescription().
        let lastBufferAccessorStartOffset mutable type: UIntPointer.
        let lastBufferStride mutable type: UIntPointer.

        convertedVertexAttributeMask := 0.
        attributes do: {:each :: Void |
            let newView := each accessor _ bufferView.
            let newViewStride := newView _ stride.
            let attributeStride := each accessor _ format bytesPerPixel.
            lastBufferView ~= newView || newViewStride = 0 ||
            ((each accessor _ offset - lastBufferAccessorStartOffset castTo: IntPointer) abs >= (lastBufferStride castTo: IntPointer)) ifTrue: {
                newViewStride = 0 ifTrue: {
                    vertexLayoutDescription addBufferWithStride: attributeStride
                } ifFalse: {
                    vertexLayoutDescription addBufferWithStride: (newViewStride castTo: UInt32)
                }.
                let newBufferStride := vertexLayoutDescription strides last.
                lastBufferStride := newBufferStride.
                lastBufferAccessorStartOffset := (each accessor _ offset / newBufferStride) * newBufferStride.

                bindingDescription buffers add: (newView _ asVertexBufferFor: engine).
                bindingDescription offsets add: (newView _ asGpuBufferOffset + lastBufferAccessorStartOffset castTo: UInt32).

                lastBufferView := newView.
            }.

            vertexLayoutDescription addAttribute: (VertexAttributeDescription()
                bufferIndex: (vertexLayoutDescription strides size - 1u castTo: UInt32);
                location: each location value;
                format: each accessor _ format;
                offset: (each accessor _ offset % vertexLayoutDescription strides last castTo: UInt32);
                yourself
            ).

            convertedVertexAttributeMask := convertedVertexAttributeMask | each location asAttributeBitMask
        }.

        bindingDescription vertexLayout: (engine _ renderingDevice _ getDefaultVertexLayoutCache _ getOrCreateWithDescription: vertexLayoutDescription).

        convertedVertexBinding := self modelAsset _ createVertexBindingFor: engine withDescription: bindingDescription.
    }.

    method vertexAttributeMask => UInt32
        := convertedVertexAttributeMask.

    method getAccessorWithLocation: (location: VertexAttributeLocation) ::=> ModelAssetBinaryBufferAccessorPtr := {
        attributes do: {:each :: Void |
            each location = location ifTrue: {
                return: each accessor
            }
        }.

        ModelAssetBinaryBufferAccessorPtr nil
    }.
}.

compileTime constant ModelAssetMeshPrimitiveAttributesPtr := ModelAssetMeshPrimitiveAttributes rcPointer.

Woden GpuStructures GenericVertex extend: {
    meta definition: {
        method modelAssetMeshPrimitiveAttributesFor: (vertexBufferView: ModelAssetBinaryBufferViewPtr const ref) ::=> ModelAssetMeshPrimitiveAttributesPtr := {
            let modelAsset := vertexBufferView _ modelAsset.
            let result mutable := modelAsset _ newMeshPrimitiveAttributes.

            result _
                createAccessorFor: vertexBufferView offset: positionData byteOffset format: PixelFormat R32G32B32_Float location: VertexAttributeLocation Position;
                createAccessorFor: vertexBufferView offset: normalData byteOffset format: PixelFormat R32G32B32_Float location: VertexAttributeLocation Normal;
                createAccessorFor: vertexBufferView offset: texcoord byteOffset format: PixelFormat R32G32_Float location: VertexAttributeLocation Texcoord0;
                createAccessorFor: vertexBufferView offset: tangent4 byteOffset format: PixelFormat R32G32B32A32_Float location: VertexAttributeLocation Tangent4;
                createAccessorFor: vertexBufferView offset: color byteOffset format: PixelFormat R32G32B32A32_Float location: VertexAttributeLocation Color0.

            result
        }.
    }.
}.

} ## End of namespace Assets
} ## End of namespace Woden
