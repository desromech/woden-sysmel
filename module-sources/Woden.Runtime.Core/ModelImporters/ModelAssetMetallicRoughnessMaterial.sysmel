namespace Woden definition: {
namespace Assets definition: {

useNamespace: Woden Runtime.
useNamespace: Stdn Graphics Core.

#**
 * I am a metallic-roughness material in a model asset.
 *#
class ModelAssetMetallicRoughnessMaterial superclass: ModelAssetMaterial; definition: {
    override method initialize => Void := {
        albedoFactor := Float32x4 white.
        metallicFactor := 0.0f.
        roughnessFactor := 0.55f.
    }.

    public field albedoFactor type: Float32x4.
    public field albedoTexture type: ModelAssetTexturePtr.

    public field metallicFactor type: Float32.
    public field roughnessFactor type: Float32.
    public field metallicRoughnessTexture type: ModelAssetTexturePtr.

    field renderingMaterial type: MaterialPtr.
    override method asRenderingMaterialFor: (engine: EnginePtr const ref) ::=> MaterialPtr := {
        renderingMaterial ifNotNil: {return: renderingMaterial}.

        let convertedMaterial := MetallicRoughnessMaterial for: engine.

        convertedMaterial _
            doubleSided: doubleSided.

        convertedMaterial _ state
            emission: Float32x4(emissionFactor, 1.0f);
            albedo: albedoFactor;
            alphaCutoff: alphaCutoff;
            metallicFactor: metallicFactor;
            roughnessFactor: roughnessFactor.

        albedoTexture ifNotNil: {
            convertedMaterial _ albedoTexture: (albedoTexture _ asSRGBTextureWithViewFor: engine)
        }.

        emissionTexture ifNotNil: {
            convertedMaterial _ emissionTexture: (emissionTexture _ asSRGBTextureWithViewFor: engine)
        }.

        normalTexture ifNotNil: {
            convertedMaterial _ normalTexture: (normalTexture _ asLinearTextureWithViewFor: engine)
        }.

        metallicRoughnessTexture ifNotNil: {
            convertedMaterial _ metallicRoughnessTexture: (metallicRoughnessTexture _ asLinearTextureWithViewFor: engine)
        }.

        occlusionTexture ifNotNil: {
            convertedMaterial _ occlusionTexture: (occlusionTexture _ asLinearTextureWithViewFor: engine)
        }.

        renderingMaterial := convertedMaterial upCastFor: Material
    }.
}.

compileTime constant ModelAssetMetallicRoughnessMaterialPtr := ModelAssetMetallicRoughnessMaterial sharedPointer.

} ## End of namespace Assets
} ## End of namespace Woden
