namespace Woden definition: {
namespace Assets definition: {

useNamespace: Woden Runtime.
useNamespace: Stdn Math.
useNamespace: Stdn Math LinearAlgebra.

#**
 * I am a scene node in a model asset.
 *#
class ModelAssetNode superclass: ModelAssetElement; definition: {
    public field children type: Stdn Collections Vector(ModelAssetNode sharedPointer).
    public field mesh type: ModelAssetMeshPtr.

    public field skeleton type: ModelAssetSkeleton sharedPointer.

    public field isJoint type: Boolean8.
    public field hasValidJointIndex type: Boolean8.
    public field jointIndex type: UIntPointer.
    public field parentJointIndex type: IntPointer.

    public field transform type: ReversibleAffineTransform3dF32.
    public field scale type: Float32x3.
    public field rotationQuaternion type: QuaternionF32.
    public field translation type: Float32x3.

    override method initialize => Void := {
        transform setIdentity.
        scale := 1.
        rotationQuaternion := QuaternionF32 identity.
    }.

    method asSceneNodeFor: (engine: EnginePtr const ref) ::=> SceneNodePtr := {
        let node := SpatialObjectNode sharedNew.
        mesh ifNotNil: {
            node _ renderable: (mesh _ asRenderableFor: engine).

            skeleton ifNotNil: {
                let skinnedMesh := SkinnedRenderableMesh sharedNew.
                skinnedMesh _
                    mesh: node _ renderable;
                    skeleton: (skeleton _ asSkeleton);
                    createRestPose.
                node _ renderable: (skinnedMesh upCastFor: Renderable)
            }
        }.

        node _ transform: transform.
        children do: {:each :: Void |
            let childNode := each _ asSceneNodeFor: engine.
            childNode ifNotNil: {
                node _ addChild: childNode.
            }
        }.

        node upCastFor: SceneNode.
    }.
}.

compileTime constant ModelAssetNodePtr := ModelAssetNode sharedPointer.

} ## End of namespace Assets
} ## End of namespace Woden
