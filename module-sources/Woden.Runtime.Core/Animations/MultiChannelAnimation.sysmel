namespace Woden definition: {
namespace Runtime definition: {

class MultiChannelAnimation superclass: Animation; definition: {
    public field duration type: Float32.
    public field curves type: Std Collections Vector(AnimationCurvePtr).
    public field channels type: Std Collections Vector(AnimationChannelPtr).

    override method newStateForTarget: (target: AnimationTargetPtr const ref) looped: (isLooped: Boolean8) ::=> AnimationState rcPointer := {
        let result := MultiChannelAnimationState rcNew.
        result _
            initializeWithAnimation: (self asRCPointer upCastFor: Animation) target: target;
            isLooped: isLooped.
        result _ curveStates resizeTo: curves size.
        result upCastFor: AnimationState
    }.

    override method update: (deltaTime: Float32) withState: (state: AnimationState ref) ::=> Boolean8 := {
        state currentTime >= duration ifTrue: {
            state isLooped ifTrue: {
                state currentTime := state currentTime - (state currentTime / duration) floor * duration.
            } ifFalse: {
                return: false
            }
        }.

        true
    }.

    override method applyInterpolationForRendering: (alpha: Float32) withState: (state: AnimationState ref) ::=> Void := {
        let interpolatedTime mutable := state previousTime interpolateTo: state currentNonLoopedTime at: alpha.

        interpolatedTime >= duration ifTrue: {
            state isLooped ifTrue: {
                interpolatedTime := state currentTime - (state currentTime / duration) floor * duration.
            } ifFalse: {
                interpolatedTime := duration
            }
        }.

        let multiChannelState ref := (state address reinterpretCastTo: MultiChannelAnimationState pointer) _.
        curves doWithIndex: {:eachCurve :index :: Void |
            multiChannelState curveStates[index] := eachCurve _ computeCurveStateAtTime: interpolatedTime
        }.

        channels do: {:each :: Void |
            each _ applyChannelState: multiChannelState atTime: interpolatedTime
        }.
    }.
}.

compileTime constant MultiChannelAnimationPtr := MultiChannelAnimation rcPointer.

} ## End of namespace Runtime
} ## End of namespace Woden
