namespace Woden definition: {
namespace Runtime definition: {

class AnimationState superclass: Std RCObject; definition: {
    field animation type: AnimationPtr.
    field target type: AnimationTargetPtr.

    const inline method animation => AnimationPtr const ref
        := animation.

    const inline method target => AnimationTargetPtr const ref
        := target.

    public field previousTime type: Float32.
    public field currentTime type: Float32.
    public field currentNonLoopedTime type: Float32. ## Non looped animation. Use for linear interpolation.
    public field isLooped type: Boolean8.

    virtual method initializeWithAnimation: (theAnimation: AnimationPtr) target: (theTarget: AnimationTargetPtr const ref)::=> Void := {
        animation := theAnimation.
        target := theTarget.
    }.

    #**
     * Ticks some amount of time in the animation.
     *#
    virtual method update: (deltaTime: Float32) ::=> Boolean8 := {
        animation ifNil: {return: false}.

        previousTime := currentTime.
        currentNonLoopedTime := currentTime := currentTime + deltaTime.
        animation _ update: deltaTime withState: self
    }.

    virtual method applyInterpolationForRendering: (alpha: Float32) ::=> Void := {
        animation ifNotNil: {
            animation _ applyInterpolationForRendering: alpha withState: self
        }.
    }.
}.

compileTime constant AnimationStatePtr := AnimationState rcPointer.

} ## End of namespace Runtime
} ## End of namespace Woden
