namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Stdn Graphics Core.
useNamespace: Stdn Math Geometry.

#**
 * I represent an abstract renderable, which is any 3D object that may be rendered by using a state tracker.
 *#
class Renderable superclass: Stdn SharedObject; definition: {
    public field renderPriority type: RenderPriority.
    public field depthWriteMask type: OverrideableBoolean.
    public field depthTestingEnabled type: OverrideableBoolean.
    public field localBoundingBox type: BoxF32.

    override method initialize => Void := {
        renderPriority := RenderPriority Default
    }.

    method withoutDepth => Void := {
        depthWriteMask := OverrideableBoolean False.
        depthTestingEnabled := OverrideableBoolean False.
    }.

    method withoutDepthWrite => Void := {
        depthWriteMask := OverrideableBoolean False.
    }.

    virtual method createRenderingSceneObjectsFor: (spatialObject: AbstractSpatialObjectPtr) ::=> Stdn Collections Vector(RenderingSceneObject sharedPointer) := {
        let renderingSceneObject := RenderingSceneRenderable sharedNew.
        renderingSceneObject _
            spatialObject: spatialObject;
            localBoundingBox: localBoundingBox;
            renderable: self asSharedPointer.

        let result mutable := RenderingSceneObjectList().
        result add: (renderingSceneObject upCastFor: RenderingSceneObject).
        result asMoveReference
    }.

    virtual method setObjectRenderContextFlags: (context: SceneRenderingContext ref) ::=> Void := {
        context
            objectDepthWriteMask: depthWriteMask;
            objectDepthTestingEnabled: depthTestingEnabled
    }.

    virtual method renderWithContext: (context: SceneRenderingContext ref) ::=> Void := {

    }.
}.

compileTime constant RenderablePtr := Renderable sharedPointer.

}. ## End of namespace Runtime
}. ## End of namespace Woden
