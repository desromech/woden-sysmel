namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Std Graphics Core.
useNamespace: Std Math Geometry.

#**
 * I represent an abstract renderable, which is any 3D object that may be rendered by using a state tracker.
 *#
class Renderable superclass: Std RCObject; definition: {
    public field renderPriority type: RenderPriority.
    public field depthWriteMask type: OverrideableBoolean.
    public field depthTestingEnabled type: OverrideableBoolean.
    public field localBoundingBox type: BoxF32.

    override method initialize => Void := {
        renderPriority := RenderPriority Default
    }.

    method withoutDepth => Void := {
        depthWriteMask := OverrideableBoolean False.
        depthTestingEnabled := OverrideableBoolean False.
    }.

    method withoutDepthWrite => Void := {
        depthWriteMask := OverrideableBoolean False.
    }.

    virtual method createRenderingSceneObjectsFor: (spatialObject: AbstractSpatialObjectRef) ::=> Std Collections Vector(RenderingSceneObject rcRef) := {
        let renderingSceneObject := RenderingSceneRenderable new
            spatialObject: spatialObject;
            localBoundingBox: localBoundingBox;
            renderable: self asRCRef;
            yourself.

        let result mutable := RenderingSceneObjectList().
        result add: (renderingSceneObject upCastFor: RenderingSceneObject).
        result asMoveReference
    }.

    virtual method setObjectRenderContextFlags: (context: SceneRenderingContext ref) ::=> Void := {
        context
            objectDepthWriteMask: depthWriteMask;
            objectDepthTestingEnabled: depthTestingEnabled
    }.

    virtual method renderWithContext: (context: SceneRenderingContext ref) ::=> Void := {

    }.

    virtual method getSkeletonPose => SkeletonPoseRef
        := SkeletonPoseRef nil.

    virtual method copy => Renderable rcRef
        := RenderableRef nil.

    virtual method setMaterial: (material: MaterialRef const ref) ::=> Void := {}.
}.

compileTime constant RenderableRef := Renderable rcRef.

}. ## End of namespace Runtime
}. ## End of namespace Woden
