namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Std Graphics Core.

#**
 * I am a mesh whose geometry data never changes in the GPU.
 *#
class GenericMeshRenderable superclass: ImmutableRenderable; definition: {
    public field submeshes type: Std Collections Vector(GenericSubmesh).

    override method renderWithContext: (context: SceneRenderingContext ref) ::=> Void := {
        submeshes ifEmpty: {return: void}.
        self setObjectRenderContextFlags: context.

        submeshes do: {:each :: Void |
            each renderWithContext: context
        }
    }.

    override method copy => Renderable rcPointer := {
        let result := GenericMeshRenderable rcNew.
        result _ := self.
        result upCastFor: Renderable
    }.

    override method setMaterial: (material: MaterialPtr const ref) ::=> Void := {
        submeshes do: {:each :: Void |
            each material: material.
        }.
    }.
}.

compileTime constant GenericMeshRenderablePtr := GenericMeshRenderable rcPointer.

}. ## End of namespace Runtime
}. ## End of namespace Woden
