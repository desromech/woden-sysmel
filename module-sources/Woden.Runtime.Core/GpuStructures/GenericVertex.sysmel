namespace Woden definition: {
namespace GpuStructures definition: {

useNamespace: Std Graphics Core.

struct GenericVertex definition: {
    public field positionData type: CompactFloat32x3; location: VertexAttributeLocation Position value.
    public field normalData type: CompactFloat32x3; location: VertexAttributeLocation Normal value.
    public field texcoord type: Float32x2; location: VertexAttributeLocation Texcoord0 value.
    public field tangent4 type: Float32x4; location: VertexAttributeLocation Tangent4 value.
    public field color type: Float32x4; location: VertexAttributeLocation Color0 value.

    const inline method position => Float32x3
        := positionData asFloat32x3.

    const inline method position: (newPosition: Float32x3) ::=> Void := {
        positionData set: newPosition
    }.

    const inline method normal => Float32x3
        := normalData asFloat32x3.

    const inline method normal: (newNormal: Float32x3) ::=> Void := {
        normalData set: newNormal
    }.

    meta definition: {
        method availableAttributeMask => UInt32 := {
            VertexAttributeLocation Position asAttributeBitMask |
            VertexAttributeLocation Normal asAttributeBitMask |
            VertexAttributeLocation Texcoord0 asAttributeBitMask |
            VertexAttributeLocation Tangent4 asAttributeBitMask |
            VertexAttributeLocation Color0 asAttributeBitMask
        }.

        method vertexLayoutDescription => VertexLayoutDescription := {
            VertexLayoutDescription()
                addAttribute: (VertexAttributeDescription()
                    location: positionData location;
                    format: PixelFormat R32G32B32_Float;
                    offset: positionData byteOffset;
                    yourself);
                addAttribute: (VertexAttributeDescription()
                    location: normalData location;
                    format: PixelFormat R32G32B32_Float;
                    offset: normalData byteOffset;
                    yourself);
                addAttribute: (VertexAttributeDescription()
                    location: texcoord location;
                    format: PixelFormat R32G32_Float;
                    offset: texcoord byteOffset;
                    yourself);
                addAttribute: (VertexAttributeDescription()
                    location: tangent4 location;
                    format: PixelFormat R32G32B32A32_Float;
                    offset: tangent4 byteOffset;
                    yourself);
                addAttribute: (VertexAttributeDescription()
                    location: color location;
                    format: PixelFormat R32G32B32A32_Float;
                    offset: color byteOffset;
                    yourself);
                addBufferWithStride: GenericVertex instanceSize;
                yourself.
        }.
    }.
}.

}. ## End of namespace GpuStructures
}. ## End of namespace Woden
