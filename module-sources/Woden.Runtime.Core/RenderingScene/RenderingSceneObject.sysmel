namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Std Math Geometry.
useNamespace: Std Math LinearAlgebra.

#**
 * I represent a 3D scene with rendering only objects.
 *#
class RenderingSceneObject superclass: Std RCObject; definition: {
    public field spatialObject type: AbstractSpatialObjectPtr.
    public field localBoundingBox type: BoxF32.
    public field boundingBox type: BoxF32.
    public field node type: SceneNode weakRCPointer.
    protected field transformValidated type: Boolean8.

    const virtual method isLightSource => Boolean8
        := false.

    const virtual method isRenderable => Boolean8
        := false.

    method transformChanged => Void := {
        transformValidated := false.
    }.

    method markTransformValidated => Void := {
        transformValidated := true.
    }.

    method validateGlobalTransform => Void := {
        transformValidated ifFalse: {
            node lock ifNotNil: {:n :: Void |
                n _ validateRenderingSceneObjectTransform: self.
                self markTransformValidated
            }.
        }.
    }.

    const inline method currentTransform => ReversibleAffineTransform3dF32
        := spatialObject _ currentTransform.

    const virtual method isVisibleInFrustum: (frustum: FrustumF32 const ref) ::=> Boolean8
        := frustum intersectsOrContainsBox: boundingBox.
}.

compileTime constant RenderingSceneObjectIterationBlock := ((RenderingSceneObject rcPointer const ref) => Void) nativeBlockClosure.
compileTime constant RenderingSceneObjectList := Std Collections Vector(RenderingSceneObject rcPointer).

compileTime constant RenderingSceneObjectPtr := RenderingSceneObject rcPointer.

}. ## End of namespace Runtime
}. ## End of namespace Woden
