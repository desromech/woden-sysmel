namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Std Math Geometry.
useNamespace: Std Math LinearAlgebra.

#**
 * I represent a 3D scene with rendering only objects.
 *#
class RenderingSceneObject superclass: Std RCObject; definition: {
    public field spatialObject type: AbstractSpatialObjectRef.
    public field localBoundingBox type: BoxF32.
    public field boundingBox type: BoxF32.
    public field node type: SceneNode weakRCRef.
    protected field transformValidated type: Boolean8.

    const virtual method isLightSource => Boolean8
        := false.

    const virtual method isRenderable => Boolean8
        := false.

    method transformChanged => Void := {
        transformValidated := false.
    }.

    method markTransformValidated => Void := {
        transformValidated := true.
    }.

    method validateGlobalTransform => Void := {
        transformValidated ifFalse: {
            node ifNotNil: {:n :: Void |
                n validateRenderingSceneObjectTransform: self.
                self markTransformValidated
            }.
        }.
    }.

    const inline method currentTransform => ReversibleAffineTransform3dF32
        := spatialObject currentTransform.

    const virtual method isVisibleInFrustum: (frustum: FrustumF32 const ref) ::=> Boolean8
        := frustum intersectsOrContainsBox: boundingBox.
}.

compileTime constant RenderingSceneObjectIterationBlock := ((RenderingSceneObject rcRef const ref) => Void) nativeBlockClosure.
compileTime constant RenderingSceneObjectList := Std Collections Vector(RenderingSceneObject rcRef).

compileTime constant RenderingSceneObjectRef := RenderingSceneObject rcRef.

}. ## End of namespace Runtime
}. ## End of namespace Woden
