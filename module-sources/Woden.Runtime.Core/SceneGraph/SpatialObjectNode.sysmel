namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Std Math Geometry.

#**
 * I am a node that poses a local coordinate system that is associated with a model matrix in the GPU.
 *#
class SpatialObjectNode superclass: AbstractSpatialObjectNode; definition: {
    protected field objectState type: SpatialObjectRef.
    protected field renderable type: RenderableRef.
    protected field renderingSceneObjects type: RenderingSceneObjectList.

    override method initialize => Void := {
        objectState := SpatialObject new.
    }.

    override method localInternalBoundingBox => BoxF32
        := renderable
            ifNil: {BoxF32 empty}
            ifNotNil: {renderable localBoundingBox}.

    override const method getSkeletonPose => SkeletonPoseRef := {
        renderable ifNil: {return: SkeletonPoseRef nil}.

        renderable getSkeletonPose
    }.

    method visible => Boolean8
        := objectState visible.

    method visible: (aBoolean8: Boolean8) ::=> Void := {
        objectState visible: aBoolean8
    }.

    method color => Float32x4
        := objectState color.

    method color: (aColor: Float32x4) ::=> Void := {
        objectState color: aColor
    }.

    const inline method renderable => RenderableRef const ref
        := renderable.

    inline method renderable: (theRenderable: RenderableRef const ref) ::=> Void := {
        let layer := self sceneLayer.
        layer ifNotNil: {
            self removeRenderingSceneObjectsFromSceneLayer: layer.
            renderingSceneObjects removeAll
        }.

        renderable := theRenderable.

        renderable ifNotNil: {
            renderingSceneObjects := renderable createRenderingSceneObjectsFor: (objectState upCastFor: AbstractSpatialObject).
            renderingSceneObjects do: {:each :: Void |
                each node: (self asWeakRCRef upCastFor: SceneNode)
            }.

            layer ifNotNil: {
                self addRenderingSceneObjectsToSceneLayer: layer.
            }.
        }.
    }.

    const override method isSpatialObjectNode => Boolean8
        := true.

    override method globalTransformChanged => Void := {
        super globalTransformChanged.
        renderingSceneObjects do: {:each :: Void |
            each transformChanged.
        }.
    }.

    override method addRenderingSceneObjectsToSceneLayer: (sceneLayer: SceneLayerRef const ref) ::=> Void := {
        sceneLayer addRenderingSceneObjects: renderingSceneObjects.
    }.

    override method removeRenderingSceneObjectsFromSceneLayer: (sceneLayer: SceneLayerRef const ref) ::=> Void := {
        sceneLayer removeRenderingSceneObjects: renderingSceneObjects.
    }.
}.

compileTime constant SpatialObjectNodeRef := SpatialObjectNode rcRef.

}. ## End of namespace Runtime
}. ## End of namespace Woden
