namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Std Math LinearAlgebra.

#**
 * I represent a 3D scene with rendering only objects.
 *#
class Scene superclass: Std RCObject; definition: {
    public field engine type: EngineRef.

    public field backgroundColor type: Float32x4.
    public field groundLighting type: Float32x3.
    public field skyLighting type: Float32x3.
    public field sunDirection type: Float32x3.

    public field currentTime type: Float32.

    field backgroundLayer type: SceneLayerRef.
    field normalLayer type: SceneLayerRef.
    field foregroundLayer type: SceneLayerRef.

    const inline method backgroundLayer => SceneLayerRef const ref
        := backgroundLayer.

    const inline method normalLayer => SceneLayerRef const ref
        := normalLayer.

    const inline method foregroundLayer => SceneLayerRef const ref
        := foregroundLayer.

    method backgroundLighting: (lighting: Float32x3) ::=> Void := {
        groundLighting := lighting.
        skyLighting := lighting.
    }.

    override method initialize => Void := {
        backgroundColor := Float32x4 black.
        skyLighting := 0.1f.
        groundLighting := 0.05f.
        sunDirection := Float32x3(0, 1, 0).

        backgroundLayer := SceneLayer new.
        normalLayer := SceneLayer new.
        foregroundLayer := SceneLayer new.

        backgroundLayer addedToScene: self asRCRef.
        normalLayer addedToScene: self asRCRef.
        foregroundLayer addedToScene: self asRCRef.
    }.

    method add: (node: SceneNodeRef const ref) ::=> Void := {
        normalLayer addChild: node
    }.

    method allNodesDo: (aBlock: SceneNodeIterationBlock) ::=> Void := {
        backgroundLayer withAllNodesDo: aBlock.
        normalLayer withAllNodesDo: aBlock.
        foregroundLayer withAllNodesDo: aBlock.
    }.

    method sceneLayersDo: (aBlock: SceneLayerIterationBlock) ::=> Void := {
        aBlock(backgroundLayer).
        aBlock(normalLayer).
        aBlock(foregroundLayer).
    }.
}.

compileTime constant SceneRef := Scene rcRef.
compileTime constant SceneWeakRef := Scene weakRCRef.

}. ## End of namespace Runtime
}. ## End of namespace Woden
