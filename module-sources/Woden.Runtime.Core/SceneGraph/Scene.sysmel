namespace Woden definition: {
namespace Runtime definition: {

useNamespace: Stdn Math LinearAlgebra.

#**
 * I represent a 3D scene with rendering only objects.
 *#
class Scene superclass: Stdn SharedObject; definition: {
    public field engine type: EnginePtr.

    public field backgroundColor type: Float32x4.
    public field groundLighting type: Float32x4.
    public field skyLighting type: Float32x4.
    public field sunDirection type: Float32x3.

    public field currentTime type: Float32.

    field backgroundLayer type: SceneLayerPtr.
    field normalLayer type: SceneLayerPtr.
    field foregroundLayer type: SceneLayerPtr.

    const inline method backgroundLayer => SceneLayerPtr const ref
        := backgroundLayer.

    const inline method normalLayer => SceneLayerPtr const ref
        := normalLayer.

    const inline method foregroundLayer => SceneLayerPtr const ref
        := foregroundLayer.

    override method initialize => Void := {
        backgroundColor := Float32x4 black.
        skyLighting := 0.1f.
        groundLighting := 0.05f.
        sunDirection := Float32x3(0, 1, 0).

        backgroundLayer := SceneLayer sharedNew.
        normalLayer := SceneLayer sharedNew.
        foregroundLayer := SceneLayer sharedNew.

        backgroundLayer _ addedToScene: self asSharedPointer.
        normalLayer _ addedToScene: self asSharedPointer.
        foregroundLayer _ addedToScene: self asSharedPointer.
    }.

    method add: (node: SceneNodePtr const ref) ::=> Void := {
        normalLayer _ addChild: node
    }.

    method allNodesDo: (aBlock: SceneNodeIterationBlock) ::=> Void := {
        backgroundLayer _ withAllNodesDo: aBlock.
        normalLayer _ withAllNodesDo: aBlock.
        foregroundLayer _ withAllNodesDo: aBlock.
    }.

    method sceneLayersDo: (aBlock: SceneLayerIterationBlock) ::=> Void := {
        aBlock(backgroundLayer _).
        aBlock(normalLayer _).
        aBlock(foregroundLayer _).
    }.
}.

compileTime constant ScenePtr := Scene sharedPointer.
compileTime constant SceneWeakPtr := Scene weakPointer.

}. ## End of namespace Runtime
}. ## End of namespace Woden
