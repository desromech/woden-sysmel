namespace Woden definition: {
namespace Runtime definition: {

#**
 * I represent a light source in the Woden scene graph.
 *#
class LightSourceNode superclass: AbstractSpatialObjectNode; definition: {
	compileTime constant DefaultSpotOuterCutoff := 45.0f.
	compileTime constant DefaultSpotInnerCutoff := 40.0f.

    private field objectState type: LightSourceObjectPtr.
    private field renderingSceneObject type: RenderingSceneLightSourcePtr.
    private field spotOuterCutoff type: Float32.
    private field spotInnerCutoff type: Float32.
    private field spotExponent type: Float32.

    override method initialize => Void := {
        spotOuterCutoff := DefaultSpotOuterCutoff.
        spotInnerCutoff := DefaultSpotInnerCutoff.
        spotExponent := 1.0f.

        objectState := LightSourceObject rcNew.
        objectState _ lightState
            intensity: Float32x4 ones;
            shadowNormalBias: 0.05f;
            innerCosCutoff: -1;
            outerCosCutoff: -1;
            spotExponent: 1;
            radius: 3.0f.
        objectState _ spotOuterCutoff: spotOuterCutoff.

        renderingSceneObject := RenderingSceneLightSource rcNew.
        renderingSceneObject _
            spatialObject: (objectState upCastFor: AbstractSpatialObject);
            node: (self asWeakRCPointer upCastFor: SceneNode);
            volumeChanged
    }.

    method makePoint => Void := {
        objectState _ lightType: LightSourceType Point.
        objectState _ lightState
            innerCosCutoff: -1;
            outerCosCutoff: -1;
            spotExponent: 1.
        renderingSceneObject _ volumeChanged.
    }.

    method makeDirectional => Void := {
        objectState _ lightType: LightSourceType Directional.
        objectState _ lightState
            innerCosCutoff: -1;
            outerCosCutoff: -1;
            spotExponent: 1.
        renderingSceneObject _ volumeChanged.
    }.

    method makeSpot => Void := {
        objectState _ lightType: LightSourceType Spot.
        objectState _ lightState
            innerCosCutoff: spotInnerCutoff degreesToRadians cos;
            outerCosCutoff: spotOuterCutoff degreesToRadians cos;
            spotExponent: spotExponent.
        renderingSceneObject _ volumeChanged.
    }.

    method spotExponent ::=> Float32
        := spotExponent.

    method spotExponent: (exponent: Float32) ::=> Void := {
        spotExponent := exponent.
        objectState _ lightState
            spotExponent: exponent.
    }.

    method spotOuterCutoff ::=> Float32
        := spotOuterCutoff.

    method spotOuterCutoff: (cutoffAngle: Float32) ::=> Void := {
        spotOuterCutoff := cutoffAngle.
        objectState _ spotOuterCutoff: spotOuterCutoff.
        objectState _ lightState outerCosCutoff: spotOuterCutoff degreesToRadians cos.
    }.

    method spotInnerCutoff ::=> Float32
        := spotInnerCutoff.

    method spotInnerCutoff: (cutoffAngle: Float32) ::=> Void := {
        spotInnerCutoff := cutoffAngle.
        objectState _ lightState innerCosCutoff: spotInnerCutoff degreesToRadians cos.
    }.

    method radius ::=> Float32
        := objectState _ lightState radius.

    method radius: (radius: Float32) ::=> Void := {
        objectState _ lightState radius: radius.
        renderingSceneObject _ volumeChanged.
    }.

    method intensity ::=> Float32x3
        := objectState _ lightState intensity xyz.

    method intensity: (intensity: Float32x3) ::=> Void := {
        objectState _ lightState intensity: Float32x4(intensity, 0.0f)
    }.

    const override method isLightSourceNode => Boolean8
        := true.

    method shadowNormalBias ::=> Float32
        := objectState _ lightState shadowNormalBias.

    method shadowNormalBias: (shadowNormalBias: Float32) ::=> Void := {
        objectState _ lightState shadowNormalBias: shadowNormalBias.
    }.

    method castShadows ::=> Boolean8
        := objectState _ castShadows.

    method castShadows: (value: Boolean8) ::=> Void := {
        objectState _ castShadows: value
    }.

    override method globalTransformChanged => Void := {
        super globalTransformChanged.
        renderingSceneObject _ transformChanged.
    }.

    override method addRenderingSceneObjectsToSceneLayer: (sceneLayer: SceneLayer ref) ::=> Void := {
        sceneLayer addRenderingSceneObject: (renderingSceneObject upCastFor: RenderingSceneObject).
    }.

    override method removeRenderingSceneObjectsFromSceneLayer: (sceneLayer: SceneLayer ref) ::=> Void := {
        sceneLayer removeRenderingSceneObject: (renderingSceneObject upCastFor: RenderingSceneObject).
    }.
}.

compileTime constant LightSourceNodePtr := LightSourceNode rcPointer.

}. ## End of namespace Runtime
}. ## End of namespace Woden
