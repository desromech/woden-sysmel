Compiler importModulesNamed: #(
    #"Woden.Application.SampleScene"
    #"Woden.Application.Headfull"
).

useNamespace: Woden Runtime.

function constructPoolScene(scene: SceneRef const ref) => Void := {
    let engine := scene engine.

    ## Build the floor.
    {
        let tileMaterial := engine resourceCache loadMaterialFrom: "materials/artificial/floor/tile" asNativeFileReference.
        let concreteMaterial := engine resourceCache loadMaterialFrom: "materials/artificial/concrete/concrete" asNativeFileReference.

        let borderThickness := 0.5f.
        let renderable := engine newGenericMeshBuilder
            currentMaterial: (tileMaterial upCastFor: Material);

            translateToX: borderThickness*0.5f negated - 4.5f y: -1.0f z: 0.0f;
    		addCubeWithWidth: 4.0f - borderThickness height: 2.0f depth: 5.0f + borderThickness*2.0f;
    		addCollisionBox;

    		translateToX: borderThickness*0.5f+ 4.5f y: -1.0f z: 0.0f;
    		addCubeWithWidth: 4.0f - borderThickness height: 2.0f depth: 5.0f + borderThickness*2.0f;
    		addCollisionBox;

    		translateToX: 0.0f y: -1.0f z: borderThickness + 4.5f;
    		addCubeWithWidth: 13.0f height: 2.0f depth: 4.0f;
    		addCollisionBox;

    		translateToX: 0.0f y: -1.0f z: borderThickness negated - 4.5f;
    		addCubeWithWidth: 13.0f height: 2.0f depth: 4.0f;
    		addCollisionBox;

    		## Bottom
    		currentMaterial: (concreteMaterial upCastFor: Material);
    		translateToX: 0.0f y: -2.05f z: 0.0f;
    		addCubeWithWidth: 5.0f height: 0.1f depth: 5.0f;
    		addCollisionBox;

    		## Border
    		translateToX: -2.5f - (borderThickness*0.5f) y: -1.0f z: 0.0f;
    		addCubeWithWidth: borderThickness height: 2.2f depth: 5.0f + borderThickness*2.0f;
    		addCollisionBox;

    		translateToX: 2.5f + (borderThickness*0.5f) y: -1.0 z: 0.0f;
    		addCubeWithWidth: borderThickness height: 2.2f depth: 5.0f + borderThickness*2.0f;
    		addCollisionBox;

    		translateToX: 0.0f y: -1.0f z: 2.5f + borderThickness*0.5f;
    		addCubeWithWidth: 5.0f height: 2.2f depth: borderThickness;
    		addCollisionBox;

    		translateToX: 0.0f y: -1.0f z: -2.5f - borderThickness*0.5f;
    		addCubeWithWidth: 5.0f height: 2.2f depth: borderThickness;
    		addCollisionBox;

    		## Use a planar texture coordinates
    		planeTextureScale: 2.0f offset: 0.0f;
    		computeTangentsBitangents;
            
            renderable.

        scene add: (SpatialObjectNode new
            renderable: renderable;
            upCastFor: SceneNode).
    }.

    ## Build and add the cube to the scene.
    scene add: (SpatialObjectNode new
        renderable: (engine newGenericMeshBuilder
            addCubeWithWidth: 1.0 height: 1.0 depth: 1.0;
            renderable);
        translateByX: 4.5f y: 0.5f z: 0.0f;
        upCastFor: SceneNode).

    ## Build the water.
    {
        let renderable := WaterRenderableMesh new
            mesh: (engine newGenericMeshBuilder
                currentMaterial: (engine resourceCache loadMaterialFrom: "materials/water/pool/pool" asNativeFileReference);
                addGridWithSize: 5.0f subdivisions: 64;
                renderable);
                yourself.
        renderable mesh withoutDepthWrite.
        renderable waterDeformationState
            harmonicAt: 0 amplitude: 0.05f frequency: -1.1f center: Float32x2(0, 0);
            harmonicAt: 1 amplitude: 0.02f frequency: 2.23f direction: Float32x2(0, -1);
            harmonicAt: 2 amplitude: 0.015f frequency: 1.80f direction: Float32x2(1, 0).

        scene add: (SpatialObjectNode new
            renderable: (renderable upCastFor: Renderable);
            translateToX: 0.0f y: -0.3 z: 0.0f;
            upCastFor: SceneNode).
    }.

    ## Add the sun light.
    scene add: (LightSourceNode new
        makeDirectional;
        castShadows: true;
        intensity: Float32x3(1f, 1f, 0.8f) normalized * 0.7f;
        rotateDegreesOnX: -35;
        rotateDegreesOnY: -5;
        translateToX: 0.0f y: 1.5f z: 2.5f;
        upCastFor: SceneNode).

    ## Add the sky box.
    {
        let renderable := engine newGenericMeshBuilder
            currentMaterial: ((engine resourceCache loadMaterialFrom: "materials/sky/sample/daylight" asNativeFileReference));
            addInteriorCubeWithWidth: 1.0 height: 1.0 depth: 1.0;
            renderable.
        renderable withoutDepth.

        scene backgroundLayer addChild: (SpatialObjectNode new
            renderable: renderable;
            upCastFor: SceneNode).
    }.

}.

Woden Application sampleSceneMainWith: {:model :: Void |
    let scene := model newScene.
    let engine := model engine.

    constructPoolScene(scene).

    ## Setup the camera
    model mainCamera
        toneMappingOperator: ToneMappingOperator Filmic;
        withAutoExposure;
        translateByX: 0.0f y: 1.0f z: -4.0f.
}.
